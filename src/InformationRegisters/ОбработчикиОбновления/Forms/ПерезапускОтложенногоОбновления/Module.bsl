///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2022, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Заголовок = "Перезапуск отложенного обновления";
КонецПроцедуры

&НаКлиенте
Процедура ГиперссылкаВыборОбработчиковОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
КонецПроцедуры

&НаКлиенте
Процедура ГиперссылкаВыборОбработчиковНажатие(Элемент)
	ОбработкаЗавершения = Новый ОписаниеОповещения("ПослеВыбораОбработчиков", ЭтотОбъект);

	ПараметрыВыбора = Новый Структура;
	ПараметрыВыбора.Вставить("ВыбранныеОбработчики", ВыбранныеОбработчики);
	ОткрытьФорму("РегистрСведений.ОбработчикиОбновления.Форма.ФормаВыбора", ПараметрыВыбора,,,,, ОбработкаЗавершения);
КонецПроцедуры

&НаКлиенте
Процедура Перезапустить(Команда)
	Элементы.ФормаПерезапустить.Доступность = Ложь;

	ДлительнаяОперация = ДлительнаяОперация();

	ОповещениеОЗавершении	= Новый ОписаниеОповещения("ОбработатьРезультат", ЭтотОбъект);
	ПараметрыОжидания		= БазоваяПодсистемаКлиент.ДО_ПараметрыОжидания(ЭтотОбъект);
	БазоваяПодсистемаКлиент.ДО_ОжидатьЗавершение(ДлительнаяОперация, ОповещениеОЗавершении, ПараметрыОжидания);
КонецПроцедуры

&НаКлиенте
Процедура ПослеВыбораОбработчиков(Результат, ДополнительныеПараметры) Экспорт
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;

	ВыбранныеОбработчики.ЗагрузитьЗначения(Результат);

	Если ВыбранныеОбработчики.Количество() = 0 Тогда
		Элементы.ГиперссылкаВыборОбработчиков.Заголовок = "Выбрать обработчики";
	Иначе
		Элементы.ГиперссылкаВыборОбработчиков.Заголовок = СтрШаблон("Выбрано обработчиков: %1", ВыбранныеОбработчики.Количество());
	КонецЕсли;
КонецПроцедуры

&НаСервере
Функция ДлительнаяОперация()
	ПараметрыВыполнения						= БазоваяПодсистемаСервер.ДО_ПараметрыВыполненияВФоне(УникальныйИдентификатор);
	ПараметрыВыполнения.ОжидатьЗавершение	= 0;

	Результат = БазоваяПодсистемаСервер.ДО_ВыполнитьПроцедуру(ПараметрыВыполнения, "ОбновлениеВерсииИБСервер.ОИБ_ПерезапуститьОтложенноеОбновление", ВыбранныеОбработчики.ВыгрузитьЗначения());

	Возврат Результат;
КонецФункции

&НаКлиенте
Процедура ОбработатьРезультат(Результат, ДополнительныеПараметры) Экспорт
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;

	Оповестить("ОтложенноеОбновлениеПерезапущено");
	Закрыть();
КонецПроцедуры
