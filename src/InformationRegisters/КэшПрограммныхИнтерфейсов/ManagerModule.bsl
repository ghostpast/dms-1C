///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2021, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

Функция ВнутренняяWSПрокси(Параметры) Экспорт
	АдресWSDL						= Параметры.АдресWSDL;
	URIПространстваИмен				= Параметры.URIПространстваИмен;
	ИмяСервиса						= Параметры.ИмяСервиса;
	ИмяТочкиПодключения				= Параметры.ИмяТочкиПодключения;
	ИмяПользователя					= Параметры.ИмяПользователя;
	Пароль							= Параметры.Пароль;
	Таймаут							= Параметры.Таймаут;
	Местоположение					= Параметры.Местоположение;
	ИспользоватьАутентификациюОС	= Параметры.ИспользоватьАутентификациюОС;
	ЗащищенноеСоединение			= Параметры.ЗащищенноеСоединение;

	Протокол	= "";
	Позиция		= СтрНайти(АдресWSDL, "://");
	Если Позиция > 0 Тогда
		Протокол	= НРег(Лев(АдресWSDL, Позиция - 1));
	КонецЕсли;

	Если (Протокол = "https" Или Протокол = "ftps") И ЗащищенноеСоединение = Неопределено Тогда
		ЗащищенноеСоединение = БазоваяПодсистемаКлиентСервер.ОН_НовоеЗащищенноеСоединение();
	КонецЕсли;

	WSОпределения = WSОпределения(АдресWSDL, ИмяПользователя, Пароль,, ЗащищенноеСоединение);

	Если ПустаяСтрока(ИмяТочкиПодключения) Тогда
		ИмяТочкиПодключения = ИмяСервиса + "Soap";
	КонецЕсли;

	ИнтернетПрокси = Неопределено;
	// Зарезервировано для новых подсистем

	Прокси = Новый WSПрокси(WSОпределения, URIПространстваИмен, ИмяСервиса, ИмяТочкиПодключения, ИнтернетПрокси, Таймаут, ЗащищенноеСоединение, Местоположение, ИспользоватьАутентификациюОС);

	Прокси.Пользователь = ИмяПользователя;
	Прокси.Пароль       = Пароль;

	Возврат Прокси;
КонецФункции

Функция WSОпределения(Знач АдресWSDL, Знач ИмяПользователя, Знач Пароль, Знач Таймаут = 10, Знач ЗащищенноеСоединение = Неопределено)
	// Зарезервировано для новых подсистем
	Попытка
		ИнтернетПрокси = Неопределено; // По умолчанию.
		Определения = Новый WSОпределения(АдресWSDL, ИмяПользователя, Пароль, ИнтернетПрокси, Таймаут, ЗащищенноеСоединение);
	Исключение
		ТекстОшибки = СтрШаблон("Не удалось получить WS-определения по адресу
				           |%1
				           |по причине:
				           |%2",
				АдресWSDL,
		КраткоеПредставлениеОшибки(ИнформацияОбОшибке()));

		// Зарезервировано для новых подсистем

		ВызватьИсключение ТекстОшибки;
	КонецПопытки;

	Возврат Определения;
КонецФункции

Функция ПодготовитьДанныеКэшаВерсий(Знач ТипДанных, Знач ПараметрыПолучения) Экспорт
	Если ТипДанных = Перечисления.ТипыДанныхКэшаПрограммныхИнтерфейсов.ВерсииИнтерфейса Тогда
		Данные			= ПолучитьВерсииИнтерфейсаВКэш(ПараметрыПолучения[0], ПараметрыПолучения[1]);
	ИначеЕсли ТипДанных = Перечисления.ТипыДанныхКэшаПрограммныхИнтерфейсов.ОписаниеWebСервиса Тогда
		Данные			= ПолучитьWSDL(ПараметрыПолучения[0], ПараметрыПолучения[1], ПараметрыПолучения[2], ПараметрыПолучения[3], ПараметрыПолучения[4]);
	Иначе
		ШаблонТекста	= "Неизвестный тип данных кэша версий: %1";
		ТекстСообщения	= СтрШаблон(ШаблонТекста, ТипДанных);

		ВызватьИсключение(ТекстСообщения);
	КонецЕсли;

	Возврат Данные;
КонецФункции

Функция ПолучитьВерсииИнтерфейсаВКэш(Знач ПараметрыПодключения, Знач ИмяИнтерфейса)
	Если Не ПараметрыПодключения.Свойство("URL") Или Не ЗначениеЗаполнено(ПараметрыПодключения.URL) Тогда
		ВызватьИсключение("Не задан URL сервиса.");
	КонецЕсли;

	Если ПараметрыПодключения.Свойство("UserName") И ЗначениеЗаполнено(ПараметрыПодключения.UserName) Тогда
		ИмяПользователя = ПараметрыПодключения.UserName;

		Если ПараметрыПодключения.Свойство("Password") Тогда
			ПарольПользователя = ПараметрыПодключения.Password;
		Иначе
			ПарольПользователя = Неопределено;
		КонецЕсли;
	Иначе
		ИмяПользователя		= Неопределено;
		ПарольПользователя	= Неопределено;
	КонецЕсли;

	АдресСервиса = ПараметрыПодключения.URL + "/ws/InterfaceVersion?wsdl";

	ПараметрыПодключения = Новый Структура;
	ПараметрыПодключения.Вставить("АдресWSDL",				АдресСервиса);
	ПараметрыПодключения.Вставить("URIПространстваИмен",	"http://www.1c.ru/SaaS/1.0/WS");
	ПараметрыПодключения.Вставить("ИмяСервиса",				"InterfaceVersion");
	ПараметрыПодключения.Вставить("ИмяПользователя",		ИмяПользователя);
	ПараметрыПодключения.Вставить("Пароль",					ПарольПользователя);
	ПараметрыПодключения.Вставить("Таймаут",				7);

	ПроксиВерсионирования = БазоваяПодсистемаСервер.ОН_СоздатьWSПрокси(ПараметрыПодключения);

	МассивXDTO = ПроксиВерсионирования.GetVersions(ИмяИнтерфейса);
	Если МассивXDTO = Неопределено Тогда
		Возврат Новый ФиксированныйМассив(Новый Массив);
	Иначе
		Сериализатор = Новый СериализаторXDTO(ПроксиВерсионирования.ФабрикаXDTO);

		Возврат Новый ФиксированныйМассив(Сериализатор.ПрочитатьXDTO(МассивXDTO));
	КонецЕсли;
КонецФункции

Функция ПолучитьWSDL(Знач Адрес, Знач ИмяПользователя, Знач Пароль, Знач Таймаут, Знач ЗащищенноеСоединение = Неопределено)
	ПараметрыПолучения = Новый Структура;
	Если НЕ ПустаяСтрока(ИмяПользователя) Тогда
		ПараметрыПолучения.Вставить("Пользователь",	ИмяПользователя);
		ПараметрыПолучения.Вставить("Пароль", 		Пароль);
	КонецЕсли;
	ПараметрыПолучения.Вставить("Таймаут", Таймаут);
	ПараметрыПолучения.Вставить("ЗащищенноеСоединение", ЗащищенноеСоединение);

	// Зарезервировано для новых подсистем

	ВызватьИсключение "Подсистема ""Получение файлов из интернета"" не доступна.";

	Возврат Неопределено;
КонецФункции

#КонецЕсли
