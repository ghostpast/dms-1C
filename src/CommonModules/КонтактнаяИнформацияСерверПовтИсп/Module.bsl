///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2021, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Область УправлениеКонтактнойИнформацией

Функция сУКИ_СведенияОДоступностиАдресногоКлассификатора() Экспорт
	Результат = Новый Соответствие;
	Результат.Вставить("КлассификаторДоступен",             Ложь);
	Результат.Вставить("ИспользоватьЗагруженные",           Ложь);
	Результат.Вставить("ИспользуетсяАдресныйКлассификатор", Ложь);

	Возврат Новый ФиксированноеСоответствие(Результат);

	// Зарезервировано для новых подсистем
КонецФункции

Функция сУКИ_ТипВидаКонтактнойИнформации(Знач ВидИнформации) Экспорт
	Результат = Неопределено;

	Тип = ТипЗнч(ВидИнформации);
	Если Тип = Тип("ПеречислениеСсылка.ТипыКонтактнойИнформации") Тогда
		Результат = ВидИнформации;
	ИначеЕсли Тип = Тип("СправочникСсылка.ВидыКонтактнойИнформации") Тогда
		Результат = БазоваяПодсистемаСервер.ОН_ЗначениеРеквизитаОбъекта(ВидИнформации, "Тип");
	ИначеЕсли ВидИнформации <> Неопределено Тогда
		Данные		= Новый Структура("Тип");
		ЗаполнитьЗначенияСвойств(Данные, ВидИнформации);
		Результат	= Данные.Тип;
	КонецЕсли;

	Возврат Результат;
КонецФункции

Функция сУКИ_НаименованияВидовКонтактнойИнформации() Экспорт
	Результат = Новый Соответствие;
	Для каждого Язык Из Метаданные.Языки Цикл
		Наименования				= Новый Соответствие;
		Результат[Язык.КодЯзыка]	= Наименования;
	КонецЦикла;

	Возврат Новый ФиксированноеСоответствие(Результат);
КонецФункции

Функция сУКИ_ВидыКонтактнойИнформацииПоИмени() Экспорт
	Виды					= Новый Соответствие;
	ПредопределенныеВиды	= КонтактнаяИнформацияСервер.УКИ_ПредопределенныеВидыКонтактнойИнформации();

	Для каждого ПредопределенныеВид Из ПредопределенныеВиды Цикл
		Виды.Вставить(ПредопределенныеВид.Имя, ПредопределенныеВид.Ссылка);
	КонецЦикла;

	Возврат Новый ФиксированноеСоответствие(Виды);
КонецФункции

Функция сУКИ_ГруппаВидаКонтактнойИнформацииПоИмениОбъекта(ПолноеИмяОбъектаМетаданных) Экспорт
	ИмяГруппыВидовКИ = СтрЗаменить(ПолноеИмяОбъектаМетаданных, ".", "");

	Запрос			= Новый Запрос;
	Запрос.Текст	= "ВЫБРАТЬ
	|	ВидыКонтактнойИнформации.Ссылка КАК Ссылка,
	|ВЫБОР
	|	КОГДА ВидыКонтактнойИнформации.ИмяПредопределенногоВида <> """"
	|	ТОГДА ВидыКонтактнойИнформации.ИмяПредопределенногоВида
	|	ИНАЧЕ ВидыКонтактнойИнформации.ИмяПредопределенныхДанных
	|КОНЕЦ КАК ИмяПредопределенногоВида
	|ИЗ
	|	Справочник.ВидыКонтактнойИнформации КАК ВидыКонтактнойИнформации
	|ГДЕ
	|	ВидыКонтактнойИнформации.ЭтоГруппа = ИСТИНА
	|	И ВидыКонтактнойИнформации.ПометкаУдаления = ЛОЖЬ
	|	И ВидыКонтактнойИнформации.Используется = ИСТИНА";

	РезультатЗапроса = Запрос.Выполнить().Выбрать();
	Пока РезультатЗапроса.Следующий() Цикл
		Если СтрСравнить(РезультатЗапроса.ИмяПредопределенногоВида, ИмяГруппыВидовКИ) = 0 Тогда
			Возврат РезультатЗапроса.Ссылка;
		КонецЕсли;
	КонецЦикла;

	Возврат Неопределено;
КонецФункции

Функция сУКИ_КонтактнаяИнформацияОбъектаСодержитКолонкуДействуетС(ОбъектСсылка) Экспорт
	Возврат ОбъектСсылка.Метаданные().ТабличныеЧасти.КонтактнаяИнформация.Реквизиты.Найти("ДействуетС") <> Неопределено;
КонецФункции

#КонецОбласти
