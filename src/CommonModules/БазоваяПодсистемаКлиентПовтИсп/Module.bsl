///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2022, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Область СтандартныеПодсистемы

Функция СП_ПараметрыРаботыКлиентаПриЗапуске() Экспорт
	СП_ПроверитьПорядокЗапускаПрограммыПередНачаломРаботыСистемы();

	ПараметрыПриЗапускеПрограммы = ПараметрыПриложения["СтандартныеПодсистемы.ПараметрыПриЗапускеПрограммы"];

	Параметры = Новый Структура;
	Параметры.Вставить("ПолученныеПараметрыКлиента", Неопределено);

	Если ПараметрыПриЗапускеПрограммы.Свойство("ПолученныеПараметрыКлиента") И ТипЗнч(ПараметрыПриЗапускеПрограммы.ПолученныеПараметрыКлиента) = Тип("Структура") Тогда
		Параметры.Вставить("ПолученныеПараметрыКлиента", БазоваяПодсистемаКлиент.ОН_СкопироватьРекурсивно(ПараметрыПриЗапускеПрограммы.ПолученныеПараметрыКлиента));
	КонецЕсли;

	Если ПараметрыПриЗапускеПрограммы.Свойство("ПропуститьОчисткуСкрытияРабочегоСтола") Тогда
		Параметры.Вставить("ПропуститьОчисткуСкрытияРабочегоСтола");
	КонецЕсли;

	Если ПараметрыПриЗапускеПрограммы.Свойство("ОпцииИнтерфейса") И ТипЗнч(Параметры.ПолученныеПараметрыКлиента) = Тип("Структура") Тогда
		Параметры.ПолученныеПараметрыКлиента.Вставить("ОпцииИнтерфейса");
	КонецЕсли;

	БазоваяПодсистемаКлиент.СП_ЗаполнитьПараметрыРаботыКлиентаНаСервере(Параметры);

	ПараметрыКлиента = БазоваяПодсистемаВызовСервера.СП_ПараметрыРаботыКлиентаПриЗапуске(Параметры);

	Если ПараметрыПриЗапускеПрограммы.Свойство("ПолученныеПараметрыКлиента")
		И ПараметрыПриЗапускеПрограммы.ПолученныеПараметрыКлиента <> Неопределено
		И Не ПараметрыПриЗапускеПрограммы.Свойство("ОпцииИнтерфейса") Тогда

		ПараметрыПриЗапускеПрограммы.Вставить("ОпцииИнтерфейса", ПараметрыКлиента.ОпцииИнтерфейса);
	КонецЕсли;

	БазоваяПодсистемаКлиент.СП_ЗаполнитьПараметрыКлиента(ПараметрыКлиента);

	// Обновление состояния скрытия рабочего стола на клиенте по состоянию на сервере.
	БазоваяПодсистемаКлиент.СП_СкрытьРабочийСтолПриНачалеРаботыСистемы(Параметры.СкрытьРабочийСтолПриНачалеРаботыСистемы, Истина);

	Возврат ПараметрыКлиента;
КонецФункции

Функция СП_ПараметрыРаботыКлиента() Экспорт
	СП_ПроверитьПорядокЗапускаПрограммыПередНачаломРаботыСистемы();
	СП_ПроверитьПорядокЗапускаПрограммыПриНачалеРаботыСистемы();

	СвойстваКлиента = Новый Структура;
	БазоваяПодсистемаКлиент.СП_ЗаполнитьПараметрыРаботыКлиентаНаСервере(СвойстваКлиента);

	Возврат БазоваяПодсистемаВызовСервера.СП_ПараметрыРаботыКлиента(СвойстваКлиента);
КонецФункции

Процедура СП_ПроверитьПорядокЗапускаПрограммыПередНачаломРаботыСистемы()
	ИмяПараметра = "СтандартныеПодсистемы.ЗапускПрограммыЗавершен";
	Если ПараметрыПриложения[ИмяПараметра] = Неопределено Тогда
		ВызватьИсключение СтрШаблон("Ошибка запуска программы.
			           |Первой процедурой, которая вызывается из обработчика события %1
			           |должна быть процедура %2.", "ПередНачаломРаботыСистемы", "БазоваяПодсистемаКлиент.ПередНачаломРаботыСистемы");
	КонецЕсли;
КонецПроцедуры

Процедура СП_ПроверитьПорядокЗапускаПрограммыПриНачалеРаботыСистемы()
	Если Не БазоваяПодсистемаКлиент.СП_ЗапускПрограммыЗавершен() Тогда
		ВызватьИсключение "Ошибка запуска программы.
				|Пока запуск не завершен следует использовать параметры работы клиента при запуске.";
	КонецЕсли;
КонецПроцедуры

#КонецОбласти
