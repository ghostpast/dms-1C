///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2021, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ВариантыОтчетов

Процедура ПриНачалеРаботыСистемы(Параметры) Экспорт
	Если Не СистемаВзаимодействия.ИспользованиеДоступно() Тогда
		Возврат;
	КонецЕсли;

	Обработчик	= Новый ОписаниеОповещения("ВО_ОбработатьДействияСообщения", ВариантыОтчетовКлиент);
	СистемаВзаимодействия.ПодключитьОбработчикДействияСообщения(Обработчик);
КонецПроцедуры

Процедура ВО_ОткрытьФормуОтчета(Знач ФормаВладелец, Знач Вариант, Знач ДополнительныеПараметры = Неопределено) Экспорт
	Тип	= ТипЗнч(Вариант);
	Если Тип = Тип("Структура") Тогда
		ПараметрыОткрытия = Вариант;
	ИначеЕсли Тип = Тип("СправочникСсылка.ВариантыОтчетов")  Или Тип = Неопределено Тогда
		// зарезервировано для новых подсистем

		ПараметрыОткрытия = Новый Структура("Ключ", Вариант);
		Если ДополнительныеПараметры <> Неопределено Тогда
			БазоваяПодсистемаКлиентСервер.ОН_ДополнитьСтруктуру(ПараметрыОткрытия, ДополнительныеПараметры, Истина);
		КонецЕсли;
		ОткрытьФорму("Справочник.ВариантыОтчетов.ФормаОбъекта", ПараметрыОткрытия, Неопределено, Истина);

		Возврат;
	Иначе
		ПараметрыОткрытия = Новый Структура("Ссылка, Отчет, ТипОтчета, ПолноеИмяОтчета, ИмяОтчета, КлючВарианта, КлючЗамеров");
		Если ТипЗнч(ФормаВладелец) = Тип("ФормаКлиентскогоПриложения") Тогда
			ЗаполнитьЗначенияСвойств(ПараметрыОткрытия, ФормаВладелец);
		КонецЕсли;
		ЗаполнитьЗначенияСвойств(ПараметрыОткрытия, Вариант);
	КонецЕсли;

	Если ДополнительныеПараметры <> Неопределено Тогда
		БазоваяПодсистемаКлиентСервер.ОН_ДополнитьСтруктуру(ПараметрыОткрытия, ДополнительныеПараметры, Истина);
	КонецЕсли;

	ВариантыОтчетовКлиентСервер.ВО_ДополнитьСтруктуруКлючом(ПараметрыОткрытия, "ВыполнятьЗамеры", Ложь);

	ПараметрыОткрытия.ТипОтчета = ВариантыОтчетовКлиентСервер.ВО_ТипОтчетаСтрокой(ПараметрыОткрытия.ТипОтчета, ПараметрыОткрытия.Отчет);
	Если Не ЗначениеЗаполнено(ПараметрыОткрытия.ТипОтчета) Тогда
		ВызватьИсключение СтрШаблон("Не определен тип отчета в %1", "ВариантыОтчетовКлиент.ОткрытьФормуОтчета");
	КонецЕсли;

	Если ПараметрыОткрытия.ТипОтчета = "Внутренний" Или ПараметрыОткрытия.ТипОтчета = "Расширение" Тогда
		Вид = "Отчет";
		КлючЗамеров = БазоваяПодсистемаКлиентСервер.ОН_СвойствоСтруктуры(ПараметрыОткрытия, "КлючЗамеров");
		Если ЗначениеЗаполнено(КлючЗамеров) Тогда
			ПараметрыКлиента = БазоваяПодсистемаКлиентСервер.ОН_СвойствоСтруктуры(БазоваяПодсистемаКлиентПовтИсп.СП_ПараметрыРаботыКлиентаПриЗапуске(),"ВариантыОтчетов");
			Если ПараметрыКлиента.ВыполнятьЗамеры Тогда
				ПараметрыОткрытия.ВыполнятьЗамеры = Истина;
				ПараметрыОткрытия.Вставить("ИмяОперации", КлючЗамеров + ".Открытие");
				ПараметрыОткрытия.Вставить("КомментарийОперации", ПараметрыКлиента.ПрефиксЗамеров);
			КонецЕсли;
		КонецЕсли;
	ИначеЕсли ПараметрыОткрытия.ТипОтчета = "Дополнительный" Тогда
		Вид = "ВнешнийОтчет";
		Если Не ПараметрыОткрытия.Свойство("Подключен") Тогда
			ВариантыОтчетовВызовСервера.ВО_ПриПодключенииОтчета(ПараметрыОткрытия);
		КонецЕсли;
		Если Не ПараметрыОткрытия.Подключен Тогда
			Возврат;
		КонецЕсли;
	Иначе
		ПоказатьПредупреждение(, "Вариант внешнего отчета можно открыть только из формы отчета.");
		Возврат;
	КонецЕсли;

	Если Не ЗначениеЗаполнено(ПараметрыОткрытия.ИмяОтчета) Тогда
		ВызватьИсключение СтрШаблон("Не определено имя отчета в %1", "ВариантыОтчетовКлиент.ОткрытьФормуОтчета");
	КонецЕсли;

	ПолноеИмяОтчета = БазоваяПодсистемаКлиентСервер.ОН_СвойствоСтруктуры(ПараметрыОткрытия, "ПолноеИмяОтчета");

	Если Не ЗначениеЗаполнено(ПолноеИмяОтчета) Тогда
		ПолноеИмяОтчета = Вид + "." + ПараметрыОткрытия.ИмяОтчета;
	КонецЕсли;

	КлючУникальности = ВариантыОтчетовКлиентСервер.О_КлючУникальности(ПолноеИмяОтчета, ПараметрыОткрытия.КлючВарианта);
	ПараметрыОткрытия.Вставить("КлючПараметровПечати",        КлючУникальности);
	ПараметрыОткрытия.Вставить("КлючСохраненияПоложенияОкна", КлючУникальности);

	Если ПараметрыОткрытия.ВыполнятьЗамеры Тогда
		// зарезервировано для новых подсистем
	КонецЕсли;

	ОткрытьФорму(ПолноеИмяОтчета + ".Форма", ПараметрыОткрытия, Неопределено, Истина);

	Если ПараметрыОткрытия.ВыполнятьЗамеры Тогда
		// зарезервировано для новых подсистем
	КонецЕсли;
КонецПроцедуры

Процедура ВО_ОбработатьДействияСообщения(Сообщение, Действие, ДополнительныеПараметры) Экспорт
	Если Действие = "ПрименитьПереданныеНастройки" Тогда
		Оповестить(Действие, Сообщение.Данные);
	КонецЕсли;
КонецПроцедуры

Процедура ВО_ПоказатьНастройкиОтчета(Вариант) Экспорт
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ПоказатьКарточку", Истина);
	ПараметрыФормы.Вставить("Ключ", Вариант);
	ОткрытьФорму("Справочник.ВариантыОтчетов.ФормаОбъекта", ПараметрыФормы);
КонецПроцедуры

Процедура ВО_ОбновитьОткрытыеФормы(Параметр = Неопределено, Источник = Неопределено) Экспорт
	Оповестить("Запись_ВариантыОтчетов", Параметр, Источник);
КонецПроцедуры

Процедура ВО_ОбновитьВариантыОтчетовИзФайлов(ОписаниеФайлов, СвойстваВариантаОтчетаОснования) Экспорт
	Если ТипЗнч(ОписаниеФайлов) <> Тип("Массив") Тогда
		Возврат;
	КонецЕсли;

	Если ОписаниеФайлов.Количество() = 1 Тогда
		ВО_ОбновитьВариантОтчетаИзФайла(ОписаниеФайлов[0], СвойстваВариантаОтчетаОснования);

		Возврат;
	КонецЕсли;

	СвойстваВариантовОтчетов = ВариантыОтчетовВызовСервера.ВО_СвойстваВариантовОтчетовИзФайлов(ОписаниеФайлов);

	ТекстУведомления = СтрШаблон("Варианты отчетов (%1) загружены из файла", СвойстваВариантовОтчетов.Количество());

	ПоказатьОповещениеПользователя(ТекстУведомления);
	Оповестить("ОбновитьВариантыОтчетовИзФайлов", СвойстваВариантовОтчетов);
КонецПроцедуры

Процедура ВО_ОткрытьФормуВыбранногоВариантаОтчета(Ответ, ВариантОтчета) Экспорт
	Если Ответ = КодВозвратаДиалога.Да Тогда
		ВО_ОткрытьФормуОтчета(Неопределено, ВариантОтчета);
	КонецЕсли;
КонецПроцедуры

Процедура ВО_ОбновитьВариантОтчетаИзФайла(ОписаниеФайла, СвойстваВариантаОтчетаОснования) Экспорт
	Если ОписаниеФайла = Неопределено Тогда
		Возврат;
	КонецЕсли;

	Если СвойстваВариантаОтчетаОснования = Неопределено Тогда
		СвойстваВариантаОтчетаОснования = Новый Структура("Ссылка, ИмяОтчета, ПредставлениеВарианта");
	КонецЕсли;

	СвойстваВариантаОтчета = ВариантыОтчетовВызовСервера.ВО_СвойстваВариантаОтчетаИзФайла(ОписаниеФайла, СвойстваВариантаОтчетаОснования.Ссылка);

	Если СвойстваВариантаОтчетаОснования.ИмяОтчета = СвойстваВариантаОтчета.ИмяОтчета Тогда
		ПараметрыОбновленияФормы = Новый Структура("КлючВарианта");
		ЗаполнитьЗначенияСвойств(ПараметрыОбновленияФормы, СвойстваВариантаОтчета);

		ВО_ОбновитьОткрытыеФормы(ПараметрыОбновленияФормы);
		ПоказатьОповещениеПользователя("Вариант отчета обновлен из файла");
	ИначеЕсли СвойстваВариантаОтчетаОснования.ПредставлениеВарианта = СвойстваВариантаОтчета.ПредставлениеВарианта Тогда
		ВО_ОткрытьФормуОтчета(Неопределено, СвойстваВариантаОтчета.Ссылка);
		ПоказатьОповещениеПользователя("Вариант отчета найден и обновлен из файла");
	Иначе
		Обработчик = Новый ОписаниеОповещения("ВО_ОткрытьФормуВыбранногоВариантаОтчета", ВариантыОтчетовКлиент, СвойстваВариантаОтчета.Ссылка);

		ШаблонТекстаВопроса = "Выбраны настройки варианта отчета ""%1"",
			|которые не совпадают с ""%2"".
			|Замена настроек выбранного варианта отчета невозможна.
			|
			|Создать новый вариант отчета (или обновить существующий при наличии)?";

		ТекстВопроса = СтрШаблон(ШаблонТекстаВопроса, СвойстваВариантаОтчета.ПредставлениеВарианта, СвойстваВариантаОтчетаОснования.ПредставлениеВарианта);

		ПоказатьВопрос(Обработчик, ТекстВопроса, РежимДиалогаВопрос.ДаНет,, КодВозвратаДиалога.Да);

	КонецЕсли;
КонецПроцедуры

Процедура ВО_ОткрытьДиалогРазмещенияВариантовВРазделах(Варианты, Владелец = Неопределено) Экспорт
	Если ТипЗнч(Варианты) <> Тип("Массив") Или Варианты.Количество() < 1 Тогда
		ПоказатьПредупреждение(, "Выберите варианты отчетов для размещения в разделах.");

		Возврат;
	КонецЕсли;

	ПараметрыОткрытия = Новый Структура("Варианты", Варианты);

	ОткрытьФорму("Справочник.ВариантыОтчетов.Форма.РазмещениеВРазделах", ПараметрыОткрытия, Владелец);
КонецПроцедуры

Процедура ВО_ДеревоПодсистемИспользованиеПриИзменении(Форма, Элемент) Экспорт
	СтрокаДерева = Форма.Элементы.ДеревоПодсистем.ТекущиеДанные;
	Если СтрокаДерева = Неопределено Тогда
		Возврат;
	КонецЕсли;

	// Пропуск корневой строки
	Если СтрокаДерева.Приоритет = "" Тогда
		СтрокаДерева.Использование = 0;

		Возврат;
	КонецЕсли;

	Если СтрокаДерева.Использование = 2 Тогда
		СтрокаДерева.Использование = 0;
	КонецЕсли;

	СтрокаДерева.Модифицированность = Истина;
КонецПроцедуры

Процедура ВО_ДеревоПодсистемВажностьПриИзменении(Форма, Элемент) Экспорт
	ДеревоПодсистем = Форма.Элементы.ДеревоПодсистем;

	СтрокаДерева = ДеревоПодсистем.ТекущиеДанные;
	Если СтрокаДерева = Неопределено Тогда
		Возврат;
	КонецЕсли;

	// Пропуск корневой строки
	Если СтрокаДерева.Приоритет = "" Тогда
		СтрокаДерева.Важность = "";

		Возврат;
	КонецЕсли;

	Если СтрокаДерева.Важность <> "" Тогда
		СтрокаДерева.Использование = 1;
	КонецЕсли;

	СтрокаДерева.Модифицированность = Истина;
КонецПроцедуры

Процедура ВО_ОформитьПользователейВариантаОтчета(Форма, СбрасыватьПризнакИспользования = Истина) Экспорт
	Элементы	= Форма.Элементы;
	Объект		= Форма.Объект;

	ПараметрыКлиента		= БазоваяПодсистемаКлиентПовтИсп.СП_ПараметрыРаботыКлиента();
	ЦветНеактивныхЗначений	= ПараметрыКлиента.ЭлементыСтиля.ТекстЗапрещеннойЯчейкиЦвет;

	Если Не СбрасыватьПризнакИспользования Тогда
		Элементы.ПользователиВарианта.ЦветТекста = ?(Объект.ТолькоДляАвтора, ЦветНеактивныхЗначений, Новый Цвет);

		Возврат;
	КонецЕсли;

	КоличествоПомеченных = 0;

	Для Каждого Строка Из Форма.ПользователиВарианта Цикл
		КоличествоПомеченных = КоличествоПомеченных + Булево(Строка.Пометка);
	КонецЦикла;

	Объект.ТолькоДляАвтора	= (КоличествоПомеченных = 0);
	Форма.Доступен			= ?(Объект.ТолькоДляАвтора, "ТолькоДляАвтора", "УказаннымПользователям");

	Элементы.ПользователиВарианта.ЦветТекста = ?(Форма.Объект.ТолькоДляАвтора, ЦветНеактивныхЗначений, Новый Цвет);
КонецПроцедуры

Процедура ВО_ПользователиВариантаОтчетаОбработкаВыбора(Форма, ВыбранныеЗначения, СтандартнаяОбработка) Экспорт
	Если ТипЗнч(ВыбранныеЗначения) <> Тип("Массив") Или ВыбранныеЗначения.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;

	СтандартнаяОбработка = Ложь;

	ПользователиВарианта = Форма.ПользователиВарианта;

	ОбщаяГруппаПользователей		= ПредопределенноеЗначение("Справочник.ГруппыПользователей.ВсеПользователи");
	ОбщаяГруппаВнешниеПользователи	= ПредопределенноеЗначение("Справочник.ГруппыВнешнихПользователей.ВсеВнешниеПользователи");

	Если ТипЗнч(ВыбранныеЗначения[0]) = Тип("СправочникСсылка.ГруппыВнешнихПользователей") Тогда
		ВО_ПодготовитьСписокКДобавлениюВнешнихПользователейВариантаОтчета(ПользователиВарианта, ВыбранныеЗначения, ОбщаяГруппаВнешниеПользователи);
	Иначе
		ВО_ПодготовитьСписокКДобавлениюПользователейВариантаОтчета(ПользователиВарианта, ВыбранныеЗначения, ОбщаяГруппаПользователей);
	КонецЕсли;

	Для Каждого Значение Из ВыбранныеЗначения Цикл
		Если ПользователиВарианта.НайтиПоЗначению(Значение) = Неопределено Тогда
			ПользователиВарианта.Добавить(Значение,, Истина, ВО_КартинкаПользователяВариантаОтчета(Значение));
		КонецЕсли;
	КонецЦикла;

	Если ПользователиВарианта.НайтиПоЗначению(ОбщаяГруппаПользователей) <> Неопределено И ПользователиВарианта.НайтиПоЗначению(ОбщаяГруппаВнешниеПользователи) <> Неопределено Тогда
		ПользователиВарианта.Очистить();
		ПользователиВарианта.Добавить(,, Истина, ВО_КартинкаПользователяВариантаОтчета());
	КонецЕсли;

	ВО_ОформитьПользователейВариантаОтчета(Форма);
КонецПроцедуры

Процедура ВО_ПодготовитьСписокКДобавлениюВнешнихПользователейВариантаОтчета(ПользователиВарианта, ВыбранныеЗначения, ОбщаяГруппаПользователей)
	Если ПользователиВарианта.НайтиПоЗначению(ОбщаяГруппаПользователей) <> Неопределено Или ПользователиВарианта.НайтиПоЗначению(Неопределено) <> Неопределено Тогда
		ВыбранныеЗначения.Очистить();

		Возврат;
	КонецЕсли;

	Если ВыбранныеЗначения.Найти(ОбщаяГруппаПользователей) = Неопределено Тогда
		Возврат;
	КонецЕсли;

	ТипыПользователей = Новый ОписаниеТипов("СправочникСсылка.ГруппыВнешнихПользователей");
	ВО_УдалитьПользователейВариантаОтчетаУказанныхТипов(ПользователиВарианта, ТипыПользователей);

	ВыбранныеЗначения.Очистить();
	ВыбранныеЗначения.Добавить(ОбщаяГруппаПользователей);
КонецПроцедуры

Процедура ВО_ПодготовитьСписокКДобавлениюПользователейВариантаОтчета(ПользователиВарианта, ВыбранныеЗначения, ОбщаяГруппаПользователей)
	Если ПользователиВарианта.НайтиПоЗначению(Неопределено) <> Неопределено Тогда
		ПользователиВарианта.Очистить();
	Иначе
		ТипыПользователей = Новый ОписаниеТипов("СправочникСсылка.ГруппыПользователей, СправочникСсылка.Пользователи");
		ВО_УдалитьПользователейВариантаОтчетаУказанныхТипов(ПользователиВарианта, ТипыПользователей);
	КонецЕсли;

	Если ВыбранныеЗначения.Найти(ОбщаяГруппаПользователей) = Неопределено Тогда
		Возврат;
	КонецЕсли;

	ВыбранныеЗначения.Очистить();
	ВыбранныеЗначения.Добавить(ОбщаяГруппаПользователей);
КонецПроцедуры

Функция ВО_КартинкаПользователяВариантаОтчета(Пользователь = Неопределено)
	Если ТипЗнч(Пользователь) = Тип("СправочникСсылка.Пользователи") Тогда
		Возврат БиблиотекаКартинок.СостояниеПользователя02;
	ИначеЕсли ТипЗнч(Пользователь) = Тип("СправочникСсылка.ГруппыВнешнихПользователей") Тогда
		Возврат БиблиотекаКартинок.СостояниеПользователя10;
	КонецЕсли;

	Возврат БиблиотекаКартинок.СостояниеПользователя04;
КонецФункции

Процедура ВО_УдалитьПользователейВариантаОтчетаУказанныхТипов(ПользователиВарианта, ТипыПользователей)
	ИндексЭлемента = ПользователиВарианта.Количество() - 1;

	Пока ИндексЭлемента >= 0 Цикл
		ЭлементСписка = ПользователиВарианта[ИндексЭлемента];

		Если ТипыПользователей.СодержитТип(ТипЗнч(ЭлементСписка.Значение)) Тогда
			ПользователиВарианта.Удалить(ЭлементСписка);
		КонецЕсли;

		ИндексЭлемента = ИндексЭлемента - 1;
	КонецЦикла;
КонецПроцедуры

Процедура ВО_ПодобратьПользователейВариантаОтчета(Форма, ПодборГруппПользователей = Ложь, ПодборГруппВнешнихПользователей = Ложь) Экспорт
	ПараметрыПодбора = ВО_ПараметрыПодбораПользователейВариантаОтчета(Форма, ПодборГруппПользователей, ПодборГруппВнешнихПользователей);

	Если ПодборГруппВнешнихПользователей Тогда
		ИмяФормыПодбора = "Справочник.ГруппыВнешнихПользователей.ФормаВыбора";
	Иначе
		ИмяФормыПодбора = "Справочник.Пользователи.ФормаВыбора";
	КонецЕсли;

	ОткрытьФорму(ИмяФормыПодбора, ПараметрыПодбора, Форма.Элементы.ПользователиВарианта);
КонецПроцедуры

Функция ВО_ПараметрыПодбораПользователейВариантаОтчета(Форма, ПодборГруппПользователей, ПодборГруппВнешнихПользователей = Ложь)
	ТекущиеДанные			= Форма.Элементы.ПользователиВарианта.ТекущиеДанные;
	ВыбранныеПользователи	= ВО_ВыбранныеПользователиВарианта(Форма.ПользователиВарианта, ПодборГруппВнешнихПользователей);

	ПараметрыПодбора = Новый Структура;
	ПараметрыПодбора.Вставить("РежимВыбора",				Истина);
	ПараметрыПодбора.Вставить("ТекущаяСтрока",				?(ТекущиеДанные = Неопределено, Неопределено, ТекущиеДанные.Значение));
	ПараметрыПодбора.Вставить("ЗакрыватьПриВыборе",			Ложь);
	ПараметрыПодбора.Вставить("МножественныйВыбор",			Истина);
	ПараметрыПодбора.Вставить("РасширенныйПодбор",			Истина);
	ПараметрыПодбора.Вставить("ЗаголовокФормыПодбора",		"Подбор пользователей варианта отчета");
	ПараметрыПодбора.Вставить("ВыборГруппПользователей",	ПодборГруппПользователей);
	ПараметрыПодбора.Вставить("ВыбранныеПользователи",		ВыбранныеПользователи);

	Возврат ПараметрыПодбора;
КонецФункции

Функция ВО_ВыбранныеПользователиВарианта(ПользователиВарианта, ПодборГруппВнешнихПользователей)
	ТипыВыбранныхПользователей = Новый ОписаниеТипов("СправочникСсылка.ГруппыПользователей, СправочникСсылка.Пользователи");

	Если ПодборГруппВнешнихПользователей Тогда
		ТипыВыбранныхПользователей = Новый ОписаниеТипов("СправочникСсылка.ГруппыВнешнихПользователей");
	КонецЕсли;

	ВыбранныеПользователи = Новый Массив;

	Для Каждого ЭлементСписка Из ПользователиВарианта Цикл
		Если ЭлементСписка.Пометка И ТипыВыбранныхПользователей.СодержитТип(ТипЗнч(ЭлементСписка.Значение)) Тогда
			ВыбранныеПользователи.Добавить(ЭлементСписка.Значение);
		КонецЕсли;
	КонецЦикла;

	Если ВыбранныеПользователи.Количество() = 1 И ВыбранныеПользователи[0] = Неопределено Тогда
		Если ПодборГруппВнешнихПользователей Тогда
			ВыбранныеПользователи[0] = ПредопределенноеЗначение("Справочник.ГруппыВнешнихПользователей.ВсеВнешниеПользователи");
		Иначе
			ВыбранныеПользователи[0] = ПредопределенноеЗначение("Справочник.ГруппыПользователей.ВсеПользователи");
		КонецЕсли;
	КонецЕсли;

	Возврат ВыбранныеПользователи;
КонецФункции

Процедура ВО_ПроверитьПользователейВариантаОтчета(Форма) Экспорт
	Объект = Форма.Объект;

	Если Не Объект.ТолькоДляАвтора Тогда
		Возврат;
	КонецЕсли;

	ПараметрыКлиента = БазоваяПодсистемаКлиентПовтИсп.СП_ПараметрыРаботыКлиентаПриЗапуске();

	ПользователиВарианта = Форма.ПользователиВарианта;
	ПользователиВарианта.Очистить();
	ПользователиВарианта.Добавить(ПараметрыКлиента.АвторизованныйПользователь, "[ЭтоТекущийПользователь]",, ВО_КартинкаПользователяВариантаОтчета(ПараметрыКлиента.АвторизованныйПользователь));
КонецПроцедуры

Процедура ВО_ПоделитьсяПользовательскимиНастройками(ОписаниеНастроек) Экспорт
	Если ОписаниеНастроек.Настройки.Элементы.Количество() = 0 Тогда
		БазоваяПодсистемаКлиентСервер.сОН_СообщитьПользователю("Настройки (пользовательские) не установлены.", Неопределено, "", "", Ложь);

		Возврат;
	КонецЕсли;

	ПараметрыПодбора = Новый Структура;
	ПараметрыПодбора.Вставить("РежимВыбора",							Истина);
	ПараметрыПодбора.Вставить("ЗакрыватьПриВыборе",						Ложь);
	ПараметрыПодбора.Вставить("МножественныйВыбор",						Истина);
	ПараметрыПодбора.Вставить("РасширенныйПодбор",						Истина);
	ПараметрыПодбора.Вставить("СкрытьПользователейБезПользователяИБ",	Истина);
	ПараметрыПодбора.Вставить("ВыбранныеПользователи",					Новый Массив);
	ПараметрыПодбора.Вставить("ЗаголовокФормыПодбора",					"Поделиться настройками отчета с пользователями");
	ПараметрыПодбора.Вставить("ЗаголовокКнопкиЗавершенияПодбора",		"Поделиться");

	Обработчик = Новый ОписаниеОповещения("ВО_ПоделитьсяПользовательскимиНастройкамиПослеВыбораПользователей", ВариантыОтчетовКлиент, ОписаниеНастроек);

	ОткрытьФорму("Справочник.Пользователи.ФормаВыбора", ПараметрыПодбора,,,,, Обработчик);
КонецПроцедуры

Процедура ВО_ПоделитьсяПользовательскимиНастройкамиПослеВыбораПользователей(ВыбранныеПользователи, ОписаниеНастроек) Экспорт
	Если ВыбранныеПользователи = Неопределено Или ВыбранныеПользователи.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;

	ВариантыОтчетовВызовСервера.ВО_ПоделитьсяПользовательскимиНастройками(ВыбранныеПользователи, ОписаниеНастроек);

	Предупреждение = БазоваяПодсистемаКлиентСервер.ОН_СвойствоСтруктуры(ОписаниеНастроек, "Предупреждение");

	Если ЗначениеЗаполнено(Предупреждение) Тогда
		ПоказатьПредупреждение(, Предупреждение);

		Возврат;
	КонецЕсли;

	Пояснение = БазоваяПодсистемаКлиентСервер.ОН_СвойствоСтруктуры(ОписаниеНастроек, "Пояснение", "");
	ПоказатьОповещениеПользователя("Настройки переданы",, Пояснение);
КонецПроцедуры

Процедура сВО_ПоказатьКонтекстнуюНастройкуОтчета(Форма, Элемент, Область, СтандартнаяОбработка) Экспорт
	Если ТипЗнч(Область) <> Тип("ОбластьЯчеекТабличногоДокумента") Или Область.ТипОбласти <> ТипОбластиЯчеекТабличногоДокумента.Прямоугольник Или Область.Расшифровка <> Неопределено И ТипЗнч(Область.Расшифровка) <> Тип("ИдентификаторРасшифровкиКомпоновкиДанных") Тогда
		Возврат;
	КонецЕсли;

	НастройкиОтчета		= Форма.НастройкиОтчета; // см. ВариантыОтчетов.НастройкиФормыОтчета
	СвойстваРезультата	= НастройкиОтчета.СвойстваРезультата; // см. ВариантыОтчетовСлужебный.СвойстваРезультатаОтчета

	Заголовки			= СвойстваРезультата.Заголовки;

	Область				= Элемент.ТекущаяОбласть; // ОбластьЯчеекТабличногоДокумента
	СвойстваЗаголовка	= Заголовки[Область.Имя]; //

	Если ТипЗнч(СвойстваЗаголовка) <> Тип("Структура") Или ТипЗнч(СвойстваЗаголовка.Поле) <> Тип("ПолеКомпоновкиДанных") Или СвойстваЗаголовка.ТипЗначения.Типы().Количество() = 0 Тогда
		Возврат;
	КонецЕсли;

	КомпоновщикНастроек = сВО_КомпоновщикНастроекОтчета(Форма);

	КэшЗначенийОтборов = БазоваяПодсистемаКлиентСервер.ОН_СвойствоСтруктуры(КомпоновщикНастроек.ПользовательскиеНастройки.ДополнительныеСвойства, "КэшЗначенийОтборов", Новый Соответствие);

	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("НастройкиОтчета",		НастройкиОтчета);
	ПараметрыФормы.Вставить("КомпоновщикНастроек",	КомпоновщикНастроек);
	ПараметрыФормы.Вставить("ДанныеРасшифровки",	Форма.ОтчетДанныеРасшифровки);
	ПараметрыФормы.Вставить("Документ",				Форма.ОтчетТабличныйДокумент);
	ПараметрыФормы.Вставить("Заголовки",			Заголовки);
	ПараметрыФормы.Вставить("СвойстваЗаголовка",	СвойстваЗаголовка);
	ПараметрыФормы.Вставить("КэшЗначенийОтборов",	КэшЗначенийОтборов);

	ОткрытьФорму("ХранилищеНастроек.ХранилищеВариантовОтчетов.Форма.КонтекстнаяНастройкаОтчета", ПараметрыФормы, Форма, Форма.УникальныйИдентификатор);

	СтандартнаяОбработка = Ложь
КонецПроцедуры

Процедура сВО_ОбработкаРасшифровки(Форма, Элемент, Расшифровка, СтандартнаяОбработка) Экспорт
	НастройкиОтчета		= Форма.НастройкиОтчета; // см. ВариантыОтчетов.НастройкиФормыОтчета
	СвойстваРезультата	= НастройкиОтчета.СвойстваРезультата; // см. ВариантыОтчетовСлужебный.СвойстваРезультатаОтчета
	ТекущаяОбласть		= Элемент.ТекущаяОбласть; // ОбластьЯчеекТабличногоДокумента

	Заголовки = СвойстваРезультата.Заголовки[ТекущаяОбласть.Имя];

	Если ТипЗнч(Заголовки) = Тип("Структура") Тогда
		СтандартнаяОбработка										= Ложь;
		Форма.Элементы.КонтекстноеМенюОбластиЗаголовка.Видимость	= Истина;

		Возврат;
	КонецЕсли;
КонецПроцедуры

Процедура сВО_ОбработкаДополнительнойРасшифровки(Форма, Данные, Элемент, Расшифровка, СтандартнаяОбработка) Экспорт
	Если Данные = Неопределено Тогда
		Возврат;
	КонецЕсли;

	СвойстваОбласти = сВО_СвойстваОбластиРасшифровки(Форма, Элемент.ТекущаяОбласть, Данные.Поле);

	Если ТипЗнч(СвойстваОбласти) <> Тип("Структура") Или ТипЗнч(СвойстваОбласти.СвойстваЗаголовка) <> Тип("Структура") Тогда
		Возврат;
	КонецЕсли;

	ОсновноеМеню		= Новый Массив;

	Если СвойстваОбласти.ЭтоЗаголовок Тогда
		СвойстваЗаголовка = СвойстваОбласти.СвойстваЗаголовка;

		Если ТипЗнч(СвойстваЗаголовка.Поле) <> Тип("ПолеКомпоновкиДанных") Или СвойстваЗаголовка.ТипЗначения.Типы().Количество() = 0 Тогда
			Возврат;
		КонецЕсли;

		ДополнительноеМеню = сВО_КонтекстноеМенюОбластиЗаголовка();
	Иначе
		ОсновноеМеню.Добавить(ДействиеОбработкиРасшифровкиКомпоновкиДанных.Расшифровать);
		ДополнительноеМеню = сВО_КонтекстноеМенюОбластиДанных(СвойстваОбласти.СвойстваЗаголовка, Данные.ДоступныеВидыСравнения);
	КонецЕсли;

	ОбработкаРасшифровки = Новый ОбработкаРасшифровкиКомпоновкиДанных(Форма.ОтчетДанныеРасшифровки, Новый ИсточникДоступныхНастроекКомпоновкиДанных(Форма.НастройкиОтчета.АдресСхемы));

	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("Форма",					Форма);
	ДополнительныеПараметры.Вставить("Данные",					Данные);
	ДополнительныеПараметры.Вставить("Расшифровка",				Расшифровка);
	ДополнительныеПараметры.Вставить("ОбработкаРасшифровки",	ОбработкаРасшифровки);
	ДополнительныеПараметры.Вставить("СвойстваОбласти",			СвойстваОбласти);
	ДополнительныеПараметры.Вставить("Меню",					ДополнительноеМеню);

	ПолеОтчета = Форма.Элементы.ОтчетТабличныйДокумент; // ПолеТабличногоДокумента

	Обработчик = Новый ОписаниеОповещения("сВО_ВыполнитьРасшифровку", ЭтотОбъект, ДополнительныеПараметры);
	ОбработкаРасшифровки.ПоказатьВыборДействия(Обработчик, Расшифровка, ОсновноеМеню, ДополнительноеМеню,, ПолеОтчета);

	СтандартнаяОбработка = Ложь;
КонецПроцедуры

Процедура сВО_СпроситьОУведомленииПользователей(ОписаниеОповещения, КоличествоПользователей) Экспорт
	ПредставлениеКоличестваПользователей = СтрокаСЧислом("; %1 пользователя; ; %1 пользователей; %1 пользователей; %1 пользователей", КоличествоПользователей, ВидЧисловогоЗначения.Количественное, "L=ru");

	ТекстВопроса = СтрШаблон("Уведомить в чате %1 о том, что этот вариант будет отображен на их панели отчетов?", ПредставлениеКоличестваПользователей);

	ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНет, 60, КодВозвратаДиалога.Нет);
КонецПроцедуры

Процедура сВО_ДобавитьФормулы(Настройки, КоллекцияПолей, ОписаниеФормул) Экспорт
	Для Каждого ОписаниеФормулы Из ОписаниеФормул Цикл
		сВО_ДобавитьФормулу(Настройки, КоллекцияПолей, ОписаниеФормулы);
	КонецЦикла;
КонецПроцедуры

Процедура сВО_ДобавитьФормулу(Настройки, КоллекцияПолей, ОписаниеФормулы) Экспорт
	ПутьКДанным = Неопределено;

	Если ТипЗнч(ОписаниеФормулы) = Тип("ПользовательскоеПолеВыражениеКомпоновкиДанных") Тогда
		ПутьКДанным = ОписаниеФормулы.ПутьКДанным;
		ОписаниеФормулы = сВО_ОписаниеФормулыПоОбразцу(ОписаниеФормулы);
	КонецЕсли;

	Если ТипЗнч(ОписаниеФормулы) <> Тип("Структура") Или Не ОписаниеФормулы.Свойство("Формула") Тогда
		Возврат;
	КонецЕсли;

	Формулы = Настройки.ПользовательскиеПоля.Элементы;

	Если ЗначениеЗаполнено(ПутьКДанным) Тогда
		Формула = ВариантыОтчетовКлиентСервер.сВО_ФормулаПоПутиКДанным(Настройки, ПутьКДанным);
	Иначе
		Формула = сВО_ФормулаПоВыражению(Формулы, ОписаниеФормулы.Формула);
	КонецЕсли;

	Если Формула = Неопределено Тогда
		Формула = Формулы.Добавить(Тип("ПользовательскоеПолеВыражениеКомпоновкиДанных"));
	КонецЕсли;

	ДополнительныеПараметры = Новый Структура("КоллекцияПолей, Формула", КоллекцияПолей, Формула);
	сВО_УстановитьСвойстваФормулы(ОписаниеФормулы, ДополнительныеПараметры);

	ОписаниеФормулы = КоллекцияПолей.НайтиПоле(Новый ПолеКомпоновкиДанных(Формула.ПутьКДанным));
	сВО_УстановитьВыражениеИтоговыхЗаписей(Формула, ОписаниеФормулы);
КонецПроцедуры

Функция сВО_ФормулаПоВыражению(Формулы, Выражение)
	Для Каждого Формула Из Формулы Цикл
		Если СокрЛП(Выражение) = Формула.ПолучитьВыражениеДетальныхЗаписей() Тогда
			Возврат Формула;
		КонецЕсли;
	КонецЦикла;

	Возврат Неопределено;
КонецФункции

Процедура сВО_ИзменитьФормулу(Форма, Настройки, ПутьКДанным, ИмяКоллекции = "ДоступныеПоляВыбора") Экспорт
	Формула = ВариантыОтчетовКлиентСервер.сВО_ФормулаПоПутиКДанным(Настройки, ПутьКДанным);

	ПараметрыРедактированияФормулы							= ВариантыОтчетовКлиентСервер.КФ_ПараметрыРедактированияФормулы();
	ПараметрыРедактированияФормулы.Операнды					= Форма.НастройкиОтчета.АдресСхемы;
	ПараметрыРедактированияФормулы.ИмяКоллекцииСКДОперандов	= ИмяКоллекции;
	ПараметрыРедактированияФормулы.Формула					= Формула.ПолучитьВыражениеДетальныхЗаписей();
	ПараметрыРедактированияФормулы.Наименование				= Формула.Заголовок;

	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("КоллекцияПолей",	Настройки.ДоступныеПоляВыбора);
	ДополнительныеПараметры.Вставить("Формула",			Формула);

	Обработчик = Новый ОписаниеОповещения("сВО_ПослеИзмененияФормулы", Форма, ДополнительныеПараметры);

	КФ_НачатьРедактированиеФормулы(ПараметрыРедактированияФормулы, Обработчик);
КонецПроцедуры

Процедура сВО_ПослеИзмененияФормулы(ОписаниеФормулы, ДополнительныеПараметры) Экспорт
	Если ТипЗнч(ОписаниеФормулы) <> Тип("Структура") Или Не ОписаниеФормулы.Свойство("Формула") Тогда
		Возврат;
	КонецЕсли;

	сВО_УстановитьСвойстваФормулы(ОписаниеФормулы, ДополнительныеПараметры);
КонецПроцедуры

Функция сВО_ОписаниеФормулыПоОбразцу(Образец)
	ОписаниеФормулы = Новый Структура;
	ОписаниеФормулы.Вставить("Формула",					Образец.ПолучитьВыражениеДетальныхЗаписей());
	ОписаниеФормулы.Вставить("ПредставлениеФормулы",	Образец.ПолучитьПредставлениеВыраженияДетальныхЗаписей());
	ОписаниеФормулы.Вставить("Наименование",			Образец.Заголовок);

	Возврат ОписаниеФормулы;
КонецФункции

Процедура сВО_УстановитьСвойстваФормулы(ОписаниеФормулы, ДополнительныеПараметры)
	Выражение				= СокрЛП(ОписаниеФормулы.Формула);
	ПредставлениеВыражения	= СокрЛП(ОписаниеФормулы.ПредставлениеФормулы);
	Заголовок				= СокрЛП(ОписаниеФормулы.Наименование);

	Формула					= ДополнительныеПараметры.Формула;
	Формула.УстановитьВыражениеДетальныхЗаписей(Выражение);
	Формула.УстановитьПредставлениеВыражений(ПредставлениеВыражения, "");
	Формула.Заголовок		= ?(ЗначениеЗаполнено(Заголовок), Заголовок, ПредставлениеВыражения);

	ОписаниеФормулы			= ДополнительныеПараметры.КоллекцияПолей.НайтиПоле(Новый ПолеКомпоновкиДанных(Формула.ПутьКДанным));
	сВО_УстановитьВыражениеИтоговыхЗаписей(Формула, ОписаниеФормулы);
КонецПроцедуры

Процедура сВО_УстановитьВыражениеИтоговыхЗаписей(Формула, ОписаниеФормулы)
	ВыражениеДетальныхЗаписей	= Формула.ПолучитьВыражениеДетальныхЗаписей();
	ВыражениеИтоговыхЗаписей	= Формула.ПолучитьВыражениеИтоговыхЗаписей();

	Если ОписаниеФормулы = Неопределено Или ОписаниеФормулы.Тип.Типы().Количество() > 1 Или Не ОписаниеФормулы.Тип.СодержитТип(Тип("Число")) Тогда
		Возврат;
	КонецЕсли;

	Попытка
		ВыражениеИтоговыхЗаписей = сВО_ВыражениеИтоговыхЗаписей(ВыражениеДетальныхЗаписей);
		Формула.УстановитьВыражения(ВыражениеДетальныхЗаписей, ВыражениеИтоговыхЗаписей);
	Исключение
		ВыражениеИтоговыхЗаписей = СтрШаблон("Сумма(%1)", ВыражениеДетальныхЗаписей);

		Формула.УстановитьВыражения(ВыражениеДетальныхЗаписей, ВыражениеИтоговыхЗаписей);
	КонецПопытки;
КонецПроцедуры

Функция сВО_ВыражениеИтоговыхЗаписей(Знач Выражение)
	ОписаниеВыражения		= Новый Массив;

	АрифметическиеОператоры	= "+-*/%";
	КоличествоСимволов		= СтрДлина(Выражение);

	Для НомерСимвола = 1 По КоличествоСимволов Цикл
		Если КоличествоСимволов = 0 Тогда
			Прервать;
		КонецЕсли;

		Символ = Сред(Выражение, НомерСимвола, 1);

		Если Не ЗначениеЗаполнено(Символ) Тогда
			Продолжить;
		КонецЕсли;

		Если Символ = "(" Тогда
			ОписаниеВыражения.Добавить(Символ);

			Выражение			= СокрЛП(Сред(Выражение, НомерСимвола + 1));

			НомерСимвола		= 0;
			КоличествоСимволов	= СтрДлина(Выражение);
		ИначеЕсли СтрНайти(АрифметическиеОператоры, Символ) > 0 Тогда
			Операнд = СокрЛП(Сред(Выражение, 1, НомерСимвола - 1));

			Если БазоваяПодсистемаКлиентСервер.ОН_ЭтоЧисло(Операнд) Тогда
				ВложенноеВыражение = Операнд;
			Иначе
				ВложенноеВыражение = СтрШаблон("Сумма(%1)", Операнд);
			КонецЕсли;

			ОписаниеВыражения.Добавить(ВложенноеВыражение);
			ОписаниеВыражения.Добавить(Символ);

			Выражение			= СокрЛП(Сред(Выражение, НомерСимвола + 1));

			НомерСимвола		= 0;
			КоличествоСимволов	= СтрДлина(Выражение);
		КонецЕсли;
	КонецЦикла;

	Если БазоваяПодсистемаКлиентСервер.ОН_ЭтоЧисло(Выражение) Тогда
		ВложенноеВыражение = Выражение;
	Иначе
		ВложенноеВыражение = СтрШаблон("Сумма(%1)", Выражение);
	КонецЕсли;

	ОписаниеВыражения.Добавить(ВложенноеВыражение);

	ВыражениеИтоговыхЗаписей = "";

	Для Каждого Элемент Из ОписаниеВыражения Цикл
		ВыражениеИтоговыхЗаписей = ВыражениеИтоговыхЗаписей + " " + Элемент;
	КонецЦикла;

	Возврат СокрЛП(ВыражениеИтоговыхЗаписей);
КонецФункции

Процедура сВО_Сортировать(Форма, Команда, СвойстваЗаголовка = Неопределено) Экспорт
	Если СвойстваЗаголовка = Неопределено Тогда
		СвойстваЗаголовка = сВО_СвойстваЗаголовкаОтчета(Форма);
	КонецЕсли;

	Если Не сВО_ТипСортировкиРазделаДоступен(Команда, СвойстваЗаголовка) Тогда
		Возврат;
	КонецЕсли;

	Настройки = сВО_ИспользуемыеНастройки(Форма, СвойстваЗаголовка.ИдентификаторНастроек);

	сВО_СброситьСортировку(Настройки, СвойстваЗаголовка);

	Группировка = Настройки.ПолучитьОбъектПоИдентификатору(СвойстваЗаголовка.ИдентификаторГруппировки);
	Если ТипЗнч(Группировка) = Тип("НастройкиВложенногоОбъектаКомпоновкиДанных") Тогда
		ЭлементыСортировки = Группировка.Настройки.Порядок.Элементы;
	ИначеЕсли СвойстваЗаголовка.КоличествоРазделов = 1 Тогда
		ЭлементыСортировки = Настройки.Порядок.Элементы;
	Иначе
		Раздел		= Настройки.ПолучитьОбъектПоИдентификатору(СвойстваЗаголовка.ИдентификаторРаздела);
		Если ТипЗнч(Раздел) = Тип("ТаблицаКомпоновкиДанных") И Раздел.Строки.Количество() = 1 Тогда
			ОсновнаяГруппировка	= Раздел.Строки[0];
			ПолеГруппировки		= ВариантыОтчетовКлиентСервер.сВО_ПолеОтчета(ОсновнаяГруппировка.ПоляГруппировки, СвойстваЗаголовка.Поле);
			Если СвойстваЗаголовка.Ресурс Или ПолеГруппировки <> Неопределено Тогда
				Группировка = ОсновнаяГруппировка;
			КонецЕсли;
		КонецЕсли;

		ЭлементыСортировки = Группировка.Порядок.Элементы;
	КонецЕсли;

	ЭлементСортировки = сВО_ЭлементСортировкиРаздела(ЭлементыСортировки, СвойстваЗаголовка.Поле);

	Если ЭлементСортировки = Неопределено Тогда
		Индекс					= сВО_ИндексЭлементСортировкиРаздела(ЭлементыСортировки, СвойстваЗаголовка.Поле);
		ЭлементСортировки		= ЭлементыСортировки.Вставить(Индекс, Тип("ЭлементПорядкаКомпоновкиДанных"));
		ЭлементСортировки.Поле	= СвойстваЗаголовка.Поле;
	КонецЕсли;

	ЭлементСортировки.ТипУпорядочивания	= сВО_ТипСортировкиРаздела(Команда);
	ЭлементСортировки.Использование		= Истина;

	сВО_ОповеститьОЗавершенииКонтекстнойНастройки(Форма, сВО_ДействиеКоманды(Команда), Ложь);
КонецПроцедуры

Функция сВО_ЗаголовкиОтчета(Форма)
	Если СтрЗаканчиваетсяНа(Форма.ИмяФормы, "Форма") Или СтрЗаканчиваетсяНа(Форма.ИмяФормы, "ФормаОтчета") Или СтрЗаканчиваетсяНа(Форма.ИмяФормы, "КонтекстнаяНастройкаОтчета") Тогда
		НастройкиОтчета		= Форма.НастройкиОтчета;
		СвойстваРезультата	= НастройкиОтчета.СвойстваРезультата;

		Возврат СвойстваРезультата.Заголовки;
	КонецЕсли;

	Возврат Форма.ЗаголовкиОтчета;
КонецФункции

Функция сВО_СвойстваЗаголовкаОтчета(Форма)
	Если СтрЗаканчиваетсяНа(Форма.ИмяФормы, "Форма") Или СтрЗаканчиваетсяНа(Форма.ИмяФормы, "ФормаОтчета") Тогда
		Заголовки	= сВО_ЗаголовкиОтчета(Форма);
		Поле		= Форма.ТекущийЭлемент; // ПолеФормы, РасширениеПоляФормыДляПоляТабличногоДокумента
		Область		= Поле.ТекущаяОбласть; // ОбластьЯчеекТабличногоДокумента

		Возврат Заголовки[Область.Имя];
	КонецЕсли;

	Возврат Форма.СвойстваЗаголовка;
КонецФункции

Процедура сВО_Фильтровать(Форма, ВидСравнения, СвойстваЗаголовка, ДанныеРасшифровки)
	Если ТипЗнч(ВидСравнения) = Тип("Строка") Тогда
		сВО_ПоказатьРасширеннуюНастройкуФильтра(Форма, СвойстваЗаголовка);

		Возврат;
	КонецЕсли;

	ЭтоГруппировка = (ДанныеРасшифровки.Тип = "Группировка");

	Настройки = сВО_ИспользуемыеНастройки(Форма, СвойстваЗаголовка.ИдентификаторНастроек);

	Фильтры	= ВариантыОтчетовКлиентСервер.сВО_ФильтрыРазделаОтчета(Настройки, СвойстваЗаголовка, ЭтоГруппировка);
	Фильтр	= ВариантыОтчетовКлиентСервер.сВО_ФильтрРазделаОтчета(Фильтры, СвойстваЗаголовка.Поле);

	Если Фильтр = Неопределено Тогда
		Фильтр					= Фильтры.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		Фильтр.ЛевоеЗначение	= СвойстваЗаголовка.Поле;
	КонецЕсли;

	Фильтр.ВидСравнения		= ВидСравнения;
	Фильтр.ПравоеЗначение	= ДанныеРасшифровки.Значение;
	Фильтр.Использование	= Истина;

	сВО_ОповеститьОЗавершенииКонтекстнойНастройки(Форма, "Фильтровать");
КонецПроцедуры

Функция сВО_ТипСортировкиРазделаДоступен(Команда, СвойстваЗаголовка)
	ТекстПредупреждения		= "";
	ШаблонПредупреждения	= "Поле ""%1"" уже отсортировано по %2";

	Если СтрЗаканчиваетсяНа(сВО_ДействиеКоманды(Команда), "ПоВозрастанию") И Не СвойстваЗаголовка.СортироватьПоВозрастанию Тогда
		ТекстПредупреждения = СтрШаблон(ШаблонПредупреждения, СвойстваЗаголовка.Текст, "возрастанию");
	ИначеЕсли СтрЗаканчиваетсяНа(сВО_ДействиеКоманды(Команда), "ПоУбыванию") И Не СвойстваЗаголовка.СортироватьПоУбыванию Тогда
		ТекстПредупреждения = СтрШаблон(ШаблонПредупреждения, СвойстваЗаголовка.Текст, "убыванию");
	КонецЕсли;

	Если ЗначениеЗаполнено(ТекстПредупреждения) Тогда
		ПоказатьПредупреждение(, ТекстПредупреждения);

		Возврат Ложь;
	КонецЕсли;

	Возврат Истина;
КонецФункции

Функция сВО_ТипСортировкиРаздела(Команда)
	Если СтрЗаканчиваетсяНа(сВО_ДействиеКоманды(Команда), "ПоВозрастанию") Тогда
		Возврат НаправлениеСортировкиКомпоновкиДанных.Возр;
	КонецЕсли;

	Возврат НаправлениеСортировкиКомпоновкиДанных.Убыв;
КонецФункции

Функция сВО_КомпоновщикНастроекОтчета(Форма, РазвернутьАвтоПоля = Истина)
	Если СтрЗаканчиваетсяНа(Форма.ИмяФормы, "КонтекстнаяНастройкаОтчета") Или СтрЗаканчиваетсяНа(Форма.ИмяФормы, "КонтекстнаяНастройкаОтчета") Тогда
		КомпоновщикНастроек	= Форма.КомпоновщикНастроек;
	Иначе
		НастройкиОтчета		= Форма.НастройкиОтчета;
		СвойстваРезультата	= НастройкиОтчета.СвойстваРезультата;

		КомпоновщикНастроек	= СвойстваРезультата.КомпоновщикНастроек;
	КонецЕсли;

	Если РазвернутьАвтоПоля Тогда
		КомпоновщикНастроек.РазвернутьАвтоПоля();
	КонецЕсли;

	Возврат КомпоновщикНастроек;
КонецФункции

Функция сВО_ИспользуемыеНастройки(Форма, ИдентификаторНастроек = Неопределено, РазвернутьАвтоПоля = Истина)
	КомпоновщикНастроек		= сВО_КомпоновщикНастроекОтчета(Форма, РазвернутьАвтоПоля);
	ИспользуемыеНастройки	= КомпоновщикНастроек.Настройки;

	Если ТипЗнч(ИдентификаторНастроек) <> Тип("ИдентификаторКомпоновкиДанных") Тогда
		Возврат ИспользуемыеНастройки;
	КонецЕсли;

	НастройкиПоИдентификатору = ИспользуемыеНастройки.ПолучитьОбъектПоИдентификатору(ИдентификаторНастроек);

	Если НастройкиПоИдентификатору = Неопределено Тогда
		Возврат ИспользуемыеНастройки;
	КонецЕсли;

	Возврат НастройкиПоИдентификатору;
КонецФункции

Функция сВО_ЭлементСортировкиРаздела(ЭлементыСортировки, Поле)
	ЭлементСортировки = Неопределено;

	Для Каждого Элемент Из ЭлементыСортировки Цикл
		Если ТипЗнч(Элемент) <> Тип("АвтоЭлементПорядкаКомпоновкиДанных") И Элемент.Поле = Поле Тогда
			ЭлементСортировки = Элемент;

			Прервать;
		КонецЕсли;
	КонецЦикла;

	Возврат ЭлементСортировки;
КонецФункции

Функция сВО_ИндексЭлементСортировкиРаздела(ЭлементыСортировки, Поле)
	Для Каждого Элемент Из ЭлементыСортировки Цикл
		Если ТипЗнч(Элемент) = Тип("АвтоЭлементПорядкаКомпоновкиДанных") Тогда
			Возврат ЭлементыСортировки.Индекс(Элемент);
		КонецЕсли;
	КонецЦикла;

	Возврат ЭлементыСортировки.Количество();
КонецФункции

Функция сВО_ДействиеКоманды(Команда)
	Если Команда = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;

	Возврат ?(ЗначениеЗаполнено(Команда.Действие), Команда.Действие, Команда.Имя);
КонецФункции

Функция сВО_ДетальныеЗаписи(ЭлементыСтруктуры, ДетальныеЗаписи = Неопределено)
	Для Каждого Элемент Из ЭлементыСтруктуры Цикл
		ТипЭлемента = ТипЗнч(Элемент);

		Если ТипЭлемента = Тип("ГруппировкаКомпоновкиДанных") Или ТипЭлемента = Тип("ГруппировкаТаблицыКомпоновкиДанных") Тогда
			ПоляГруппировки = Элемент.ПоляГруппировки.Элементы;

			Если ПоляГруппировки.Количество() = 0 Тогда
				ДетальныеЗаписи = Элемент;
			Иначе
				сВО_ДетальныеЗаписи(Элемент.Структура, ДетальныеЗаписи);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;

	Возврат ДетальныеЗаписи;
КонецФункции

Процедура сВО_ОповеститьОЗавершенииКонтекстнойНастройки(Форма, Действие, РазвернутьАвтоПоля = Истина)
	ЭтоФормОтчета = СтрЗаканчиваетсяНа(Форма.ИмяФормы, "Форма") Или СтрЗаканчиваетсяНа(Форма.ИмяФормы, "ФормаОтчета");

	ИспользуемыеНастройки	= сВО_ИспользуемыеНастройки(Форма, Неопределено, РазвернутьАвтоПоля);
	КомпоновщикНастроек		= сВО_КомпоновщикНастроекОтчета(Форма, РазвернутьАвтоПоля);
	КомпоновщикНастроек.ЗагрузитьНастройки(ИспользуемыеНастройки);

	ИдентификаторВладельца = ?(ЭтоФормОтчета, Форма.УникальныйИдентификатор, Форма.ВладелецФормы.УникальныйИдентификатор);

	Результат = сВО_СтандартныйРезультатНастройкиИзКонтекстногоМеню(КомпоновщикНастроек, Действие, ИдентификаторВладельца);

	Если ЭтоФормОтчета Тогда
		Оповестить(Действие, Результат, ЭтотОбъект);
	Иначе
		Форма.ОповеститьОВыборе(Результат);
	КонецЕсли;
КонецПроцедуры

Функция сВО_СтандартныйРезультатНастройкиИзКонтекстногоМеню(КомпоновщикНастроек, Действие, ИдентификаторВладельца) Экспорт
	Результат = Новый Структура;
	Результат.Вставить("КомпоновщикНастроекКД",						КомпоновщикНастроек);
	Результат.Вставить("Действие",									Действие);
	Результат.Вставить("ИдентификаторВладельца",					ИдентификаторВладельца);
	Результат.Вставить("Переформировать",							СтрЗаканчиваетсяНа(Действие, "Сформировать"));
	Результат.Вставить("УчитыватьВремяФормирования",				Истина);
	Результат.Вставить("ВариантМодифицирован",						Истина);
	Результат.Вставить("ПользовательскиеНастройкиМодифицированы",	Истина);
	Результат.Вставить("СброситьПользовательскиеНастройки",			Истина);

	Возврат Результат;
КонецФункции

Процедура сВО_ВыбратьПолеОтчетаИзМеню(Форма, Команда, ИмяКоллекции = "ВыбранныеПоля") Экспорт
	Если Не сВО_ДействиеДоступно(Форма, Команда) Тогда
		Возврат;
	КонецЕсли;

	Обработчик	= сВО_ОбработчикВыбораПоляОтчета(Форма, сВО_ДействиеКоманды(Команда));
	сВО_УточнитьИмяКоллекции(ИмяКоллекции, Обработчик);

	ПоляОтчета = сВО_ПоляОтчета(Форма, Команда, ИмяКоллекции);

	Если ПоляОтчета = Неопределено Тогда
		Возврат;
	КонецЕсли;

	Если ПоляОтчета.Количество() > 20 Или ПоляОтчета.Количество() = 1 И ПоляОтчета.НайтиПоЗначению("Еще") <> Неопределено Тогда
		сВО_ВыбратьПолеОтчета(Форма, сВО_ДействиеКоманды(Команда), ИмяКоллекции, Обработчик);

		Возврат;
	КонецЕсли;

	Форма.ПоказатьВыборИзМеню(Обработчик, ПоляОтчета, Форма.ТекущийЭлемент);
КонецПроцедуры

Процедура сВО_ПереместитьПолеГоризонтально(Форма, Команда) Экспорт
	Если Не сВО_ДействиеДоступно(Форма, Команда) Тогда
		Возврат;
	КонецЕсли;

	СвойстваЗаголовка = сВО_СвойстваЗаголовкаОтчета(Форма);

	КомпоновщикНастроек	= сВО_КомпоновщикНастроекОтчета(Форма);
	Настройки			= сВО_ИспользуемыеНастройки(Форма, СвойстваЗаголовка.ИдентификаторНастроек);

	Группировка			= Настройки.ПолучитьОбъектПоИдентификатору(СвойстваЗаголовка.ИдентификаторГруппировки);

	КомпоновщикНастроек.РазвернутьАвтоПоля();

	Поля		= Группировка.Выбор;
	ТекущееПоле	= ВариантыОтчетовКлиентСервер.сВО_ПолеОтчета(Поля, СвойстваЗаголовка.Поле);

	ИсходныйИндексПоля = Поля.Элементы.Индекс(ТекущееПоле);

	Если сВО_ДействиеКоманды(Команда) = "ПереместитьПолеВлево" Тогда
		НаправлениеСдвига = -1
	ИначеЕсли сВО_ДействиеКоманды(Команда) = "ПереместитьПолеВправо" Тогда
		НаправлениеСдвига = 1
	КонецЕсли;

	КонечныйИндексПоля = ИсходныйИндексПоля + НаправлениеСдвига;

	Если КонечныйИндексПоля >= 0 Тогда
		СоседнееПоле = Поля.Элементы[КонечныйИндексПоля];

		Пока КонечныйИндексПоля > 0 И Не СоседнееПоле.Использование Цикл
			КонечныйИндексПоля	= КонечныйИндексПоля + НаправлениеСдвига;
			СоседнееПоле		= Поля.Элементы[КонечныйИндексПоля];
		КонецЦикла;
	КонецЕсли;

	Поля.Элементы.Сдвинуть(ТекущееПоле, КонечныйИндексПоля - ИсходныйИндексПоля);

	сВО_ОповеститьОЗавершенииКонтекстнойНастройки(Форма, сВО_ДействиеКоманды(Команда));
КонецПроцедуры

Процедура сВО_ПереместитьПолеВертикально(Форма, Команда, Ответ = Неопределено) Экспорт
	Если Не сВО_ДействиеДоступно(Форма, Команда) Тогда
		Возврат;
	КонецЕсли;

	СвойстваЗаголовка = сВО_СвойстваЗаголовкаОтчета(Форма);

	КомпоновщикНастроек	= сВО_КомпоновщикНастроекОтчета(Форма);
	Настройки			= сВО_ИспользуемыеНастройки(Форма, СвойстваЗаголовка.ИдентификаторНастроек);

	Группировка = Настройки.ПолучитьОбъектПоИдентификатору(СвойстваЗаголовка.ИдентификаторГруппировки);

	КомпоновщикНастроек.РазвернутьАвтоПоля();

	ПолеГруппировки	= ВариантыОтчетовКлиентСервер.сВО_ПолеОтчета(Группировка.ПоляГруппировки, СвойстваЗаголовка.Поле);
	ВыбранноеПоле	= ВариантыОтчетовКлиентСервер.сВО_ПолеОтчета(Группировка.Выбор, СвойстваЗаголовка.Поле,, СвойстваЗаголовка.Текст);

	СоседняяГруппировка = сВО_СоседняяГруппировка(Форма, Команда, Настройки, СвойстваЗаголовка.ПорядокГруппировки);

	Если СоседняяГруппировка = Неопределено Тогда
		Возврат;
	КонецЕсли;

	ИсходныйИндексПоля	= Группировка.Выбор.Элементы.Индекс(ВыбранноеПоле);

	Поля				= СоседняяГруппировка.ПоляГруппировки;
	КоличествоПолей		= Поля.Элементы.Количество();

	Если КоличествоПолей > 0 Тогда
		ИндексПоля	= ?(ИсходныйИндексПоля < КоличествоПолей, ИсходныйИндексПоля, КоличествоПолей);
		НовоеПоле	= Поля.Элементы.Вставить(ИндексПоля, Тип("ПолеГруппировкиКомпоновкиДанных"));
		ЗаполнитьЗначенияСвойств(НовоеПоле, ?(ПолеГруппировки = Неопределено, ВыбранноеПоле, ПолеГруппировки));
	КонецЕсли;

	Поля					= СоседняяГруппировка.Выбор;
	ПараметрыПеремещения	= Новый Структура("Форма, Команда, Ответ", Форма, Команда, Ответ);
	ИндексПоля				= сВО_ИндексВыбранногоПоля(ИсходныйИндексПоля, Поля, СвойстваЗаголовка.Поле, ПараметрыПеремещения);

	Если ИндексПоля = Неопределено Тогда
		Возврат;
	КонецЕсли;

	НовоеПоле	= Поля.Элементы.Вставить(ИндексПоля, Тип("ВыбранноеПолеКомпоновкиДанных"));

	ЗаполнитьЗначенияСвойств(НовоеПоле, ?(ВыбранноеПоле = Неопределено, ПолеГруппировки, ВыбранноеПоле));

	сВО_УстановитьВыводРеквизитовГруппировкиОтдельно(СоседняяГруппировка);

	Если ПолеГруппировки <> Неопределено Тогда
		Группировка.ПоляГруппировки.Элементы.Удалить(ПолеГруппировки);
	КонецЕсли;

	Если ВыбранноеПоле <> Неопределено Тогда
		Если Группировка.ПоляГруппировки.Элементы.Количество() = 0 Тогда
			ВыбранноеПоле.Использование = Ложь;
			Если сВО_КоличествоИспользуемыхПолей(Группировка.Выбор) = 0 Тогда
				сВО_СкрытьГруппировку(Настройки, Группировка, СвойстваЗаголовка.Поле);
			КонецЕсли;
		Иначе
			Группировка.Выбор.Элементы.Удалить(ВыбранноеПоле);
		КонецЕсли;
	КонецЕсли;

	сВО_ОповеститьОЗавершенииКонтекстнойНастройки(Форма, сВО_ДействиеКоманды(Команда));
КонецПроцедуры

Функция сВО_ОбработчикВыбораПоляОтчета(Форма, Действие)
	ПараметрыОбработчика = Новый Структура;
	ПараметрыОбработчика.Вставить("Форма",				Форма);
	ПараметрыОбработчика.Вставить("Действие",			Действие);
	ПараметрыОбработчика.Вставить("СвойстваЗаголовка",	сВО_СвойстваЗаголовкаОтчета(Форма));

	Возврат Новый ОписаниеОповещения("ПослеВыбораПоля", Форма, ПараметрыОбработчика);
КонецФункции

Процедура сВО_УточнитьИмяКоллекции(ИмяКоллекции, Обработчик)
	ДополнительныеПараметры	= Обработчик.ДополнительныеПараметры;
	СвойстваЗаголовка		= ДополнительныеПараметры.СвойстваЗаголовка;

	Если СвойстваЗаголовка = Неопределено Тогда
		Возврат;
	КонецЕсли;

	Если СвойстваЗаголовка.ТипПоля = Тип("ПолеГруппировкиКомпоновкиДанных") Тогда
		ИмяКоллекции = "ПоляГруппировки";
	КонецЕсли;

	ДополнительныеПараметры.Вставить("ИмяКоллекции", ИмяКоллекции);
КонецПроцедуры

Функция сВО_ПоляОтчета(Форма, Команда, ИмяКоллекции)
	ДоступныеПоля	= сВО_ДоступныеПоляОтчета(сВО_КомпоновщикНастроекОтчета(Форма), ИмяКоллекции);
	ПоляОтчета		= сВО_ОсновныеПоляОтчета(Форма.НастройкиОтчета.СвойстваРезультата, ДоступныеПоля);

	Если ПоляОтчета.Количество() = 0 Тогда
		сВО_ДобавитьДоступныеПоляОтчета(ПоляОтчета, ДоступныеПоля);
	КонецЕсли;

	сВО_ИсключитьИспользуемыеПоляОтчета(ПоляОтчета, Форма, Команда);
	сВО_ИсключитьНедоступныеПоляОтчета(ПоляОтчета, Форма, Команда);

	СвойстваЗаголовка = сВО_СвойстваЗаголовкаОтчета(Форма);

	Если СвойстваЗаголовка = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;

	Если ПоляОтчета.НайтиПоЗначению("Еще") = Неопределено Тогда
		ПоляОтчета.Добавить("Еще", "Еще...");
	КонецЕсли;

	Возврат ПоляОтчета;
КонецФункции

Процедура сВО_ВыбратьПолеОтчета(Форма, Действие, ИмяКоллекции, Обработчик = Неопределено, Поле = Неопределено, ИдентификаторУзлаНастроек = Неопределено)
	Если Обработчик = Неопределено Тогда
		Обработчик = сВО_ОбработчикВыбораПоляОтчета(Форма, Действие);
	КонецЕсли;

	ПараметрыВыбора = Новый Структура;
	ПараметрыВыбора.Вставить("НастройкиОтчета",							Форма.НастройкиОтчета);
	ПараметрыВыбора.Вставить("КомпоновщикНастроек",						сВО_КомпоновщикНастроекОтчета(Форма));
	ПараметрыВыбора.Вставить("Режим",									ИмяКоллекции);
	ПараметрыВыбора.Вставить("ПолеКД",									Поле);
	ПараметрыВыбора.Вставить("ИдентификаторЭлементаСтруктурыНастроек",	сВО_ИдентификаторЭлементаСтруктурыНастроек(Форма, ИдентификаторУзлаНастроек));

	ОткрытьФорму("ХранилищеНастроек.ХранилищеВариантовОтчетов.Форма.ВыборПоляОтчета", ПараметрыВыбора, Форма, Форма.УникальныйИдентификатор,,, Обработчик);
КонецПроцедуры

Функция сВО_ДоступныеПоляОтчета(КомпоновщикНастроек, ИмяКоллекции = "Выбор")
	Если ИмяКоллекции = "Отборы" Тогда
		Возврат КомпоновщикНастроек.Настройки.ДоступныеПоляОтбора;
	ИначеЕсли ИмяКоллекции = "Сортировка" Тогда
		Возврат КомпоновщикНастроек.Настройки.ДоступныеПоляПорядка;
	ИначеЕсли ИмяКоллекции = "ПоляГруппировки" Тогда
		Возврат КомпоновщикНастроек.Настройки.ДоступныеПоляГруппировок;
	КонецЕсли;

	Возврат КомпоновщикНастроек.Настройки.ДоступныеПоляВыбора;
КонецФункции

Функция сВО_ОсновныеПоляОтчета(СвойстваРезультата, ДоступныеПоля)
	ОсновныеПоляИзДоступных = Новый СписокЗначений;

	ОсновныеПоля = СвойстваРезультата.ОсновныеПоля;

	Для Каждого Поле Из ОсновныеПоля Цикл
		ДоступноеПоле = ДоступныеПоля.НайтиПоле(Новый ПолеКомпоновкиДанных(Поле));

		Если ДоступноеПоле <> Неопределено И ОсновныеПоляИзДоступных.НайтиПоЗначению(ДоступноеПоле) = Неопределено Тогда
			ОсновныеПоляИзДоступных.Добавить(ДоступноеПоле, ДоступноеПоле.Заголовок,, сВО_КартинкаПоля(ДоступноеПоле.ТипЗначения));
		КонецЕсли;
	КонецЦикла;

	Возврат ОсновныеПоляИзДоступных;
КонецФункции

Процедура сВО_ДобавитьДоступныеПоляОтчета(ПоляОтчета, ДоступныеПоля)
	Для Каждого ДоступноеПоле Из ДоступныеПоля.Элементы Цикл
		Если Не ДоступноеПоле.Папка Тогда
			ПоляОтчета.Добавить(ДоступноеПоле, ДоступноеПоле.Заголовок,, сВО_КартинкаПоля(ДоступноеПоле.ТипЗначения));
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

Процедура сВО_ИсключитьИспользуемыеПоляОтчета(ОсновныеПоля, Форма, Команда)
	Владелец = Форма.ТекущийЭлемент;

	Если ТипЗнч(Владелец) <> Тип("КнопкаФормы") И (ТипЗнч(Владелец) <> Тип("ПолеФормы") Или Владелец.Вид <> ВидПоляФормы.ПолеТабличногоДокумента) Тогда
		Возврат;
	КонецЕсли;

	Если сВО_ДействиеКоманды(Команда) <> "ВставитьПолеСлева" И сВО_ДействиеКоманды(Команда) <> "ВставитьПолеСправа" Тогда
		Возврат;
	КонецЕсли;

	СвойстваЗаголовка	= сВО_СвойстваЗаголовкаОтчета(Форма);
	ИспользуемыеПоля	= сВО_ИспользуемыеПоляОтчета(СвойстваЗаголовка, Форма.НастройкиОтчета.СвойстваРезультата.ИндексПолей);

	Индекс = ОсновныеПоля.Количество() - 1;
	Пока Индекс >= 0 Цикл
		ДоступноеПоле = ОсновныеПоля[Индекс].Значение;

		Если ИспользуемыеПоля[ДоступноеПоле.Поле] <> Неопределено Тогда
			ОсновныеПоля.Удалить(Индекс);
		КонецЕсли;

		Индекс = Индекс - 1;
	КонецЦикла;
КонецПроцедуры

Функция сВО_ИспользуемыеПоляОтчета(СвойстваЗаголовка, ИндексПолей)
	ИндексПолейРаздела		= ИндексПолей[СвойстваЗаголовка.ПорядокРаздела];
	ИндексПолейГруппировки	= ИндексПолейРаздела[СвойстваЗаголовка.ПорядокГруппировки];

	Возврат ИндексПолейГруппировки;
КонецФункции

Функция сВО_КартинкаПоля(ТипЗначенияПоля)
	ДоступныеТипы = ТипЗначенияПоля.Типы();

	Если ДоступныеТипы.Количество() = 0 Тогда
		Возврат БиблиотекаКартинок.Пустая;
	КонецЕсли;

	Если ДоступныеТипы.Количество() > 1 Тогда
		Возврат БиблиотекаКартинок.ТипСоставнойОсновной;
	КонецЕсли;

	Если ТипЗначенияПоля.СодержитТип(Тип("Число")) Тогда
		Возврат БиблиотекаКартинок.ТипЧисло;
	КонецЕсли;

	Если ТипЗначенияПоля.СодержитТип(Тип("Строка")) Тогда
		Возврат БиблиотекаКартинок.ТипСтрока;
	КонецЕсли;

	Если ТипЗначенияПоля.СодержитТип(Тип("Дата")) Тогда
		Возврат БиблиотекаКартинок.ТипДата;
	КонецЕсли;

	Если ТипЗначенияПоля.СодержитТип(Тип("Булево")) Тогда
		Возврат БиблиотекаКартинок.ТипБулево;
	КонецЕсли;

	Если ТипЗначенияПоля.СодержитТип(Тип("УникальныйИдентификатор")) Тогда
		Возврат БиблиотекаКартинок.ТипИдентификатор;
	КонецЕсли;

	Возврат БиблиотекаКартинок.ТипСсылка;
КонецФункции

Процедура сВО_ИсключитьНедоступныеПоляОтчета(ОсновныеПоля, Форма, Команда)
	Владелец = Форма.ТекущийЭлемент;

	Если ТипЗнч(Владелец) <> Тип("КнопкаФормы") И (ТипЗнч(Владелец) <> Тип("ПолеФормы") Или Владелец.Вид <> ВидПоляФормы.ПолеТабличногоДокумента) Тогда
		Возврат;
	КонецЕсли;

	Если сВО_ДействиеКоманды(Команда) <> "ВставитьПолеСлева" И сВО_ДействиеКоманды(Команда) <> "ВставитьПолеСправа" Тогда
		Возврат;
	КонецЕсли;

	СвойстваЗаголовка	= сВО_СвойстваЗаголовкаОтчета(Форма);
	РолиПолей			= Форма.НастройкиОтчета.СвойстваРезультата.РолиПолей;

	Индекс = ОсновныеПоля.Количество() - 1;

	Пока Индекс >= 0 Цикл
		ДоступноеПоле = ОсновныеПоля[Индекс].Значение;

		Если СвойстваЗаголовка.Ресурс И Не ДоступноеПоле.Ресурс Тогда
			ОсновныеПоля.Удалить(Индекс);
		ИначеЕсли СвойстваЗаголовка.Период И РолиПолей.Периоды[ДоступноеПоле.Поле] = Неопределено Тогда
			ОсновныеПоля.Удалить(Индекс);
		ИначеЕсли Не СвойстваЗаголовка.Период И РолиПолей.Периоды[ДоступноеПоле.Поле] <> Неопределено Тогда
			ОсновныеПоля.Удалить(Индекс);
		ИначеЕсли СвойстваЗаголовка.ИспользуетсяВПоляхГруппировки И СвойстваЗаголовка.ПереместитьПолеВправо И сВО_ДействиеКоманды(Команда) = "ВставитьПолеСправа" И ДоступноеПоле.Ресурс Тогда
			ОсновныеПоля.Удалить(Индекс);
		ИначеЕсли СвойстваЗаголовка.ИспользуетсяВПоляхГруппировки И сВО_ДействиеКоманды(Команда) = "ВставитьПолеСлева" И ДоступноеПоле.Ресурс Тогда
			ОсновныеПоля.Удалить(Индекс);
		КонецЕсли;

		Индекс = Индекс - 1;
	КонецЦикла;
КонецПроцедуры

Функция сВО_ИдентификаторЭлементаСтруктурыНастроек(Форма, ИдентификаторУзлаНастроек)
	Если ИдентификаторУзлаНастроек <> Неопределено Тогда
		Возврат ИдентификаторУзлаНастроек;
	КонецЕсли;

	Если СтрЗаканчиваетсяНа(Форма.ИмяФормы, "Форма") Или СтрЗаканчиваетсяНа(Форма.ИмяФормы, "ФормаОтчета") Тогда
		Возврат Неопределено;
	КонецЕсли;

	Возврат Форма.ИдентификаторЭлементаСтруктурыНастроек;
КонецФункции

Функция сВО_СоседняяГруппировка(Форма, Команда, Настройки, ПорядокГруппировки)
	СоседняяГруппировка = Неопределено;

	Если сВО_ДействиеКоманды(Команда) = "ПереместитьПолеВыше" Тогда
		ПорядокСоседнейГруппировки = ПорядокГруппировки - 1;
	Иначе
		ПорядокСоседнейГруппировки = ПорядокГруппировки + 1;
	КонецЕсли;

	Заголовки = сВО_ЗаголовкиОтчета(Форма);

	Для Каждого Свойства Из Заголовки Цикл
		Если Свойства.Значение.ПорядокГруппировки = ПорядокСоседнейГруппировки Тогда
			СоседняяГруппировка = Настройки.ПолучитьОбъектПоИдентификатору(Свойства.Значение.ИдентификаторГруппировки);

			Прервать;
		КонецЕсли;
	КонецЦикла;

	Возврат СоседняяГруппировка;
КонецФункции

Функция сВО_ИндексВыбранногоПоля(ИсходныйИндексПоля, КонечнаяКоллекцияПолей, Поле, ПараметрыПеремещения)
	Граница = КонечнаяКоллекцияПолей.Элементы.Количество();

	НовоеПоле		= ВариантыОтчетовКлиентСервер.сВО_ПолеОтчета(КонечнаяКоллекцияПолей, Поле);
	ДоступноеПоле	= КонечнаяКоллекцияПолей.ДоступныеПоляВыбора.НайтиПоле(Поле);

	Если НовоеПоле <> Неопределено Тогда
		Если ПараметрыПеремещения.Ответ = Неопределено Тогда
			Обработчик = Новый ОписаниеОповещения("сВО_ПродолжитьПеремещениеПоляВертикально", ЭтотОбъект, ПараметрыПеремещения);

			ШаблонТекстаВопроса = "Группировка уже содержит подобное поле - %1.
				|Удалить поле?";

			ТекстВопроса = СтрШаблон(ШаблонТекстаВопроса, ?(ДоступноеПоле = Неопределено, Поле, ДоступноеПоле.Заголовок));

			ПоказатьВопрос(Обработчик, ТекстВопроса, РежимДиалогаВопрос.ДаНетОтмена,, КодВозвратаДиалога.Да);

			Возврат Неопределено;
		ИначеЕсли ПараметрыПеремещения.Ответ = КодВозвратаДиалога.Да Тогда
			КонечнаяКоллекцияПолей.Элементы.Удалить(НовоеПоле);
		КонецЕсли;
	КонецЕсли;

	Для Каждого Элемент Из КонечнаяКоллекцияПолей.Элементы Цикл
		Если ТипЗнч(Элемент) = Тип("АвтоВыбранноеПолеКомпоновкиДанных") Или ТипЗнч(Элемент) = Тип("ГруппаВыбранныхПолейКомпоновкиДанных") Тогда
			Продолжить;
		КонецЕсли;

		ДоступноеПоле = КонечнаяКоллекцияПолей.ДоступныеПоляВыбора.НайтиПоле(Элемент.Поле);

		Если ДоступноеПоле <> Неопределено И ДоступноеПоле.Ресурс Тогда
			Граница = КонечнаяКоллекцияПолей.Элементы.Индекс(Элемент);

			Прервать;
		КонецЕсли;
	КонецЦикла;

	Возврат ?(ИсходныйИндексПоля < Граница, ИсходныйИндексПоля, Граница);
КонецФункции

Процедура сВО_ПродолжитьПеремещениеПоляВертикально(Ответ, ПараметрыПеремещения) Экспорт
	Если Ответ = КодВозвратаДиалога.Да Или Ответ = КодВозвратаДиалога.Нет Тогда
		сВО_ПереместитьПолеВертикально(ПараметрыПеремещения.Форма, ПараметрыПеремещения.Команда, Ответ);
	КонецЕсли;
КонецПроцедуры

Функция сВО_СвойстваОбластиРасшифровки(Форма, Область, ИмяПоля)
	НастройкиОтчета		= Форма.НастройкиОтчета;
	СвойстваРезультата	= НастройкиОтчета.СвойстваРезультата;
	СвойстваЗаголовка	= СвойстваРезультата.Заголовки[Область.Имя];

	СвойстваОбласти = Новый Структура;
	СвойстваОбласти.Вставить("ЭтоЗаголовок",		ТипЗнч(СвойстваЗаголовка) = Тип("Структура"));
	СвойстваОбласти.Вставить("СвойстваЗаголовка",	СвойстваЗаголовка);

	Если СвойстваОбласти.ЭтоЗаголовок Тогда
		Возврат СвойстваОбласти;
	КонецЕсли;

	ПорядокРаздела			= 0;
	ГраницаТекущегоРаздела	= 0;

	Для Каждого Граница Из СвойстваРезультата.ГраницыРазделов Цикл
		ПорядокРаздела = ПорядокРаздела + 1;

		Если Область.Верх >= ГраницаТекущегоРаздела И Область.Верх < Граница.Значение Тогда
			Прервать;
		КонецЕсли;

		ГраницаТекущегоРаздела = Граница.Значение;
	КонецЦикла;

	Поле = Новый ПолеКомпоновкиДанных(ИмяПоля);

	Для Каждого Элемент Из СвойстваРезультата.Заголовки Цикл
		СвойстваЗаголовка = Элемент.Значение;

		Если СвойстваЗаголовка.ПорядокРаздела = ПорядокРаздела И СвойстваЗаголовка.Поле = Поле И СвойстваЗаголовка.Лево = Область.Лево Тогда
			СвойстваОбласти.СвойстваЗаголовка = СвойстваЗаголовка;

			Возврат СвойстваОбласти;
		КонецЕсли;
	КонецЦикла;

	Возврат СвойстваОбласти;
КонецФункции

Функция сВО_КонтекстноеМенюОбластиЗаголовка()
	КонтекстноеМеню = Новый СписокЗначений;
	КонтекстноеМеню.Добавить(ДействиеОбработкиРасшифровкиКомпоновкиДанных.ОткрытьЗначение, "Открыть");

	// Фильтровать
	КонтекстноеМеню.Добавить("СнятьФильтр", "Снять фильтр");
	КонтекстноеМеню.Добавить("Фильтровать", "Фильтровать...",, БиблиотекаКартинок.ОтборКомпоновкиДанных);

	// Сортировать
	КонтекстноеМеню.Добавить(НаправлениеСортировкиКомпоновкиДанных.Возр, "Сортировать по возрастанию",,	БиблиотекаКартинок.СортироватьСтрокиПоВозрастанию);
	КонтекстноеМеню.Добавить(НаправлениеСортировкиКомпоновкиДанных.Убыв, "Сортировать по убыванию",,	БиблиотекаКартинок.СортироватьСтрокиПоУбыванию);

	// Оформить
	ПодменюОформления = Новый СписокЗначений;
	ПодменюОформления.Добавить("УстановитьВысотуСтроки",	"Высота строки...",,	БиблиотекаКартинок.ВысотаСтроки);
	ПодменюОформления.Добавить("УстановитьШиринуКолонки",	"Ширина колонки...",,	БиблиотекаКартинок.ШиринаКолонки);

	КонтекстноеМеню.Добавить(ПодменюОформления, "Оформить",, БиблиотекаКартинок.УсловноеОформлениеКомпоновкиДанных);

	Возврат КонтекстноеМеню;
КонецФункции

Функция сВО_КонтекстноеМенюОбластиДанных(СвойстваЗаголовка, ДоступныеВидыСравнения)
	КонтекстноеМеню = Новый СписокЗначений;
	КонтекстноеМеню.Добавить(ДействиеОбработкиРасшифровкиКомпоновкиДанных.ОткрытьЗначение, "Открыть");

	Если ТипЗнч(СвойстваЗаголовка) <> Тип("Структура") Тогда
		Возврат КонтекстноеМеню;
	КонецЕсли;

	Информация = Новый СистемнаяИнформация;

	Если БазоваяПодсистемаКлиентСервер.ОН_СравнитьВерсии(Информация.ВерсияПриложения, "8.3.16.0") >= 0 Тогда
		КартинкиОформления = Новый Структура;
		КартинкиОформления.Вставить("ОформитьКрасным",	БиблиотекаКартинок["ОформлениеКругКрасный"]);
		КартинкиОформления.Вставить("ОформитьЖелтым",	БиблиотекаКартинок["ОформлениеКругЖелтый"]);
		КартинкиОформления.Вставить("ОформитьЗеленым",	БиблиотекаКартинок["ОформлениеКругЗеленый"]);
	Иначе
		ПустаяКартинка = БиблиотекаКартинок["Пустая"];

		КартинкиОформления = Новый Структура;
		КартинкиОформления.Вставить("ОформитьКрасным",	ПустаяКартинка);
		КартинкиОформления.Вставить("ОформитьЖелтым",	ПустаяКартинка);
		КартинкиОформления.Вставить("ОформитьЗеленым",	ПустаяКартинка);
	КонецЕсли;

	// Фильтровать
	сВО_УточнитьДоступныеВидыСравнения(ДоступныеВидыСравнения);
	КонтекстноеМеню.Добавить(ДоступныеВидыСравнения, "Фильтровать",, БиблиотекаКартинок.ОтборКомпоновкиДанных);

	// Сортировать
	КонтекстноеМеню.Добавить(НаправлениеСортировкиКомпоновкиДанных.Возр, "Сортировать по возрастанию",,	БиблиотекаКартинок.СортироватьСтрокиПоВозрастанию);
	КонтекстноеМеню.Добавить(НаправлениеСортировкиКомпоновкиДанных.Убыв, "Сортировать по убыванию",,	БиблиотекаКартинок.СортироватьСтрокиПоУбыванию);

	// Оформить
	ПодменюОформления = Новый СписокЗначений;
	ПодменюОформления.Добавить("ОчиститьОформление",		"Очистить оформление");
	ПодменюОформления.Добавить("ОформитьКрасным",			"Красным",,				КартинкиОформления.ОформитьКрасным);
	ПодменюОформления.Добавить("ОформитьЖелтым",			"Желтым",,				КартинкиОформления.ОформитьЖелтым);
	ПодменюОформления.Добавить("ОформитьЗеленым",			"Зеленым",,				КартинкиОформления.ОформитьЗеленым);
	ПодменюОформления.Добавить("ОформитьОтрицательные",		"Отрицательные красным");
	ПодменюОформления.Добавить("ОформитьПоложительные",		"Положительные зеленым");
	ПодменюОформления.Добавить("УстановитьВысотуСтроки",	"Высота строки...",,	БиблиотекаКартинок.ВысотаСтроки);
	ПодменюОформления.Добавить("УстановитьШиринуКолонки",	"Ширина колонки...",,	БиблиотекаКартинок.ШиринаКолонки);
	ПодменюОформления.Добавить("ОформитьЕще",				"Еще...");

	КонтекстноеМеню.Добавить(ПодменюОформления, "Оформить",, БиблиотекаКартинок.УсловноеОформлениеКомпоновкиДанных);

	Возврат КонтекстноеМеню;
КонецФункции

Процедура сВО_УточнитьДоступныеВидыСравнения(ДоступныеВидыСравнения)
	НедоступныеВидыСравнения = Новый Массив;
	НедоступныеВидыСравнения.Добавить(ВидСравненияКомпоновкиДанных.ВСписке);
	НедоступныеВидыСравнения.Добавить(ВидСравненияКомпоновкиДанных.НеВСписке);
	НедоступныеВидыСравнения.Добавить(ВидСравненияКомпоновкиДанных.ВСпискеПоИерархии);
	НедоступныеВидыСравнения.Добавить(ВидСравненияКомпоновкиДанных.НеВСпискеПоИерархии);

	Для Каждого Вид Из НедоступныеВидыСравнения Цикл
		НайденныйВид = ДоступныеВидыСравнения.НайтиПоЗначению(Вид);

		Если НайденныйВид <> Неопределено Тогда
			ДоступныеВидыСравнения.Удалить(НайденныйВид);
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

Процедура сВО_УстановитьВыводРеквизитовГруппировкиОтдельно(Группировка)
	Если ТипЗнч(Группировка) <> Тип("ГруппировкаКомпоновкиДанных") И Не ТипЗнч(Группировка) <> Тип("ГруппировкаТаблицыКомпоновкиДанных") Тогда
		Возврат;
	КонецЕсли;

	ВыводРеквизитов = Группировка.ПараметрыВывода.Элементы.Найти("РасположениеРеквизитов");

	Если Не ВыводРеквизитов.Использование Тогда
		ВыводРеквизитов.Значение		= РасположениеРеквизитовКомпоновкиДанных.Отдельно;
		ВыводРеквизитов.Использование	= Истина;
	КонецЕсли;
КонецПроцедуры

Процедура сВО_СкрытьГруппировку(Настройки, Группировка, Поле)
	Поля = Группировка.ПоляГруппировки;

	КоличествоИспользуемыхПолей = 0;

	Для Каждого Элемент Из Поля.Элементы Цикл
		Если Элемент.Использование Тогда
			КоличествоИспользуемыхПолей = КоличествоИспользуемыхПолей + 1;
		КонецЕсли;
	КонецЦикла;

	Если КоличествоИспользуемыхПолей > 0 Тогда
		Возврат;
	КонецЕсли;

	Группировка.Использование	= Ложь;
	РодительскаяГруппировка		= Группировка.Родитель;

	Если ТипЗнч(РодительскаяГруппировка) = Тип("ТаблицаКомпоновкиДанных") Тогда
		ИдентификаторГруппировки = Настройки.ПолучитьИдентификаторПоОбъекту(Группировка);

		Если СтрНайти(ИдентификаторГруппировки, "/row/") > 0 Тогда
			Группировки = РодительскаяГруппировка.Строки;
		Иначе
			Группировки = РодительскаяГруппировка.Колонки;
		КонецЕсли;
	Иначе
		Группировки = РодительскаяГруппировка.Структура;
	КонецЕсли;

	ИндексГруппировки = Группировки.Индекс(Группировка);

	Для Каждого ДочерняяГруппировка Из Группировка.Структура Цикл
		Если Не ДочерняяГруппировка.Использование Тогда
			Продолжить;
		КонецЕсли;

		ПоискЭлементов = Новый Соответствие;
		ВариантыОтчетовКлиентСервер.О_СкопироватьРекурсивно(Настройки, ДочерняяГруппировка, Группировки, ИндексГруппировки, ПоискЭлементов);

		ДочерняяГруппировка.Использование = Ложь;
	КонецЦикла;
КонецПроцедуры

Функция сВО_КоличествоИспользуемыхПолей(Поля, Количество = Неопределено)
	Если Количество = Неопределено Тогда
		Количество = 0;
	КонецЕсли;

	Для Каждого Элемент Из Поля.Элементы Цикл
		Если ТипЗнч(Элемент) = Тип("ГруппаВыбранныхПолейКомпоновкиДанных") Тогда
			сВО_КоличествоИспользуемыхПолей(Элемент, Количество);
		ИначеЕсли Элемент.Использование Тогда
			Количество = Количество + 1;
		КонецЕсли;
	КонецЦикла;

	Возврат Количество;
КонецФункции

Функция сВО_ОписаниеПоляОтчета(КомпоновщикНастроек, Поле, ИмяКоллекции = "Выбор")
	ДоступныеПоля = сВО_ДоступныеПоляОтчета(КомпоновщикНастроек, ИмяКоллекции);

	Возврат ДоступныеПоля.НайтиПоле(Поле);
КонецФункции

Процедура сВО_СкрытьПоле(Форма, Команда) Экспорт
	Если Не сВО_ДействиеДоступно(Форма, Команда) Тогда
		Возврат;
	КонецЕсли;

	СвойстваЗаголовка = сВО_СвойстваЗаголовкаОтчета(Форма);

	Если СвойстваЗаголовка.ИдентификаторНастроек = Неопределено Тогда
		Возврат;
	КонецЕсли;

	КомпоновщикНастроек	= сВО_КомпоновщикНастроекОтчета(Форма);
	Настройки			= сВО_ИспользуемыеНастройки(Форма, СвойстваЗаголовка.ИдентификаторНастроек);
	Раздел				= Настройки.ПолучитьОбъектПоИдентификатору(СвойстваЗаголовка.ИдентификаторРаздела);

	КомпоновщикНастроек.РазвернутьАвтоПоля();

	Группировка		= Настройки.ПолучитьОбъектПоИдентификатору(СвойстваЗаголовка.ИдентификаторГруппировки);
	ОписаниеПоля	= сВО_ОписаниеПоляОтчета(КомпоновщикНастроек, СвойстваЗаголовка.Поле);

	ЭтоГруппировкаДетальныхЗаписей = Группировка.ПоляГруппировки.Элементы.Количество() = 0;

	Если ОписаниеПоля <> Неопределено И ОписаниеПоля.Ресурс Тогда
		сВО_СкрытьВыбранноеПолеРаздела(Раздел, СвойстваЗаголовка.Поле);
	Иначе
		сВО_СкрытьВыбранноеПолеГруппировки(Группировка, СвойстваЗаголовка.Поле);

		Если Не ЭтоГруппировкаДетальныхЗаписей Тогда
			сВО_СкрытьГруппировку(Настройки, Группировка, СвойстваЗаголовка.Поле);
		КонецЕсли;
	КонецЕсли;

	сВО_ОповеститьОЗавершенииКонтекстнойНастройки(Форма, сВО_ДействиеКоманды(Команда));
КонецПроцедуры

Процедура сВО_СкрытьВыбранноеПолеРаздела(Раздел, Поле)
	ГруппировкиРаздела = Новый Массив;

	Если ТипЗнч(Раздел) = Тип("ТаблицаКомпоновкиДанных") Тогда
		ГруппировкиРаздела.Добавить(Раздел.Строки);
		ГруппировкиРаздела.Добавить(Раздел.Колонки);
	Иначе
		ГруппировкиРаздела.Добавить(Раздел.Структура);
		сВО_СкрытьВыбранноеПолеГруппировки(Раздел, Поле);
	КонецЕсли;

	Для Каждого Группировки Из ГруппировкиРаздела Цикл
		Для Каждого Группировка Из Группировки Цикл
			сВО_СкрытьВыбранноеПолеГруппировки(Группировка, Поле);
			сВО_СкрытьВыбранноеПолеРаздела(Группировка, Поле);
		КонецЦикла;
	КонецЦикла;
КонецПроцедуры

Процедура сВО_СкрытьВыбранноеПолеГруппировки(Группировка, Поле)
	Если ТипЗнч(Группировка) <> Тип("ГруппировкаКомпоновкиДанных") И ТипЗнч(Группировка) <> Тип("ГруппировкаТаблицыКомпоновкиДанных") Тогда
		Возврат;
	КонецЕсли;

	ПоляОтчета = ВариантыОтчетовКлиентСервер.сВО_ПолеОтчета(Группировка.Выбор, Поле);

	Если ПоляОтчета <> Неопределено Тогда
		ПоляОтчета.Использование = Ложь;
	КонецЕсли;

	ПоляОтчета = ВариантыОтчетовКлиентСервер.сВО_ПолеОтчета(Группировка.ПоляГруппировки, Поле);

	Если ПоляОтчета <> Неопределено Тогда
		ПоляОтчета.Использование = Ложь;
	КонецЕсли;

	ПоляОтчета = ВариантыОтчетовКлиентСервер.сВО_ПолеОтчета(Группировка.Порядок, Поле);

	Если ПоляОтчета <> Неопределено Тогда
		ПоляОтчета.Использование = Ложь;
	КонецЕсли;
КонецПроцедуры

Процедура сВО_ПереименоватьПоле(Форма, Команда, Заголовок = "") Экспорт
	Если Не сВО_ДействиеДоступно(Форма, Команда) Тогда
		Возврат;
	КонецЕсли;

	СвойстваЗаголовка = сВО_СвойстваЗаголовкаОтчета(Форма);

	КомпоновщикНастроек	= сВО_КомпоновщикНастроекОтчета(Форма);
	Настройки			= сВО_ИспользуемыеНастройки(Форма, СвойстваЗаголовка.ИдентификаторНастроек);
	Группировка			= Настройки.ПолучитьОбъектПоИдентификатору(СвойстваЗаголовка.ИдентификаторГруппировки);

	ОписаниеПоля		= сВО_ОписаниеПоляОтчета(КомпоновщикНастроек, СвойстваЗаголовка.Поле);

	ПолеОтчета			= ВариантыОтчетовКлиентСервер.сВО_ПолеОтчета(Группировка.Выбор, СвойстваЗаголовка.Поле);

	Если Не ЗначениеЗаполнено(Заголовок) Тогда
		ПараметрыОбработчика	= Новый Структура("Форма, Команда", Форма, Команда);
		Обработчик				= Новый ОписаниеОповещения("сВО_ПослеВводаЗаголовкаПоляОтчета", ЭтотОбъект, ПараметрыОбработчика);

		ТекущийЗаголовок = сВО_ТекущийЗаголовокПоляОтчета(ПолеОтчета, ОписаниеПоля);

		ЗаголовокДиалога = СтрШаблон("Заголовок поля: %1", ?(ОписаниеПоля = Неопределено, Строка(ПолеОтчета), ОписаниеПоля.Заголовок));

		ПоказатьВводСтроки(Обработчик, ТекущийЗаголовок, ЗаголовокДиалога);

		Возврат;
	КонецЕсли;

	Если ПолеОтчета = Неопределено Тогда
		КомпоновщикНастроек.РазвернутьАвтоПоля();

		ПолеОтчета	= ВариантыОтчетовКлиентСервер.сВО_ПолеОтчета(Группировка.Выбор, СвойстваЗаголовка.Поле);
	КонецЕсли;

	Если ПолеОтчета <> Неопределено Тогда
		сВО_УстановитьЗаголовокПоляОтчета(Настройки, ПолеОтчета, ОписаниеПоля, Заголовок);
	КонецЕсли;

	сВО_ОповеститьОЗавершенииКонтекстнойНастройки(Форма, сВО_ДействиеКоманды(Команда));
КонецПроцедуры

Процедура сВО_ПослеВводаЗаголовкаПоляОтчета(Заголовок, ДополнительныеПараметры) Экспорт
	Если ЗначениеЗаполнено(Заголовок) Тогда
		сВО_ПереименоватьПоле(ДополнительныеПараметры.Форма, ДополнительныеПараметры.Команда, Заголовок);
	КонецЕсли;
КонецПроцедуры

Функция сВО_ТекущийЗаголовокПоляОтчета(Поле, ОписаниеПоля)
	ТекущийЗаголовок = "";

	Если Поле <> Неопределено И ЗначениеЗаполнено(Поле.Заголовок) Тогда
		ТекущийЗаголовок = Поле.Заголовок;
	ИначеЕсли ОписаниеПоля <> Неопределено Тогда
		ТекущийЗаголовок = ОписаниеПоля.Заголовок;
	ИначеЕсли Поле <> Неопределено Тогда
		ТекущийЗаголовок = Строка(Поле);
	КонецЕсли;

	Возврат ТекущийЗаголовок;
КонецФункции

Процедура сВО_УстановитьЗаголовокПоляОтчета(Настройки, Поле, ОписаниеПоля, Заголовок)
	Если ОписаниеПоля <> Неопределено И Заголовок = ОписаниеПоля.Заголовок Тогда
		Поле.Заголовок = "";
	Иначе
		Поле.Заголовок = Заголовок;
	КонецЕсли;

	Формула = ВариантыОтчетовКлиентСервер.сВО_ФормулаПоПутиКДанным(Настройки, Поле.Поле);

	Если ТипЗнч(Формула) = Тип("ПользовательскоеПолеВыражениеКомпоновкиДанных") Тогда
		Формула.Заголовок = Заголовок;
	КонецЕсли;
КонецПроцедуры

Процедура сВО_СгруппироватьПоВыбранномуПолю(Форма, Команда) Экспорт
	Если Не сВО_ДействиеДоступно(Форма, Команда) Тогда
		Возврат;
	КонецЕсли;

	СвойстваЗаголовка	= сВО_СвойстваЗаголовкаОтчета(Форма);
	Настройки			= сВО_ИспользуемыеНастройки(Форма, СвойстваЗаголовка.ИдентификаторНастроек);

	ВыбранноеПоле = Настройки.ДоступныеПоляГруппировок.НайтиПоле(СвойстваЗаголовка.Поле);
	Если ВыбранноеПоле = Неопределено Тогда
		Возврат;
	КонецЕсли;

	Раздел = Настройки.ПолучитьОбъектПоИдентификатору(СвойстваЗаголовка.ИдентификаторРаздела);

	Если ТипЗнч(Раздел) = Тип("ТаблицаКомпоновкиДанных") Тогда
		Группировки = Раздел.Строки;
	Иначе
		Группировки = Раздел.Родитель.Структура;
	КонецЕсли;

	ПеремещаемыеГруппировки = Новый Массив;

	Для Каждого Элемент Из Группировки Цикл
		ПеремещаемыеГруппировки.Добавить(Элемент);
	КонецЦикла;

	ДетальныеЗаписи = сВО_ДетальныеЗаписи(Группировки);

	Если ТипЗнч(Группировки) = Тип("КоллекцияЭлементовСтруктурыНастроекКомпоновкиДанных") Тогда
		НоваяГруппировка = Группировки.Вставить(0, Тип("ГруппировкаКомпоновкиДанных")); // ГруппировкаКомпоновкиДанных
	Иначе
		НоваяГруппировка = Группировки.Вставить(0);
	КонецЕсли;

	НовоеПоле				= НоваяГруппировка.ПоляГруппировки.Элементы.Добавить(Тип("ПолеГруппировкиКомпоновкиДанных"));
	НовоеПоле.Поле			= ВыбранноеПоле.Поле;
	НовоеПоле.Использование	= Истина;

	НоваяГруппировка.Выбор.Элементы.Добавить(Тип("АвтоВыбранноеПолеКомпоновкиДанных"));
	НоваяГруппировка.Порядок.Элементы.Добавить(Тип("АвтоЭлементПорядкаКомпоновкиДанных"));

	Если ДетальныеЗаписи <> Неопределено Тогда
		ПоискЭлементов = Новый Соответствие;
		ВариантыОтчетовКлиентСервер.О_СкопироватьРекурсивно(Настройки, ДетальныеЗаписи, НоваяГруппировка.Структура, 0, ПоискЭлементов);
	КонецЕсли;

	Для Каждого Группировка Из ПеремещаемыеГруппировки Цикл
		Группировки.Удалить(Группировка);
	КонецЦикла;

	сВО_ОповеститьОЗавершенииКонтекстнойНастройки(Форма, сВО_ДействиеКоманды(Команда));
КонецПроцедуры

Процедура сВО_ОчиститьОформление(Форма, СвойстваЗаголовка) Экспорт
	Настройки = сВО_ИспользуемыеНастройки(Форма, СвойстваЗаголовка.ИдентификаторНастроек);

	Группировка = Настройки.ПолучитьОбъектПоИдентификатору(СвойстваЗаголовка.ИдентификаторГруппировки);
	Если ТипЗнч(Группировка) = Тип("НастройкиВложенногоОбъектаКомпоновкиДанных") Тогда
		сВО_ОчиститьОформлениеГруппировкиОтчета(Группировка.Настройки, СвойстваЗаголовка);
	ИначеЕсли СвойстваЗаголовка.КоличествоРазделов = 1 Тогда
		сВО_ОчиститьОформлениеГруппировкиОтчета(Настройки, СвойстваЗаголовка);
	Иначе
		Раздел = Настройки.ПолучитьОбъектПоИдентификатору(СвойстваЗаголовка.ИдентификаторРаздела);

		Если ТипЗнч(Раздел) = Тип("ТаблицаКомпоновкиДанных") Тогда
			сВО_ОчиститьОформлениеГруппировокРазделаОтчета(Раздел.Строки, СвойстваЗаголовка);
		Иначе
			сВО_ОчиститьОформлениеГруппировкиРазделаОтчета(Раздел, СвойстваЗаголовка);
		КонецЕсли;
	КонецЕсли;

	сВО_ОповеститьОЗавершенииКонтекстнойНастройки(Форма, "ОчиститьОформление");
КонецПроцедуры

Процедура сВО_ОформитьКрасным(Форма, Команда, СвойстваЗаголовка = Неопределено, Значение = Неопределено) Экспорт
	Если СвойстваЗаголовка = Неопределено Тогда
		СвойстваЗаголовка = сВО_СвойстваЗаголовкаОтчета(Форма);
	КонецЕсли;

	ДействиеКоманды								= сВО_ДействиеКоманды(Команда);

	ПараметрыРаскраски							= сВО_ПараметрыРаскраскиРазделаОтчета(ДействиеКоманды);
	ПараметрыРаскраски.Условие.ЛевоеЗначение	= СвойстваЗаголовка.Поле;

	Если ДействиеКоманды = "ОформитьОтрицательные" Тогда
		Если Не сВО_ДействиеНадПолемДоступно(ДействиеКоманды, СвойстваЗаголовка) Тогда
			Возврат;
		КонецЕсли;

		ПараметрыРаскраски.Условие.ВидСравнения		= ВидСравненияКомпоновкиДанных.Меньше;
		ПараметрыРаскраски.Условие.ПравоеЗначение	= 0;
		ПараметрыРаскраски.Представление			= "Оформить отрицательные красным";
	Иначе
		ПараметрыРаскраски.Условие.ВидСравнения		= сВО_ВидСравненияУсловияОформления(Значение);
		ПараметрыРаскраски.Условие.ПравоеЗначение	= Значение;
		ПараметрыРаскраски.Представление			= "Оформить красным";
	КонецЕсли;

	ЭлементыСтиля		= БазоваяПодсистемаКлиент.СП_ЭлементыСтиля();

	Оформление			= сВО_СтандартныйПараметрРаскраскиРазделаОтчета();
	Оформление.Параметр	= "ЦветТекста";
	Оформление.Значение	= ЭлементыСтиля.ЦветТекстаОтрицательногоЗначения;

	ПараметрыРаскраски.Оформление.Добавить(Оформление);

	Оформление			= сВО_СтандартныйПараметрРаскраскиРазделаОтчета();
	Оформление.Параметр	= "ЦветФона";
	Оформление.Значение	= ЭлементыСтиля.ЦветФонаОтрицательногоЗначения;

	ПараметрыРаскраски.Оформление.Добавить(Оформление);

	сВО_РаскраситьРазделОтчета(Форма, ПараметрыРаскраски, СвойстваЗаголовка);

	сВО_ОповеститьОЗавершенииКонтекстнойНастройки(Форма, сВО_ДействиеКоманды(Команда));
КонецПроцедуры

Процедура сВО_ОформитьЗеленым(Форма, Команда, СвойстваЗаголовка = Неопределено, Значение = Неопределено) Экспорт
	Если СвойстваЗаголовка = Неопределено Тогда
		СвойстваЗаголовка = сВО_СвойстваЗаголовкаОтчета(Форма);
	КонецЕсли;

	ДействиеКоманды								= сВО_ДействиеКоманды(Команда);

	ПараметрыРаскраски							= сВО_ПараметрыРаскраскиРазделаОтчета(ДействиеКоманды);
	ПараметрыРаскраски.Условие.ЛевоеЗначение	= СвойстваЗаголовка.Поле;

	Если ДействиеКоманды = "ОформитьПоложительные" Тогда
		Если Не сВО_ДействиеНадПолемДоступно(ДействиеКоманды, СвойстваЗаголовка) Тогда
			Возврат;
		КонецЕсли;

		ПараметрыРаскраски.Условие.ВидСравнения		= ВидСравненияКомпоновкиДанных.Больше;
		ПараметрыРаскраски.Условие.ПравоеЗначение	= 0;
		ПараметрыРаскраски.Представление			= "Оформить положительные зеленым";
	Иначе
		ПараметрыРаскраски.Условие.ВидСравнения		= сВО_ВидСравненияУсловияОформления(Значение);
		ПараметрыРаскраски.Условие.ПравоеЗначение	= Значение;
		ПараметрыРаскраски.Представление			= "Оформить зеленым";
	КонецЕсли;

	ЭлементыСтиля		= БазоваяПодсистемаКлиент.СП_ЭлементыСтиля();

	Оформление			= сВО_СтандартныйПараметрРаскраскиРазделаОтчета();
	Оформление.Параметр	= "ЦветТекста";
	Оформление.Значение	= ЭлементыСтиля.ЦветТекстаПоложительногоЗначения;

	ПараметрыРаскраски.Оформление.Добавить(Оформление);

	Оформление			= сВО_СтандартныйПараметрРаскраскиРазделаОтчета();
	Оформление.Параметр	= "ЦветФона";
	Оформление.Значение	= ЭлементыСтиля.ЦветФонаПоложительногоЗначения;

	ПараметрыРаскраски.Оформление.Добавить(Оформление);

	сВО_РаскраситьРазделОтчета(Форма, ПараметрыРаскраски, СвойстваЗаголовка);

	сВО_ОповеститьОЗавершенииКонтекстнойНастройки(Форма, сВО_ДействиеКоманды(Команда));
КонецПроцедуры

Процедура сВО_ОформитьЖелтым(Форма, Команда, СвойстваЗаголовка = Неопределено, Значение = Неопределено)
	Если СвойстваЗаголовка = Неопределено Тогда
		СвойстваЗаголовка = сВО_СвойстваЗаголовкаОтчета(Форма);
	КонецЕсли;

	ПараметрыРаскраски							= сВО_ПараметрыРаскраскиРазделаОтчета(сВО_ДействиеКоманды(Команда));
	ПараметрыРаскраски.Условие.ЛевоеЗначение	= СвойстваЗаголовка.Поле;
	ПараметрыРаскраски.Условие.ВидСравнения		= сВО_ВидСравненияУсловияОформления(Значение);
	ПараметрыРаскраски.Условие.ПравоеЗначение	= Значение;

	ЭлементыСтиля		= БазоваяПодсистемаКлиент.СП_ЭлементыСтиля();

	Оформление			= сВО_СтандартныйПараметрРаскраскиРазделаОтчета();
	Оформление.Параметр	= "ЦветТекста";
	Оформление.Значение	= ЭлементыСтиля.ЦветТекстаВнимание;

	ПараметрыРаскраски.Оформление.Добавить(Оформление);

	Оформление			= сВО_СтандартныйПараметрРаскраскиРазделаОтчета();
	Оформление.Параметр	= "ЦветФона";
	Оформление.Значение	= ЭлементыСтиля.ЦветФонаВнимание;

	ПараметрыРаскраски.Оформление.Добавить(Оформление);
	ПараметрыРаскраски.Представление = "Оформить желтым";

	сВО_РаскраситьРазделОтчета(Форма, ПараметрыРаскраски, СвойстваЗаголовка);

	сВО_ОповеститьОЗавершенииКонтекстнойНастройки(Форма, сВО_ДействиеКоманды(Команда));
КонецПроцедуры

Функция сВО_ПараметрыРаскраскиРазделаОтчета(Действие)
	Условие = Новый Структура;
	Условие.Вставить("ЛевоеЗначение",	Неопределено);
	Условие.Вставить("ВидСравнения",	ВидСравненияКомпоновкиДанных.Равно);
	Условие.Вставить("ПравоеЗначение",	Неопределено);

	ПараметрыРаскраски = Новый Структура;
	ПараметрыРаскраски.Вставить("Условие",			Условие);
	ПараметрыРаскраски.Вставить("Оформление",		Новый Массив);
	ПараметрыРаскраски.Вставить("Представление",	"");

	Возврат ПараметрыРаскраски;
КонецФункции

Функция сВО_СтандартныйПараметрРаскраскиРазделаОтчета()
	Параметр = Новый Структура;
	Параметр.Вставить("Параметр",	"");
	Параметр.Вставить("Значение",	Неопределено);

	Возврат Параметр;
КонецФункции

Процедура сВО_РаскраситьРазделОтчета(Форма, ПараметрыРаскраски, СвойстваЗаголовка)
	Настройки	= сВО_ИспользуемыеНастройки(Форма, СвойстваЗаголовка.ИдентификаторНастроек);
	Группировка	= Настройки.ПолучитьОбъектПоИдентификатору(СвойстваЗаголовка.ИдентификаторГруппировки);

	Если ТипЗнч(Группировка) = Тип("НастройкиВложенногоОбъектаКомпоновкиДанных") Тогда
		сВО_ПрименитьРаскраскуГруппировкиОтчета(Группировка.Настройки, ПараметрыРаскраски, СвойстваЗаголовка);
	ИначеЕсли СвойстваЗаголовка.КоличествоРазделов = 1 Тогда
		сВО_ПрименитьРаскраскуГруппировкиОтчета(Настройки, ПараметрыРаскраски, СвойстваЗаголовка);
	Иначе
		Раздел = Настройки.ПолучитьОбъектПоИдентификатору(СвойстваЗаголовка.ИдентификаторРаздела);
		Если ТипЗнч(Раздел) = Тип("ТаблицаКомпоновкиДанных") Тогда
			сВО_РаскраситьГруппировкиРазделаОтчета(Раздел.Строки, ПараметрыРаскраски, СвойстваЗаголовка);
		Иначе
			сВО_РаскраситьГруппировкуРазделаОтчета(Раздел, ПараметрыРаскраски, СвойстваЗаголовка);
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

Процедура сВО_РаскраситьГруппировкиРазделаОтчета(Группировки, ПараметрыРаскраски, СвойстваЗаголовка)
	Для Каждого Группировка Из Группировки Цикл
		сВО_РаскраситьГруппировкуРазделаОтчета(Группировка, ПараметрыРаскраски, СвойстваЗаголовка);
	КонецЦикла;
КонецПроцедуры

Процедура сВО_РаскраситьГруппировкуРазделаОтчета(Группировка, ПараметрыРаскраски, СвойстваЗаголовка)
	Если ТипЗнч(Группировка) = Тип("ТаблицаКомпоновкиДанных") Тогда
		сВО_РаскраситьГруппировкиРазделаОтчета(Группировка.Строки, ПараметрыРаскраски, СвойстваЗаголовка);
	Иначе
		сВО_ПрименитьРаскраскуГруппировкиОтчета(Группировка, ПараметрыРаскраски, СвойстваЗаголовка);

		сВО_РаскраситьГруппировкиРазделаОтчета(Группировка.Структура, ПараметрыРаскраски, СвойстваЗаголовка)
	КонецЕсли;
КонецПроцедуры

Процедура сВО_ПрименитьРаскраскуГруппировкиОтчета(Группировка, ПараметрыРаскраски, СвойстваЗаголовка)
	ПолеОформления = БазоваяПодсистемаКлиентСервер.ОН_СвойствоСтруктуры(ПараметрыРаскраски.Условие, "ЛевоеЗначение");

	ПараметрыРаскраскиПрименимы = сВО_ОформлениеПрименимоКГруппировке(Группировка, ПолеОформления, СвойстваЗаголовка.Ресурс);

	Если ПараметрыРаскраскиПрименимы Тогда
		Раскраска = сВО_РаскраскаГруппировкиОтчета(Группировка.УсловноеОформление, ПараметрыРаскраски);

		Если Раскраска = Неопределено Тогда
			сВО_ДобавитьРаскраскуГруппировкиОтчета(Группировка.УсловноеОформление, ПараметрыРаскраски);
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

Функция сВО_ОформлениеПрименимоКГруппировке(Группировка, Поле, ЭтоРесурс)
	Если ТипЗнч(Группировка) <> Тип("ГруппировкаКомпоновкиДанных") И ТипЗнч(Группировка)<> Тип("ГруппировкаТаблицыКомпоновкиДанных") Тогда
		Возврат Истина;
	КонецЕсли;

	ПоляГруппировки = Группировка.ПоляГруппировки;

	Возврат ЭтоРесурс Или Поле = Неопределено Или ПоляГруппировки.Элементы.Количество() = 0 Или ВариантыОтчетовКлиентСервер.сВО_ПолеОтчета(ПоляГруппировки, Поле) <> Неопределено;
КонецФункции

Функция сВО_РаскраскаГруппировкиОтчета(УсловноеОформление, ПараметрыРаскраски)
	 Для Каждого Элемент Из УсловноеОформление.Элементы Цикл
		Условие = сВО_УсловиеРаскраскиГруппировкиОтчета(Элемент.Отбор, ПараметрыРаскраски.Условие);

		Если Условие <> Неопределено Тогда
			сВО_ОбновитьРаскраскуГруппировкиОтчета(Элемент, Условие, ПараметрыРаскраски);

			Возврат Элемент;
		КонецЕсли;
	 КонецЦикла;

	Возврат Неопределено;
КонецФункции

Функция сВО_УсловиеРаскраскиГруппировкиОтчета(Отбор, Поиск)
	Условие = Неопределено;

	Для Каждого Элемент Из Отбор.Элементы Цикл
		Если ТипЗнч(Элемент) = Тип("ЭлементОтбораКомпоновкиДанных") И Элемент.ЛевоеЗначение = Поиск.ЛевоеЗначение И Элемент.ВидСравнения = Поиск.ВидСравнения И Элемент.ПравоеЗначение = Поиск.ПравоеЗначение Тогда
			Условие = Элемент;

			Прервать;
		КонецЕсли;
	КонецЦикла;

	Возврат Условие;
КонецФункции

Процедура сВО_ОбновитьРаскраскуГруппировкиОтчета(Оформление, Условие, ПараметрыРаскраски)
	Оформление.Использование	= Истина;
	Условие.Использование		= Истина;

	Оформление.Поля.Элементы.Очистить();

	Поле				= Оформление.Поля.Элементы.Добавить();
	Поле.Поле			= ПараметрыРаскраски.Условие.ЛевоеЗначение;
	Поле.Использование	= Истина;

	сВО_СброситьОформлениеГруппировкиОтчета(Оформление);

	Для Каждого Параметр Из ПараметрыРаскраски.Оформление Цикл
		Оформление.Оформление.УстановитьЗначениеПараметра(Параметр.Параметр, Параметр.Значение);
	КонецЦикла;

	сВО_УстановитьОбластьОформленияГруппировкиОтчета(Оформление);
КонецПроцедуры

Процедура сВО_ДобавитьРаскраскуГруппировкиОтчета(УсловноеОформление, ПараметрыРаскраски)
	Оформление = УсловноеОформление.Элементы.Добавить();

	Условие = Оформление.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ЗаполнитьЗначенияСвойств(Условие, ПараметрыРаскраски.Условие);
	Условие.Использование = Истина;

	Поле				= Оформление.Поля.Элементы.Добавить();
	Поле.Поле			= ПараметрыРаскраски.Условие.ЛевоеЗначение;
	Поле.Использование	= Истина;

	Для Каждого Параметр Из ПараметрыРаскраски.Оформление Цикл
		Оформление.Оформление.УстановитьЗначениеПараметра(Параметр.Параметр, Параметр.Значение);
	КонецЦикла;

	Оформление.ПредставлениеПользовательскойНастройки = ПараметрыРаскраски.Представление;

	сВО_УстановитьОбластьОформленияГруппировкиОтчета(Оформление);
КонецПроцедуры

Процедура сВО_УстановитьОбластьОформленияГруппировкиОтчета(ЭлементОформления)
	НеиспользуемыеОбласти = сВО_НеиспользуемыеОбластиОформления();

	Для Каждого Область Из НеиспользуемыеОбласти Цикл
		ЭлементОформления[Область] = ИспользованиеУсловногоОформленияКомпоновкиДанных.НеИспользовать;
	КонецЦикла;
КонецПроцедуры

Функция сВО_НеиспользуемыеОбластиОформления()

	НеиспользуемыеОбласти = Новый Массив;
	НеиспользуемыеОбласти.Добавить("ИспользоватьВЗаголовке");
	НеиспользуемыеОбласти.Добавить("ИспользоватьВЗаголовкеПолей");
	НеиспользуемыеОбласти.Добавить("ИспользоватьВОбщемИтоге");
	НеиспользуемыеОбласти.Добавить("ИспользоватьВОтборе");
	НеиспользуемыеОбласти.Добавить("ИспользоватьВПараметрах");

	ПараметрыКлиента = БазоваяПодсистемаКлиентПовтИсп.СП_ПараметрыРаботыКлиента();

	Если БазоваяПодсистемаКлиентСервер.ОН_СравнитьВерсии(ПараметрыКлиента.ВерсияРежимаСовместимости, "8.3.16.0") < 0 Тогда
		Возврат НеиспользуемыеОбласти;
	КонецЕсли;

	НеиспользуемыеОбласти.Добавить("ИспользоватьВЗаголовкеПолейРесурсов");
	НеиспользуемыеОбласти.Добавить("ИспользоватьВЗаголовкеОбщегоИтога");
	НеиспользуемыеОбласти.Добавить("ИспользоватьВЗаголовкеПолейРесурсовОбщегоИтога");

	Возврат НеиспользуемыеОбласти;
КонецФункции

Процедура сВО_СброситьОформлениеГруппировкиОтчета(ЭлементОформления, ТолькоПараметры = Истина)
	Для Каждого Элемент Из ЭлементОформления.Оформление.Элементы Цикл
		Элемент.Использование = Ложь;
	КонецЦикла;

	Если ТолькоПараметры Тогда
		Возврат;
	КонецЕсли;

	ЭлементОформления.Поля.Элементы.Очистить();
	ЭлементОформления.Отбор.Элементы.Очистить();
КонецПроцедуры

Функция сВО_ДействиеНадПолемДоступно(Действие, СвойстваЗаголовка)
	Если Не СвойстваЗаголовка[Действие] Тогда
		ТекстПредупреждения = СтрШаблон("Действие недоступно для поля %1", СвойстваЗаголовка.Текст);

		ПоказатьПредупреждение(, ТекстПредупреждения);

		Возврат Ложь;
	КонецЕсли;

	Возврат Истина;
КонецФункции

Процедура сВО_УстановитьВысотуСтроки(Форма, Команда, СвойстваЗаголовка = Неопределено, ВысотаСтроки = Неопределено, ПараметрыВысотыСтроки = Неопределено) Экспорт
	Если СвойстваЗаголовка = Неопределено Тогда
		СвойстваЗаголовка = сВО_СвойстваЗаголовкаОтчета(Форма);
	КонецЕсли;

	Если ВысотаСтроки = Неопределено Тогда
		ПараметрыВысотыСтроки = сВО_ПараметрыРазмераПоляОтчета(Форма, СвойстваЗаголовка);

		ПараметрыОбработчика = Новый Структура;
		ПараметрыОбработчика.Вставить("Форма",					Форма);
		ПараметрыОбработчика.Вставить("Команда",				Команда);
		ПараметрыОбработчика.Вставить("СвойстваЗаголовка",		СвойстваЗаголовка);
		ПараметрыОбработчика.Вставить("ПараметрыРазмераПоля",	ПараметрыВысотыСтроки);

		Обработчик = Новый ОписаниеОповещения("сВО_ПослеВводаВысотыСтрокиОтчета", ЭтотОбъект, ПараметрыОбработчика);
		ПоказатьВводЧисла(Обработчик, ПараметрыВысотыСтроки.Размер, "Высота строки", 5);

		Возврат;
	КонецЕсли;

	Если ПараметрыВысотыСтроки = Неопределено Или ПараметрыВысотыСтроки.Элемент = Неопределено Тогда
		сВО_ДобавитьПараметрыРазмераПоляОтчета(Форма, СвойстваЗаголовка, ВысотаСтроки);
	Иначе
		сВО_ОбновитьПараметрыРазмера(ПараметрыВысотыСтроки, ВысотаСтроки);
	КонецЕсли;

	сВО_ОповеститьОЗавершенииКонтекстнойНастройки(Форма, сВО_ДействиеКоманды(Команда));
КонецПроцедуры

Процедура сВО_УстановитьШиринуКолонки(Форма, Команда, СвойстваЗаголовка = Неопределено, ШиринаКолонки = Неопределено, ПараметрыШириныКолонки = Неопределено) Экспорт
	Если СвойстваЗаголовка = Неопределено Тогда
		СвойстваЗаголовка = сВО_СвойстваЗаголовкаОтчета(Форма);
	КонецЕсли;

	ПараметрыШириныКолонки = сВО_ПараметрыРазмераПоляОтчета(Форма, СвойстваЗаголовка, "Ширина");

	Если ШиринаКолонки = Неопределено Тогда
		ПараметрыОбработчика = Новый Структура;
		ПараметрыОбработчика.Вставить("Форма",					Форма);
		ПараметрыОбработчика.Вставить("Команда",				Команда);
		ПараметрыОбработчика.Вставить("СвойстваЗаголовка",		СвойстваЗаголовка);
		ПараметрыОбработчика.Вставить("ПараметрыРазмераПоля",	ПараметрыШириныКолонки);

		Обработчик = Новый ОписаниеОповещения("сВО_ПослеВводаШириныКолонкиОтчета", ЭтотОбъект, ПараметрыОбработчика);
		ПоказатьВводЧисла(Обработчик, ПараметрыШириныКолонки.Размер, "Ширина колонки", 5);

		Возврат;
	КонецЕсли;

	Если ПараметрыШириныКолонки = Неопределено Или ПараметрыШириныКолонки.Элемент = Неопределено Тогда
		сВО_ДобавитьПараметрыРазмераПоляОтчета(Форма, СвойстваЗаголовка, ШиринаКолонки, "Ширина");
	Иначе
		сВО_ОбновитьПараметрыРазмера(ПараметрыШириныКолонки, ШиринаКолонки);
	КонецЕсли;

	сВО_ОповеститьОЗавершенииКонтекстнойНастройки(Форма, сВО_ДействиеКоманды(Команда));
КонецПроцедуры

Процедура сВО_ПослеВводаВысотыСтрокиОтчета(ВысотаСтроки, ДополнительныеПараметры) Экспорт
	Если ЗначениеЗаполнено(ВысотаСтроки) Тогда
		сВО_УстановитьВысотуСтроки(ДополнительныеПараметры.Форма, ДополнительныеПараметры.Команда, ДополнительныеПараметры.СвойстваЗаголовка, ВысотаСтроки, ДополнительныеПараметры.ПараметрыРазмераПоля);
	КонецЕсли;
КонецПроцедуры

Процедура сВО_ПослеВводаШириныКолонкиОтчета(ШиринаКолонки, ДополнительныеПараметры) Экспорт
	Если ЗначениеЗаполнено(ШиринаКолонки) Тогда
		сВО_УстановитьШиринуКолонки(ДополнительныеПараметры.Форма, ДополнительныеПараметры.Команда, ДополнительныеПараметры.СвойстваЗаголовка, ШиринаКолонки, ДополнительныеПараметры.ПараметрыРазмераПоля);
	КонецЕсли;
КонецПроцедуры

Функция сВО_ПараметрыРазмераПоляОтчета(Форма, СвойстваЗаголовка, Ориентация = "Высота") Экспорт
	Если ТипЗнч(СвойстваЗаголовка.ИдентификаторНастроек) <> Тип("ИдентификаторКомпоновкиДанных") Тогда
		Возврат сВО_СтандартныеПараметрыРазмераПоляОтчета();
	КонецЕсли;

	Настройки	= сВО_ИспользуемыеНастройки(Форма, СвойстваЗаголовка.ИдентификаторНастроек);
	Группировка	= Настройки.ПолучитьОбъектПоИдентификатору(СвойстваЗаголовка.ИдентификаторГруппировки);
	Если ТипЗнч(Группировка) = Тип("НастройкиВложенногоОбъектаКомпоновкиДанных") Тогда
		Оформление = Группировка.Настройки.УсловноеОформление;
	ИначеЕсли СвойстваЗаголовка.КоличествоРазделов = 1 Тогда
		Оформление	= Настройки.УсловноеОформление;
	Иначе
		Оформление	= Группировка.УсловноеОформление;
	КонецЕсли;

	Для Каждого Элемент Из Оформление.Элементы Цикл
		ПараметрыОформления = Элемент.Оформление.Элементы;

		Идентификаторы		= сВО_ИдентификаторыПараметровРазмераПоля(Ориентация);
		МинимальныйРазмер	= ПараметрыОформления.Найти(Идентификаторы.МинимальныйРазмер);
		МаксимальныйРазмер	= ПараметрыОформления.Найти(Идентификаторы.МаксимальныйРазмер);

		Если МинимальныйРазмер.Использование Или МаксимальныйРазмер.Использование Тогда
			Параметры						= сВО_СтандартныеПараметрыРазмераПоляОтчета();
			Параметры.Поле					= СвойстваЗаголовка.Поле;
			Параметры.Элемент				= Элемент;
			Параметры.МинимальныйРазмер		= МинимальныйРазмер;
			Параметры.МаксимальныйРазмер	= МаксимальныйРазмер;
			Параметры.Размер				= Макс(МинимальныйРазмер.Значение, МаксимальныйРазмер.Значение);

			Возврат Параметры;
		КонецЕсли;
	КонецЦикла;

	Возврат сВО_СтандартныеПараметрыРазмераПоляОтчета();
КонецФункции

Процедура сВО_ОбновитьПараметрыРазмера(Параметры, Размер)
	Параметры.Элемент.Использование				= Истина;

	Параметры.МинимальныйРазмер.Значение		= Размер;
	Параметры.МинимальныйРазмер.Использование	= Истина;

	Параметры.МаксимальныйРазмер.Значение		= Размер;
	Параметры.МаксимальныйРазмер.Использование	= Истина;

	сВО_ПроверитьИзменяемоеПоле(Параметры.Элемент.Поля, Параметры.Поле);
	сВО_УстановитьОбластьИзмененияРазмера(Параметры.Элемент);
КонецПроцедуры

Функция сВО_ИдентификаторыПараметровРазмераПоля(Ориентация)
	Идентификаторы = Новый Структура;

	Если Ориентация = "Высота" Тогда
		Идентификаторы.Вставить("МинимальныйРазмер", "МинимальнаяВысота");
		Идентификаторы.Вставить("МаксимальныйРазмер", "МаксимальнаяВысота");
	Иначе
		Идентификаторы.Вставить("МинимальныйРазмер", "МинимальнаяШирина");
		Идентификаторы.Вставить("МаксимальныйРазмер", "МаксимальнаяШирина");
	КонецЕсли;

	Возврат Идентификаторы;
КонецФункции

Процедура сВО_ДобавитьПараметрыРазмераПоляОтчета(Форма, СвойстваЗаголовка, Размер, Ориентация = "Высота")
	Настройки			= сВО_ИспользуемыеНастройки(Форма, СвойстваЗаголовка.ИдентификаторНастроек);
	Группировка			= Настройки.ПолучитьОбъектПоИдентификатору(СвойстваЗаголовка.ИдентификаторГруппировки);

	Если ТипЗнч(Группировка) = Тип("НастройкиВложенногоОбъектаКомпоновкиДанных") Тогда
		Элемент = Группировка.Настройки.УсловноеОформление.Элементы.Добавить();
	ИначеЕсли СвойстваЗаголовка.КоличествоРазделов = 1 Тогда
		Элемент = Настройки.УсловноеОформление.Элементы.Добавить();
	Иначе
		Элемент = Группировка.УсловноеОформление.Элементы.Добавить();
	КонецЕсли;

	ПараметрыОформления	= Элемент.Оформление.Элементы;

	Идентификаторы		= сВО_ИдентификаторыПараметровРазмераПоля(Ориентация);

	Параметры						= сВО_СтандартныеПараметрыРазмераПоляОтчета(Размер);
	Параметры.Поле					= СвойстваЗаголовка.Поле;
	Параметры.Элемент				= Элемент;
	Параметры.МинимальныйРазмер		= ПараметрыОформления.Найти(Идентификаторы.МинимальныйРазмер);
	Параметры.МаксимальныйРазмер	= ПараметрыОформления.Найти(Идентификаторы.МаксимальныйРазмер);

	сВО_ОбновитьПараметрыРазмера(Параметры, Размер);
КонецПроцедуры

Функция сВО_СтандартныеПараметрыРазмераПоляОтчета(Размер = 0)
	Параметры = Новый Структура;
	Параметры.Вставить("Поле",					Неопределено);
	Параметры.Вставить("Элемент",				Неопределено);
	Параметры.Вставить("МинимальныйРазмер",		Неопределено);
	Параметры.Вставить("МаксимальныйРазмер",	Неопределено);
	Параметры.Вставить("Размер",				Размер);

	Возврат Параметры;
КонецФункции

Процедура сВО_ПроверитьИзменяемоеПоле(Поля, ПолеИскомое)
	Поле = Неопределено;

	Для Каждого Элемент Из Поля.Элементы Цикл
		Если Элемент.Поле = ПолеИскомое Тогда
			Поле = Элемент;

			Прервать;
		КонецЕсли;
	КонецЦикла;

	Если Поле = Неопределено Тогда
		Поле		= Поля.Элементы.Добавить();
		Поле.Поле	= ПолеИскомое;
	КонецЕсли;

	Поле.Использование = Истина;
КонецПроцедуры

Процедура сВО_УстановитьОбластьИзмененияРазмера(Элемент)
	НеиспользуемыеОбласти = Новый Массив;
	НеиспользуемыеОбласти.Добавить("ИспользоватьВОбщемИтоге");
	НеиспользуемыеОбласти.Добавить("ИспользоватьВОтборе");
	НеиспользуемыеОбласти.Добавить("ИспользоватьВПараметрах");

	ПараметрыКлиента = БазоваяПодсистемаКлиентПовтИсп.СП_ПараметрыРаботыКлиента();

	Если БазоваяПодсистемаКлиентСервер.ОН_СравнитьВерсии(ПараметрыКлиента.ВерсияРежимаСовместимости, "8.3.16.0") >= 0 Тогда
		НеиспользуемыеОбласти.Добавить("ИспользоватьВЗаголовкеОбщегоИтога");
	КонецЕсли;

	Для Каждого Область Из НеиспользуемыеОбласти Цикл
		Элемент[Область] = ИспользованиеУсловногоОформленияКомпоновкиДанных.НеИспользовать;
	КонецЦикла;
КонецПроцедуры

Процедура сВО_ОформитьЕще(Форма, Команда, СвойстваЗаголовка = Неопределено, Значение = Неопределено) Экспорт
	Если Не сВО_ДействиеДоступно(Форма,, СвойстваЗаголовка) Тогда
		Возврат;
	КонецЕсли;

	ИспользуемыеНастройки		= сВО_ИспользуемыеНастройки(Форма, СвойстваЗаголовка.ИдентификаторНастроек);

	Раздел						= ИспользуемыеНастройки.ПолучитьОбъектПоИдентификатору(СвойстваЗаголовка.ИдентификаторРаздела);
	Группировка					= ИспользуемыеНастройки.ПолучитьОбъектПоИдентификатору(СвойстваЗаголовка.ИдентификаторГруппировки);

	Если ТипЗнч(Группировка) = Тип("НастройкиВложенногоОбъектаКомпоновкиДанных") Тогда
		ИдентификаторГруппировки	= ИспользуемыеНастройки.ПолучитьИдентификаторПоОбъекту(Группировка);
		ИдентификаторОформления		= сВО_ИдентификаторЭлементаОформленияГруппировкиОтчета(Группировка.Настройки, СвойстваЗаголовка.Поле);
	ИначеЕсли СвойстваЗаголовка.КоличествоРазделов = 1 Тогда
		ИдентификаторГруппировки	= Неопределено;
		ИдентификаторОформления		= сВО_ИдентификаторЭлементаОформленияГруппировкиОтчета(ИспользуемыеНастройки, СвойстваЗаголовка.Поле);
	Иначе
		ИдентификаторГруппировки	= ИспользуемыеНастройки.ПолучитьИдентификаторПоОбъекту(Группировка);
		ИдентификаторОформления		= сВО_ИдентификаторЭлементаОформленияГруппировкиОтчета(Группировка, СвойстваЗаголовка.Поле);
	КонецЕсли;

	КомпоновщикНастроек = сВО_КомпоновщикНастроекОтчета(Форма);

	ПараметрыОформления = Новый Структура;
	ПараметрыОформления.Вставить("КомпоновщикНастроек",						КомпоновщикНастроек);
	ПараметрыОформления.Вставить("НастройкиОтчета",							Форма.НастройкиОтчета);
	ПараметрыОформления.Вставить("ИдентификаторЭлементаСтруктурыНастроек",	ИдентификаторГруппировки);
	ПараметрыОформления.Вставить("ИдентификаторКД",							ИдентификаторОформления);
	ПараметрыОформления.Вставить("Наименование",							"");
	ПараметрыОформления.Вставить("Поле",									СвойстваЗаголовка.Поле);
	ПараметрыОформления.Вставить("Условие",									сВО_УсловиеОформленияГруппировкиОтчета(СвойстваЗаголовка.Поле, Значение));

	ПараметрыОбработчика = Новый Структура;
	ПараметрыОбработчика.Вставить("Форма",				Форма);
	ПараметрыОбработчика.Вставить("Настройки",			КомпоновщикНастроек.Настройки);
	ПараметрыОбработчика.Вставить("Раздел",				Раздел);
	ПараметрыОбработчика.Вставить("Группировка",		Группировка);
	ПараметрыОбработчика.Вставить("Поле",				СвойстваЗаголовка.Поле);
	ПараметрыОбработчика.Вставить("Ресурс",				СвойстваЗаголовка.Ресурс);
	ПараметрыОбработчика.Вставить("Действие",			сВО_ДействиеКоманды(Команда));
	ПараметрыОбработчика.Вставить("КоличествоРазделов",	СвойстваЗаголовка.КоличествоРазделов);

	Обработчик = Новый ОписаниеОповещения("сВО_ПослеИзмененияЭлементаОформленияГруппировкиОтчета", ЭтотОбъект, ПараметрыОбработчика);

	ОткрытьФорму("ХранилищеНастроек.ХранилищеВариантовОтчетов.Форма.ЭлементУсловногоОформленияОтчета", ПараметрыОформления, Форма, Форма.УникальныйИдентификатор,,, Обработчик);
КонецПроцедуры

Процедура сВО_ПослеИзмененияЭлементаОформленияГруппировкиОтчета(Результат, ДополнительныеПараметры) Экспорт
	Если ТипЗнч(Результат) <> Тип("Структура") Тогда
		Возврат;
	КонецЕсли;

	Настройки = сВО_ИспользуемыеНастройки(ДополнительныеПараметры.Форма);

	Если Результат.Свойство("ОписаниеФормул") Тогда
		сВО_ДобавитьФормулы(Настройки, Настройки.ДоступныеПоляВыбора, Результат.ОписаниеФормул);
	КонецЕсли;

	Раздел = ДополнительныеПараметры.Раздел;
	ОбразецОформления = Результат.ЭлементКД;
	Поле = ДополнительныеПараметры.Поле;
	ЭтоРесурс = ДополнительныеПараметры.Ресурс;

	Если ТипЗнч(Раздел) = Тип("НастройкиВложенногоОбъектаКомпоновкиДанных") Тогда
		сВО_ПрименитьОформлениеГруппировкиОтчета(Раздел.Настройки, ОбразецОформления, Поле, ЭтоРесурс);
	ИначеЕсли ДополнительныеПараметры.КоличествоРазделов = 1 Тогда
		сВО_ПрименитьОформлениеГруппировкиОтчета(ДополнительныеПараметры.Настройки, ОбразецОформления, Поле, ЭтоРесурс);
	ИначеЕсли ТипЗнч(Раздел) = Тип("ТаблицаКомпоновкиДанных") Тогда
		сВО_ОформитьГруппировкиРазделаОтчета(Раздел.Строки, Результат.ЭлементКД, Поле, ЭтоРесурс);
	Иначе
		сВО_ОформитьГруппировкуРазделаОтчета(Раздел, Результат.ЭлементКД, Поле, ЭтоРесурс);
	КонецЕсли;

	сВО_ОповеститьОЗавершенииКонтекстнойНастройки(ДополнительныеПараметры.Форма, ДополнительныеПараметры.Действие);
КонецПроцедуры

Процедура сВО_ПрименитьОформлениеГруппировкиОтчета(Группировка, ОбразецОформления, Поле, ЭтоРесурс)
	ОформлениеПрименимоКГруппировке = сВО_ОформлениеПрименимоКГруппировке(Группировка, Поле, ЭтоРесурс);

	Если ОформлениеПрименимоКГруппировке Тогда
		Оформление			= Группировка.УсловноеОформление;
		ЭлементОформления	= сВО_ЭлементОформленияГруппировкиОтчета(Группировка, Поле);

		Если ЭлементОформления = Неопределено Тогда
			ЭлементОформления = Оформление.Элементы.Добавить();
		Иначе
			сВО_СброситьОформлениеГруппировкиОтчета(ЭлементОформления, Ложь);
		КонецЕсли;

		ВариантыОтчетовКлиентСервер.О_ЗаполнитьСвойстваРекурсивно(Оформление, ЭлементОформления, ОбразецОформления);

		сВО_УстановитьОбластьОформленияГруппировкиОтчета(ЭлементОформления);
	КонецЕсли;
КонецПроцедуры

Процедура сВО_ОформитьГруппировкиРазделаОтчета(Группировки, ОбразецОформления, Поле, ЭтоРесурс)
	Для Каждого Группировка Из Группировки Цикл
		сВО_ОформитьГруппировкуРазделаОтчета(Группировка, ОбразецОформления, Поле, ЭтоРесурс);
	КонецЦикла;
КонецПроцедуры

Процедура сВО_ОформитьГруппировкуРазделаОтчета(Группировка, ОбразецОформления, Поле, ЭтоРесурс)
	Если ТипЗнч(Группировка) = Тип("ТаблицаКомпоновкиДанных") Тогда
		сВО_ОформитьГруппировкиРазделаОтчета(Группировка.Строки, ОбразецОформления, Поле, ЭтоРесурс);
	Иначе
		сВО_ПрименитьОформлениеГруппировкиОтчета(Группировка, ОбразецОформления, Поле, ЭтоРесурс);

		сВО_ОформитьГруппировкиРазделаОтчета(Группировка.Структура, ОбразецОформления, Поле, ЭтоРесурс);
	КонецЕсли;
КонецПроцедуры

Функция сВО_ЭлементОформленияГруппировкиОтчета(Группировка, Поле)
	Оформление = Группировка.УсловноеОформление;

	Для Каждого ЭлементОформления Из Оформление.Элементы Цикл
		Для Каждого ПолеОформления Из ЭлементОформления.Поля.Элементы Цикл
			Если ПолеОформления.Поле = Поле Тогда
				Возврат ЭлементОформления;
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;

	Возврат Неопределено;
КонецФункции

Функция сВО_ИдентификаторЭлементаОформленияГруппировкиОтчета(Группировка, Поле)
	ЭлементОформления = сВО_ЭлементОформленияГруппировкиОтчета(Группировка, Поле);

	Если ЭлементОформления <> Неопределено Тогда
		Возврат Группировка.УсловноеОформление.ПолучитьИдентификаторПоОбъекту(ЭлементОформления);
	КонецЕсли;

	Возврат Неопределено;
КонецФункции

Функция сВО_УсловиеОформленияГруппировкиОтчета(Поле, Значение)
	Если Значение = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;

	Условие = Новый Структура;
	Условие.Вставить("ЛевоеЗначение",	Поле);
	Условие.Вставить("ВидСравнения",	сВО_ВидСравненияУсловияОформления(Значение));
	Условие.Вставить("ПравоеЗначение",	Значение);

	Возврат Условие;
КонецФункции

Процедура сВО_ПослеВыбораПоля(ВыбранноеПоле, ДополнительныеПараметры) Экспорт
	Если ВыбранноеПоле = Неопределено Тогда
		Возврат;
	КонецЕсли;

	Форма = ДополнительныеПараметры.Форма;
	Поле = ?(ТипЗнч(ВыбранноеПоле) = Тип("ЭлементСпискаЗначений"), ВыбранноеПоле.Значение, ВыбранноеПоле);

	Если Поле = "Еще" Тогда
		сВО_ВыбратьПолеОтчета(Форма, ДополнительныеПараметры.Действие, ДополнительныеПараметры.ИмяКоллекции);

		Возврат;
	КонецЕсли;

	СвойстваЗаголовка	= ДополнительныеПараметры.СвойстваЗаголовка;

	КомпоновщикНастроек	= сВО_КомпоновщикНастроекОтчета(Форма);
	Настройки			= сВО_ИспользуемыеНастройки(Форма, СвойстваЗаголовка.ИдентификаторНастроек);
	Действие			= ДополнительныеПараметры.Действие;
	РолиПолей			= Форма.НастройкиОтчета.СвойстваРезультата.РолиПолей;

	Если СтрНачинаетсяС(Действие, "ВставитьПоле") Тогда
		сВО_ДобавитьФормулу(Настройки, Настройки.ДоступныеПоляВыбора, Поле);
		сВО_ВставитьПоле(КомпоновщикНастроек, Настройки, Поле, Действие, СвойстваЗаголовка, РолиПолей);
	ИначеЕсли СтрНачинаетсяС(Действие, "ВставитьГруппировку") Тогда
		сВО_ДобавитьФормулу(Настройки, Настройки.ДоступныеПоляГруппировок, Поле);
		сВО_ВставитьГруппировку(Настройки, Действие, Поле, СвойстваЗаголовка);
	КонецЕсли;

	сВО_ОповеститьОЗавершенииКонтекстнойНастройки(Форма, Действие);
КонецПроцедуры

Процедура сВО_ВставитьПоле(КомпоновщикНастроек, Настройки, ВыбранноеПоле, Действие, СвойстваЗаголовка, РолиПолей)
	КомпоновщикНастроек.РазвернутьАвтоПоля();

	Группировка	= Настройки.ПолучитьОбъектПоИдентификатору(СвойстваЗаголовка.ИдентификаторГруппировки);

	сВО_ВставитьПолеВПоляГруппировки(Группировка.ПоляГруппировки, ВыбранноеПоле, СвойстваЗаголовка.Поле, Действие, РолиПолей);
	сВО_ВставитьПолеВГруппировкуРаздела(Группировка, ВыбранноеПоле, СвойстваЗаголовка.Поле, Действие, РолиПолей);
КонецПроцедуры

Процедура сВО_ВставитьПолеВПоляГруппировки(Поля, ВыбранноеПоле, ТекущееПоле, Действие, РолиПолей)
	Если ВыбранноеПоле.Ресурс Тогда
		Возврат;
	КонецЕсли;

	НайденноеТекущееПоле = ВариантыОтчетовКлиентСервер.сВО_ПолеОтчета(Поля, ТекущееПоле);

	Если НайденноеТекущееПоле = Неопределено Тогда
		Возврат;
	КонецЕсли;

	ТекущееПолеЯвляетсяПериодом		= (РолиПолей.Периоды[ТекущееПоле] <> Неопределено);
	ВыбранноеПолеЯвляетсяПериодом	= (РолиПолей.Периоды[ВыбранноеПоле.Поле] <> Неопределено);

	Если ТекущееПолеЯвляетсяПериодом И Не ВыбранноеПолеЯвляетсяПериодом Или Не ТекущееПолеЯвляетсяПериодом И ВыбранноеПолеЯвляетсяПериодом Тогда
		Возврат;
	КонецЕсли;

	ИндексПоля = Поля.Элементы.Индекс(НайденноеТекущееПоле);

	Если Действие = "ВставитьПолеСправа" Тогда
		ИндексПоля = ИндексПоля + 1;
	КонецЕсли;

	НайденноеПоле	= ВариантыОтчетовКлиентСервер.сВО_ПолеОтчета(Поля, ВыбранноеПоле.Поле);
	ВставляемоеПоле	= Неопределено;

	Если НайденноеПоле <> Неопределено Тогда
		Если Поля.Элементы.Индекс(НайденноеПоле) = ИндексПоля Тогда
			ВставляемоеПоле					= НайденноеПоле;
			ВставляемоеПоле.Использование	= Истина;
		Иначе
			Поля.Элементы.Удалить(НайденноеПоле);
		КонецЕсли;
	КонецЕсли;

	Если ВставляемоеПоле <> Неопределено Тогда
		Возврат;
	КонецЕсли;

	ВставляемоеПоле = Поля.Элементы.Вставить(ИндексПоля, Тип("ПолеГруппировкиКомпоновкиДанных"));
	ЗаполнитьЗначенияСвойств(ВставляемоеПоле, ВыбранноеПоле);
КонецПроцедуры

Процедура сВО_ВставитьПолеВГруппировкуРаздела(Группировка, ВыбранноеПоле, ТекущееПоле, Действие, РолиПолей)
	Если ТипЗнч(Группировка) <> Тип("ГруппировкаКомпоновкиДанных") И ТипЗнч(Группировка) <> Тип("ГруппировкаТаблицыКомпоновкиДанных") Тогда
		Возврат;
	КонецЕсли;

	Поля					= Группировка.Выбор;
	НайденноеТекущееПоле	= ВариантыОтчетовКлиентСервер.сВО_ПолеОтчета(Поля, ТекущееПоле);

	Если НайденноеТекущееПоле = Неопределено Тогда
		ИндексПоля = 0;
	Иначе
		ИндексПоля = Поля.Элементы.Индекс(НайденноеТекущееПоле);

		Если Действие = "ВставитьПолеСправа" Тогда
			ИндексПоля = ИндексПоля + 1;
		КонецЕсли;
	КонецЕсли;

	НайденноеПоле	= ВариантыОтчетовКлиентСервер.сВО_ПолеОтчета(Поля, ВыбранноеПоле.Поле, Ложь);
	ВставляемоеПоле	= Неопределено;
	ЗаголовокПоля	= ВыбранноеПоле.Заголовок;

	Если НайденноеПоле <> Неопределено Тогда
		Если Поля.Элементы.Индекс(НайденноеПоле) = ИндексПоля Тогда
			ВставляемоеПоле					= НайденноеПоле;
			ВставляемоеПоле.Использование	= Истина;
		Иначе
			ЗаголовокПоля	= НайденноеПоле.Заголовок;
			Поля.Элементы.Удалить(НайденноеПоле);
			ИндексПоля		= ИндексПоля - 1;
		КонецЕсли;
	КонецЕсли;

	Если ВставляемоеПоле = Неопределено Тогда
		ВставляемоеПоле = Поля.Элементы.Вставить(ИндексПоля, Тип("ВыбранноеПолеКомпоновкиДанных"));
		ЗаполнитьЗначенияСвойств(ВставляемоеПоле, ВыбранноеПоле);
		ВставляемоеПоле.Заголовок = ЗаголовокПоля;
	КонецЕсли;

	сВО_УстановитьВыводРеквизитовГруппировкиОтдельно(Группировка);

	сВО_ВставитьПолеВГруппировкиРаздела(Группировка, ВыбранноеПоле, ТекущееПоле, Действие, РолиПолей);
КонецПроцедуры

Процедура сВО_ВставитьПолеВГруппировкиРаздела(Родитель, ВыбранноеПоле, ТекущееПоле, Действие, РолиПолей)
	Если Не ВыбранноеПоле.Ресурс Тогда
		Возврат;
	КонецЕсли;

	Если ТипЗнч(Родитель) = Тип("ТаблицаКомпоновкиДанных") Тогда
		Группировки = Родитель.Строки;
	Иначе
		Группировки = Родитель.Структура;
	КонецЕсли;

	Для Каждого Группировка Из Группировки Цикл
		сВО_ВставитьПолеВГруппировкуРаздела(Группировка, ВыбранноеПоле, ТекущееПоле, Действие, РолиПолей);
	КонецЦикла;
КонецПроцедуры

Процедура сВО_ВставитьГруппировку(Настройки, Действие, Поле, СвойстваЗаголовка)
	Раздел		= Настройки.ПолучитьОбъектПоИдентификатору(СвойстваЗаголовка.ИдентификаторРаздела);
	Группировка	= Настройки.ПолучитьОбъектПоИдентификатору(СвойстваЗаголовка.ИдентификаторГруппировки);

	КоллекцияГруппировок = сВО_КоллекцияГруппировокРаздела(Раздел, Группировка, СвойстваЗаголовка.ИдентификаторГруппировки, Действие);

	ПеремещаемыеГруппировки = Новый Массив;

	Для Каждого Элемент Из КоллекцияГруппировок.Группировки Цикл
		ПеремещаемыеГруппировки.Добавить(Элемент);
	КонецЦикла;

	Если ТипЗнч(КоллекцияГруппировок.Группировки) = Тип("КоллекцияЭлементовСтруктурыНастроекКомпоновкиДанных") Тогда
		НоваяГруппировка = КоллекцияГруппировок.Группировки.Вставить(КоллекцияГруппировок.ИндексГруппировки, Тип("ГруппировкаКомпоновкиДанных")); // ГруппировкаКомпоновкиДанных
	Иначе
		НоваяГруппировка = КоллекцияГруппировок.Группировки.Вставить(КоллекцияГруппировок.ИндексГруппировки);
	КонецЕсли;

	НовоеПоле				= НоваяГруппировка.ПоляГруппировки.Элементы.Добавить(Тип("ПолеГруппировкиКомпоновкиДанных"));
	НовоеПоле.Поле			= ?(ТипЗнч(Поле) = Тип("ПолеКомпоновкиДанных"), Поле, Поле.Поле);
	НовоеПоле.Использование	= Истина;

	НоваяГруппировка.Выбор.Элементы.Добавить(Тип("АвтоВыбранноеПолеКомпоновкиДанных"));
	НоваяГруппировка.Порядок.Элементы.Добавить(Тип("АвтоЭлементПорядкаКомпоновкиДанных"));

	Для Каждого Группировка Из ПеремещаемыеГруппировки Цикл
		ПоискЭлементов = Новый Соответствие;
		ВариантыОтчетовКлиентСервер.О_СкопироватьРекурсивно(Настройки, Группировка, НоваяГруппировка.Структура, КоллекцияГруппировок.ИндексГруппировки, ПоискЭлементов);

		КоллекцияГруппировок.Группировки.Удалить(Группировка);
	КонецЦикла;
КонецПроцедуры

Функция сВО_КоллекцияГруппировокРаздела(Раздел, Группировка, ИдентификаторГруппировки, Действие)
	Коллекция = Новый Структура;
	Коллекция.Вставить("Группировки", Неопределено);
	Коллекция.Вставить("ИндексГруппировки", 0);

	Если Действие = "ВставитьГруппировкуНиже" Тогда
		Коллекция.Группировки = Группировка.Структура;

		Возврат Коллекция;
	КонецЕсли;

	Родитель = Группировка.Родитель; // ГруппировкаКомпоновкиДанных, ГруппировкаТаблицыКомпоновкиДанных

	Если Родитель <> Раздел Тогда
		Группировки					= Родитель.Структура; // КоллекцияЭлементовСтруктурыНастроекКомпоновкиДанных, КоллекцияЭлементовСтруктурыТаблицыКомпоновкиДанных
		Коллекция.Группировки		= Группировки;
		Коллекция.ИндексГруппировки = Группировки.Индекс(Группировка);

		Возврат Коллекция;
	КонецЕсли;

	Если ТипЗнч(Раздел) = Тип("ТаблицаКомпоновкиДанных") Тогда
		Коллекция.Группировки = ?(СтрНайти(ИдентификаторГруппировки, "/row/") > 0, Раздел.Строки, Раздел.Колонки);
	Иначе
		Коллекция.Группировки = Раздел.Структура;
	КонецЕсли;

	Группировки					= Коллекция.Группировки; // КоллекцияЭлементовСтруктурыНастроекКомпоновкиДанных, КоллекцияЭлементовСтруктурыТаблицыКомпоновкиДанных 
	Коллекция.ИндексГруппировки	= Группировки.Индекс(Группировка);

	Возврат Коллекция;
КонецФункции

Процедура сВО_ПоказатьРасширеннуюНастройкуФильтра(Форма, СвойстваЗаголовка) Экспорт
	Если Не сВО_ДействиеДоступно(Форма,, СвойстваЗаголовка) Тогда
		Возврат;
	КонецЕсли;

	НастройкиОтчета = Форма.НастройкиОтчета;

	Поле			= Форма.Элементы.ОтчетТабличныйДокумент; // ПолеТабличногоДокумента
	ТекущаяОбласть	= Поле.ТекущаяОбласть;

	Ячейка = Новый Структура("Текст, Расшифровка", "");

	Если ТекущаяОбласть.Верх <> СвойстваЗаголовка.Верх Или ТекущаяОбласть.Низ <> СвойстваЗаголовка.Низ Или ТекущаяОбласть.Лево <> СвойстваЗаголовка.Лево Или ТекущаяОбласть.Право <> СвойстваЗаголовка.Право Тогда
		ЗаполнитьЗначенияСвойств(Ячейка, ТекущаяОбласть);
	КонецЕсли;

	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("КлючТекущегоВарианта",	Форма.КлючТекущегоВарианта);
	ПараметрыФормы.Вставить("НастройкиОтчета",		НастройкиОтчета);
	ПараметрыФормы.Вставить("КомпоновщикНастроек",	сВО_КомпоновщикНастроекОтчета(Форма));
	ПараметрыФормы.Вставить("СвойстваЗаголовка",	СвойстваЗаголовка);
	ПараметрыФормы.Вставить("ДанныеРасшифровки",	Форма.ОтчетДанныеРасшифровки);
	ПараметрыФормы.Вставить("Ячейка",				Ячейка);

	ОткрытьФорму("ХранилищеНастроек.ХранилищеВариантовОтчетов.Форма.РасширеннаяНастройкаФильтраОтчета", ПараметрыФормы, Форма, Форма.УникальныйИдентификатор);
КонецПроцедуры

Процедура сВО_СнятьФильтр(Форма, СвойстваЗаголовка, ДанныеРасшифровки = Неопределено) Экспорт
	ЭтоГруппировка = (ДанныеРасшифровки <> Неопределено И ДанныеРасшифровки.Тип = "Группировка");

	Настройки = сВО_ИспользуемыеНастройки(Форма, СвойстваЗаголовка.ИдентификаторНастроек);

	Фильтры = ВариантыОтчетовКлиентСервер.сВО_ФильтрыРазделаОтчета(Настройки, СвойстваЗаголовка, ЭтоГруппировка);
	Фильтр = ВариантыОтчетовКлиентСервер.сВО_ФильтрРазделаОтчета(Фильтры, СвойстваЗаголовка.Поле);

	Если Фильтр = Неопределено Тогда
		Фильтры	= ВариантыОтчетовКлиентСервер.сВО_ФильтрыРазделаОтчета(Настройки, СвойстваЗаголовка, Не ЭтоГруппировка);
		Фильтр	= ВариантыОтчетовКлиентСервер.сВО_ФильтрРазделаОтчета(Фильтры, СвойстваЗаголовка.Поле);
	КонецЕсли;

	Если Фильтр = Неопределено Тогда
		ТекстПредупреждения = СтрШаблон("По полю ""%1"" отсутствуют фильтры", СвойстваЗаголовка.Текст);

		ПоказатьПредупреждение(, ТекстПредупреждения);

		Возврат;
	КонецЕсли;

	Фильтры.Элементы.Удалить(Фильтр);

	сВО_ОповеститьОЗавершенииКонтекстнойНастройки(Форма, "СнятьФильтр");
КонецПроцедуры

Процедура сВО_СброситьСортировку(Настройки, СвойстваЗаголовка, ЭлементыСтруктуры = Неопределено)
	Если ЭлементыСтруктуры = Неопределено Тогда
		Настройки.ОчиститьПорядокЭлемента(Настройки);
		ЭлементыСтруктуры = Настройки.Структура;
	КонецЕсли;

	Для Каждого Элемент Из ЭлементыСтруктуры Цикл
		Настройки.ОчиститьПорядокЭлемента(Элемент);
		ТипЭлемента = ТипЗнч(Элемент);

		Если ТипЭлемента = Тип("ТаблицаКомпоновкиДанных") Тогда
			сВО_СброситьСортировку(Настройки, СвойстваЗаголовка, Элемент.Строки);
			сВО_СброситьСортировку(Настройки, СвойстваЗаголовка, Элемент.Колонки);
		ИначеЕсли ТипЭлемента = Тип("НастройкиВложенногоОбъектаКомпоновкиДанных") Тогда
			сВО_СброситьСортировку(Элемент.Настройки, СвойстваЗаголовка);
		ИначеЕсли ТипЭлемента = Тип("ГруппировкаКомпоновкиДанных") Или ТипЭлемента = Тип("ГруппировкаТаблицыКомпоновкиДанных") Тогда
			сВО_СброситьСортировку(Настройки, СвойстваЗаголовка, Элемент.Структура);
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

Процедура сВО_ОчиститьОформлениеГруппировокРазделаОтчета(Группировки, СвойстваЗаголовка)
	Для Каждого Группировка Из Группировки Цикл
		сВО_ОчиститьОформлениеГруппировкиРазделаОтчета(Группировка, СвойстваЗаголовка);
	КонецЦикла;
КонецПроцедуры

Процедура сВО_ОчиститьОформлениеГруппировкиРазделаОтчета(Группировка, СвойстваЗаголовка)
	Если ТипЗнч(Группировка) = Тип("ТаблицаКомпоновкиДанных") Тогда
		сВО_ОчиститьОформлениеГруппировокРазделаОтчета(Группировка.Строки, СвойстваЗаголовка);
	Иначе
		сВО_ОчиститьОформлениеГруппировкиОтчета(Группировка, СвойстваЗаголовка);

		сВО_ОчиститьОформлениеГруппировокРазделаОтчета(Группировка.Структура, СвойстваЗаголовка)
	КонецЕсли;
КонецПроцедуры

Процедура сВО_ОчиститьОформлениеГруппировкиОтчета(Группировка, СвойстваЗаголовка)
	Для Каждого ЭлементОформления Из Группировка.УсловноеОформление.Элементы Цикл
		ОформляемыеПоля = ЭлементОформления.Поля.Элементы;

		ОформляемыеПоляПодходящие	= Новый Массив;
		ОформляемыеПоляИспользуемые	= Новый Массив;

		Для Каждого ОформляемоеПоле Из ОформляемыеПоля Цикл
			Если ОформляемоеПоле.Поле = СвойстваЗаголовка.Поле Тогда
				ОформляемыеПоляПодходящие.Добавить(ОформляемоеПоле);
			КонецЕсли;

			Если ОформляемоеПоле.Использование Тогда
				ОформляемыеПоляИспользуемые.Добавить(ОформляемоеПоле);
			КонецЕсли;
		КонецЦикла;

		Если ОформляемыеПоляИспользуемые.Количество() = 1 И ОформляемыеПоляИспользуемые[0].Поле = СвойстваЗаголовка.Поле Тогда
			ОформляемыеПоляИспользуемые.Очистить();
		ИначеЕсли ОформляемыеПоляПодходящие.Количество() > 0 Тогда
			ОформляемыеПоляПодходящие[0].Использование = Ложь;
		КонецЕсли;

		Если ОформляемыеПоляИспользуемые.Количество() = 0 Тогда
			ЭлементОформления.Использование = Ложь;
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

Функция сВО_ВидСравненияУсловияОформления(Значение)
	Если ТипЗнч(Значение) = Тип("СписокЗначений") Тогда
		Возврат ВидСравненияКомпоновкиДанных.ВСписке;
	КонецЕсли;

	Возврат ВидСравненияКомпоновкиДанных.Равно;
КонецФункции

Функция сВО_ДействиеДоступно(Форма, Команда = Неопределено, СвойстваЗаголовка = Неопределено)
	Если СвойстваЗаголовка = Неопределено Тогда
		СвойстваЗаголовка = сВО_СвойстваЗаголовкаОтчета(Форма);
	КонецЕсли;

	ДействиеКоманды		= сВО_ДействиеКоманды(Команда);

	Если ТипЗнч(СвойстваЗаголовка) <> Тип("Структура") Или ДействиеКоманды <> Неопределено И Не СвойстваЗаголовка[ДействиеКоманды] Тогда
		ПоказатьПредупреждение(, "Действие недоступно");

		Возврат Ложь;
	КонецЕсли;

	Возврат Истина;
КонецФункции

Процедура сВО_ВыполнитьРасшифровку(ВыполненноеДействие, ПараметрВыполненногоДействия, ДополнительныеПараметры) Экспорт
	Если ВыполненноеДействие = Неопределено Или ВыполненноеДействие = ДействиеОбработкиРасшифровкиКомпоновкиДанных.Нет Тогда
		Возврат;
	КонецЕсли;

	Форма			= ДополнительныеПараметры.Форма;
	Данные			= ДополнительныеПараметры.Данные;
	СвойстваОбласти	= ДополнительныеПараметры.СвойстваОбласти;
	Меню			= ДополнительныеПараметры.Меню;

	Если СвойстваОбласти.ЭтоЗаголовок Тогда
		СвойстваЗаголовка = СвойстваОбласти.СвойстваЗаголовка;

		Если ТипЗнч(ВыполненноеДействие) = Тип("Строка") И СвойстваЗаголовка.Свойство(ВыполненноеДействие) И Не СвойстваЗаголовка[ВыполненноеДействие] Тогда
			ТекстСообщения = СтрШаблон("Действие ""%1"" недоступно", Меню.НайтиПоЗначению(ВыполненноеДействие));

			ПоказатьПредупреждение(, ТекстСообщения);
		КонецЕсли;

		Если СтрНайти(СвойстваЗаголовка.ИдентификаторГруппировки, "/column/") = 0 Тогда
			Возврат;
		КонецЕсли;
	КонецЕсли;

	Если ТипЗнч(ВыполненноеДействие) = Тип("Строка") И ВыполненноеДействие = "СнятьФильтр" Тогда
		сВО_СнятьФильтр(Форма, СвойстваОбласти.СвойстваЗаголовка, Данные);

		Возврат;
	КонецЕсли;

	Если ТипЗнч(ВыполненноеДействие) = Тип("ВидСравненияКомпоновкиДанных") Или ТипЗнч(ВыполненноеДействие) = Тип("Строка") И СтрНачинаетсяС(ВыполненноеДействие, "Фильтровать") Тогда
		сВО_Фильтровать(Форма, ВыполненноеДействие, СвойстваОбласти.СвойстваЗаголовка, Данные);

		Возврат;
	КонецЕсли;

	Если ТипЗнч(ВыполненноеДействие) = Тип("НаправлениеСортировкиКомпоновкиДанных") Тогда
		Если ВыполненноеДействие = НаправлениеСортировкиКомпоновкиДанных.Возр Тогда
			Команда = Форма.Команды.Найти("СортироватьПоВозрастанию");
		Иначе
			Команда = Форма.Команды.Найти("СортироватьПоУбыванию");
		КонецЕсли;

		сВО_Сортировать(Форма, Команда, СвойстваОбласти.СвойстваЗаголовка);

		Возврат;
	КонецЕсли;

	Если ТипЗнч(ВыполненноеДействие) = Тип("Строка") И ВыполненноеДействие = "ОчиститьОформление" Тогда
		сВО_ОчиститьОформление(Форма, СвойстваОбласти.СвойстваЗаголовка);

		Возврат;
	КонецЕсли;

	Если ТипЗнч(ВыполненноеДействие) = Тип("Строка") И (ВыполненноеДействие = "ОформитьКрасным" Или ВыполненноеДействие = "ОформитьЖелтым" Или ВыполненноеДействие = "ОформитьЗеленым" Или ВыполненноеДействие = "ОформитьОтрицательные" Или ВыполненноеДействие = "ОформитьПоложительные" Или ВыполненноеДействие = "УстановитьВысотуСтроки" Или ВыполненноеДействие = "УстановитьШиринуКолонки" Или ВыполненноеДействие = "ОформитьЕще") Тогда
		Команда = Форма.Команды.Найти(ВыполненноеДействие);

		Если ВыполненноеДействие = "ОформитьКрасным" Или ВыполненноеДействие = "ОформитьОтрицательные" Тогда
			сВО_ОформитьКрасным(Форма, Команда, СвойстваОбласти.СвойстваЗаголовка, Данные.Значения);
		ИначеЕсли ВыполненноеДействие = "ОформитьЖелтым" Тогда
			сВО_ОформитьЖелтым(Форма, Команда, СвойстваОбласти.СвойстваЗаголовка, Данные.Значения);
		ИначеЕсли ВыполненноеДействие = "ОформитьЗеленым" Или ВыполненноеДействие = "ОформитьПоложительные" Тогда
			сВО_ОформитьЗеленым(Форма, Команда, СвойстваОбласти.СвойстваЗаголовка, Данные.Значения);
		ИначеЕсли ВыполненноеДействие = "УстановитьВысотуСтроки" Тогда
			сВО_УстановитьВысотуСтроки(Форма, Команда, СвойстваОбласти.СвойстваЗаголовка);
		ИначеЕсли ВыполненноеДействие = "УстановитьШиринуКолонки" Тогда
			сВО_УстановитьШиринуКолонки(Форма, Команда, СвойстваОбласти.СвойстваЗаголовка);
		ИначеЕсли ВыполненноеДействие = "ОформитьЕще" Тогда
			сВО_ОформитьЕще(Форма, Команда, СвойстваОбласти.СвойстваЗаголовка, Данные.Значения);
		КонецЕсли;

		Возврат;
	КонецЕсли;

	Если ВыполненноеДействие = ДействиеОбработкиРасшифровкиКомпоновкиДанных.ОткрытьЗначение Тогда
		ПоказатьЗначение(, Данные.Значение);

		Возврат;
	КонецЕсли;

	Если ВыполненноеДействие = ДействиеОбработкиРасшифровкиКомпоновкиДанных.Расшифровать Тогда
		ДополнительныеПараметры.Вставить("Настройки", ПараметрВыполненногоДействия);

		сВО_ОткрытьФормуОтчета(Форма, ДополнительныеПараметры);
	КонецЕсли;
КонецПроцедуры

Процедура сВО_ОткрытьФормуОтчета(Форма, ПараметрыОткрытия)
	НастройкиОтчета	= Форма.НастройкиОтчета;
	Расшифровка		= ПараметрыОткрытия.Расшифровка;

	СтандартнаяОбработка = Истина;

	Если СтандартнаяОбработка Тогда
		Расшифровка = Новый ОписаниеОбработкиРасшифровкиКомпоновкиДанных(Форма.ОтчетДанныеРасшифровки, ПараметрыОткрытия.Расшифровка, ПараметрыОткрытия.Настройки); 

		ПредставлениеВарианта = СтрШаблон("%1 (Расшифровка)", Форма.ПредставлениеТекущегоВарианта);

		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("Расшифровка",				Расшифровка);
		ПараметрыФормы.Вставить("СформироватьПриОткрытии",	Истина);
		ПараметрыФормы.Вставить("ПредставлениеВарианта",	ПредставлениеВарианта);
		ПараметрыФормы.Вставить("НастройкиОтчета",			НастройкиОтчета);

		ОткрытьФорму(НастройкиОтчета.ПолноеИмя + ".Форма", ПараметрыФормы, Форма, Форма.УникальныйИдентификатор);
	КонецЕсли;
КонецПроцедуры

Процедура сВО_ПриАктивизацииРезультатаОтчета(Форма, Элемент) Экспорт
	Область = Элемент.ТекущаяОбласть; // ОбластьЯчеекТабличногоДокумента

	Если ТипЗнч(Область) <> Тип("ОбластьЯчеекТабличногоДокумента") Тогда
		Возврат;
	КонецЕсли;

	НастройкиОтчета		= Форма.НастройкиОтчета;
	СвойстваРезультата	= НастройкиОтчета.СвойстваРезультата;
	Элементы			= Форма.Элементы;
	ПолеОтчета			= Элементы.ОтчетТабличныйДокумент; // ПолеФормы, РасширениеПоляФормыДляПоляТабличногоДокумента

	СвойстваЗаголовка	= СвойстваРезультата.Заголовки[Область.Имя];
	ЭтоРасшифровка		= Область.ТипОбласти = ТипОбластиЯчеекТабличногоДокумента.Прямоугольник И ТипЗнч(Область.Расшифровка) = Тип("ИдентификаторРасшифровкиКомпоновкиДанных");

	Элементы.КонтекстноеМенюОбластиЗаголовка.Видимость			= (ТипЗнч(СвойстваЗаголовка) = Тип("Структура")) И ТипЗнч(СвойстваЗаголовка.Поле) = Тип("ПолеКомпоновкиДанных");
	Элементы.КонтекстноеМенюОбластиДанных.Видимость				= Не Элементы.КонтекстноеМенюОбластиЗаголовка.Видимость И ЭтоРасшифровка;
	Элементы.ГруппаУровниГруппировокКонтекстноеМеню.Видимость		= Не ЭтоРасшифровка;
	Элементы.КонтекстноеМенюОбластиОбщееРедактирование.Видимость	= Не ЭтоРасшифровка;

	Элементы.КонтекстноеМенюОбластиДанныхВставитьИзБуфераОбмена.Доступность	= ПолеОтчета.Редактирование;
	Элементы.КонтекстноеМенюОбластиОбщееВставитьИзБуфераОбмена.Доступность	= ПолеОтчета.Редактирование;

	ВариантыОтчетовКлиентСервер.сВО_ОпределитьДоступностьДействийКонтекстногоМеню(Форма, СвойстваЗаголовка);

	Если Элементы.КонтекстноеМенюОбластиЗаголовка.Видимость Тогда
		ЗаголовокКнопки = СтрШаблон("Сгруппировать по полю %1", СвойстваЗаголовка.Текст);

		БазоваяПодсистемаКлиентСервер.ОН_УстановитьСвойствоЭлементаФормы(Элементы, "КонтекстноеМенюОбластиЗаголовкаСгруппироватьПоВыбранномуПолю", "Заголовок", ЗаголовокКнопки);
	КонецЕсли;
КонецПроцедуры

Функция сВО_ЭтоСобытиеКонтекстнойНастройки(Событие) Экспорт
	События = сВО_СобытияКонтекстнойНастройки();

	Возврат События[Событие] = Истина;
КонецФункции

Функция сВО_СобытияКонтекстнойНастройки()
	События = Новый Соответствие;
	События.Вставить("СгруппироватьПоВыбранномуПолю",	Истина);

	События.Вставить("ВставитьПолеСлева",			Истина);
	События.Вставить("ВставитьПолеСправа",			Истина);
	События.Вставить("ВставитьГруппировкуВыше",		Истина);
	События.Вставить("ВставитьГруппировкуНиже",		Истина);

	События.Вставить("ПереместитьПолеВлево",		Истина);
	События.Вставить("ПереместитьПолеВправо",		Истина);
	События.Вставить("ПереместитьПолеВыше",			Истина);
	События.Вставить("ПереместитьПолеНиже",			Истина);

	События.Вставить("СкрытьПоле",					Истина);
	События.Вставить("ПереименоватьПоле",			Истина);

	События.Вставить("СнятьФильтр",					Истина);
	События.Вставить("Фильтровать",					Истина);
	События.Вставить("ФильтроватьИСформировать",	Истина);

	События.Вставить("СортироватьПоВозрастанию",	Истина);
	События.Вставить("СортироватьПоУбыванию",		Истина);

	События.Вставить("ОчиститьОформление",			Истина);

	События.Вставить("ОформитьКрасным",				Истина);
	События.Вставить("ОформитьЖелтым",				Истина);
	События.Вставить("ОформитьЗеленым",				Истина);

	События.Вставить("ОформитьОтрицательные",		Истина);
	События.Вставить("ОформитьПоложительные",		Истина);

	События.Вставить("УстановитьВысотуСтроки",		Истина);
	События.Вставить("УстановитьШиринуКолонки",		Истина);

	События.Вставить("ОформитьЕще",					Истина);

	Возврат События;
КонецФункции

Функция сВО_ПараметрыСохраненияВариантаОтчетаВФайл(Форма) Экспорт
	ПараметрыСохранения = Новый Структура;

	НастройкиОтчета		= Форма.НастройкиОтчета;
	КомпоновщикНастроек	= Форма.Отчет.КомпоновщикНастроек;

	ПараметрыСохранения.Вставить("Ссылка",									НастройкиОтчета.ВариантСсылка);
	ПараметрыСохранения.Вставить("ИмяОтчета",								НастройкиОтчета.ПолноеИмя);
	ПараметрыСохранения.Вставить("Настройки",								КомпоновщикНастроек.Настройки);
	ПараметрыСохранения.Вставить("КлючВарианта",							Форма.КлючТекущегоВарианта);
	ПараметрыСохранения.Вставить("ПредставлениеВарианта",					Форма.ПредставлениеТекущегоВарианта);
	ПараметрыСохранения.Вставить("КлючПользовательскихНастроек",			Форма.КлючТекущихПользовательскихНастроек);
	ПараметрыСохранения.Вставить("ПредставлениеПользовательскихНастроек",	Форма.ПредставлениеТекущихПользовательскихНастроек);

	Возврат ПараметрыСохранения;
КонецФункции

#КонецОбласти

#Область ОтчетыКлиент

Процедура О_СформироватьОтчет(ФормаОтчета, ОбработчикЗавершения = Неопределено) Экспорт
	Если ТипЗнч(ОбработчикЗавершения) = Тип("ОписаниеОповещения") Тогда
		ФормаОтчета.ОбработчикПослеФормированияНаКлиенте = ОбработчикЗавершения;
	КонецЕсли;
	ФормаОтчета.ПодключитьОбработчикОжидания("Сформировать", 0.1, Истина);
КонецПроцедуры

Функция О_ЗначениеТипаИспользованиеГруппИЭлементов(ИсходноеЗначение, Условие = Неопределено) Экспорт
	Если Условие <> Неопределено Тогда
		Если Условие = ВидСравненияКомпоновкиДанных.ВСпискеПоИерархии Или Условие = ВидСравненияКомпоновкиДанных.НеВСпискеПоИерархии Тогда
			Если ИсходноеЗначение = ГруппыИЭлементы.Группы Или ИсходноеЗначение = ИспользованиеГруппИЭлементов.Группы Тогда
				Возврат ИспользованиеГруппИЭлементов.Группы;
			Иначе
				Возврат ИспользованиеГруппИЭлементов.ГруппыИЭлементы;
			КонецЕсли;
		ИначеЕсли Условие = ВидСравненияКомпоновкиДанных.ВИерархии Или Условие = ВидСравненияКомпоновкиДанных.НеВИерархии Тогда
			Возврат ИспользованиеГруппИЭлементов.Группы;
		КонецЕсли;
	КонецЕсли;

	Если ТипЗнч(ИсходноеЗначение) = Тип("ИспользованиеГруппИЭлементов") Тогда
		Возврат ИсходноеЗначение;
	ИначеЕсли ИсходноеЗначение = ГруппыИЭлементы.Элементы Тогда
		Возврат ИспользованиеГруппИЭлементов.Элементы;
	ИначеЕсли ИсходноеЗначение = ГруппыИЭлементы.ГруппыИЭлементы Тогда
		Возврат ИспользованиеГруппИЭлементов.ГруппыИЭлементы;
	ИначеЕсли ИсходноеЗначение = ГруппыИЭлементы.Группы Тогда
		Возврат ИспользованиеГруппИЭлементов.Группы;
	КонецЕсли;

	Возврат Неопределено;
КонецФункции

Процедура О_КэшироватьЗначениеОтбора(КомпоновщикНастроек, ЭлементОтбора, ЗначениеОтбора) Экспорт
	ДополнительныеСвойства = КомпоновщикНастроек.ПользовательскиеНастройки.ДополнительныеСвойства;

	КэшЗначенийОтборов = БазоваяПодсистемаКлиентСервер.ОН_СвойствоСтруктуры(ДополнительныеСвойства, "КэшЗначенийОтборов", Новый Соответствие);

	ЭлементОтбораОсновныхНастроек = Неопределено;

	Если ЗначениеЗаполнено(ЭлементОтбора.ИдентификаторПользовательскойНастройки) Тогда
		КэшЗначенийОтборов.Вставить(ЭлементОтбора.ИдентификаторПользовательскойНастройки, ЗначениеОтбора);

		НайденныеЭлементыНастроек = КомпоновщикНастроек.ПользовательскиеНастройки.ПолучитьОсновныеНастройкиПоИдентификаторуПользовательскойНастройки(ЭлементОтбора.ИдентификаторПользовательскойНастройки);

		Если НайденныеЭлементыНастроек.Количество() > 0 Тогда
			ЭлементОтбораОсновныхНастроек = НайденныеЭлементыНастроек[0];
		КонецЕсли;
	Иначе
		ЭлементОтбораОсновныхНастроек = ЭлементОтбора;
	КонецЕсли;

	Если ТипЗнч(ЭлементОтбораОсновныхНастроек) = Тип("ЭлементОтбораКомпоновкиДанных") Тогда
		КэшЗначенийОтборов.Вставить(ЭлементОтбораОсновныхНастроек.ЛевоеЗначение, ЗначениеОтбора);
	КонецЕсли;

	ДополнительныеСвойства.Вставить("КэшЗначенийОтборов", КэшЗначенийОтборов);
КонецПроцедуры

Функция О_ПолныйПутьКЭлементуНастроек(Знач Настройки, Знач ЭлементНастроек) Экспорт
	Результат					= Новый Массив;
	РодительЭлементаНастроек	= ЭлементНастроек;

	Пока РодительЭлементаНастроек <> Неопределено И РодительЭлементаНастроек <> Настройки Цикл
		ЭлементНастроек				= РодительЭлементаНастроек;
		РодительЭлементаНастроек	= РодительЭлементаНастроек.Родитель;
		ТипРодителя					= ТипЗнч(РодительЭлементаНастроек);

		Если ТипРодителя = Тип("ТаблицаКомпоновкиДанных") Тогда
			СтрокиТаблицы = РодительЭлементаНастроек.Строки; // КоллекцияЭлементовСтруктурыТаблицыКомпоновкиДанных
			Индекс = СтрокиТаблицы.Индекс(ЭлементНастроек);
			Если Индекс = -1 Тогда
				КолонкиТаблицы	= РодительЭлементаНастроек.Колонки; // КоллекцияЭлементовСтруктурыТаблицыКомпоновкиДанных
				Индекс			= КолонкиТаблицы.Индекс(ЭлементНастроек);
				ИмяКоллекции	= "Колонки";
			Иначе
				ИмяКоллекции	= "Строки";
			КонецЕсли;
		ИначеЕсли ТипРодителя = Тип("ДиаграммаКомпоновкиДанных") Тогда
			СерииДиаграммы	= РодительЭлементаНастроек.Серии; // КоллекцияЭлементовСтруктурыДиаграммыКомпоновкиДанных
			Индекс			= СерииДиаграммы.Индекс(ЭлементНастроек);
			Если Индекс = -1 Тогда
				ТочкиДиаграммы	= РодительЭлементаНастроек.Точки; // КоллекцияЭлементовСтруктурыДиаграммыКомпоновкиДанных
				Индекс			= ТочкиДиаграммы.Индекс(ЭлементНастроек);
				ИмяКоллекции	= "Точки";
			Иначе
				ИмяКоллекции	= "Серии";
			КонецЕсли;
		ИначеЕсли ТипРодителя = Тип("НастройкиВложенногоОбъектаКомпоновкиДанных") Тогда
			ИмяКоллекции	= "Настройки";
			Индекс			= Неопределено;
		Иначе
			ИмяКоллекции	= "Структура";
			Индекс			= РодительЭлементаНастроек.Структура.Индекс(ЭлементНастроек);
		КонецЕсли;

		Если Индекс = -1 Тогда
			Возврат Неопределено;
		КонецЕсли;

		Если Индекс <> Неопределено Тогда
			Результат.Вставить(0, Индекс);
		КонецЕсли;

		Результат.Вставить(0, ИмяКоллекции);
	КонецЦикла;

	Возврат СтрСоединить(Результат, "/");
КонецФункции

Процедура О_ВыбратьПериод(Форма, ИмяКоманды, ВариантПериода = Неопределено) Экспорт
	Путь		= СтрЗаменить(ИмяКоманды, "ВыбратьПериод", "Период");
	Период		= Форма[Путь];

	Контекст = Новый Структура;
	Контекст.Вставить("Форма",		Форма);
	Контекст.Вставить("ИмяКоманды",	ИмяКоманды);
	Контекст.Вставить("Путь",		Путь);
	Контекст.Вставить("Период",		Период);

	Обработчик	= Новый ОписаниеОповещения("О_ВыбратьПериодЗавершение", ЭтотОбъект, Контекст);

	СтандартнаяОбработка = Истина;
	Если Не СтандартнаяОбработка Тогда
		Возврат;
	КонецЕсли;

	Если ВариантПериода = Неопределено Тогда
		ВариантПериода = Форма.НастройкиОтчета.ВариантПериода;
	КонецЕсли;

	Если ВариантПериода = ПредопределенноеЗначение("Перечисление.ВариантыПериода.Финансовый") Тогда
		Контекст.Удалить("Форма");

		ОткрытьФорму("ХранилищеНастроек.ХранилищеВариантовОтчетов.Форма.ВыборФинансовогоПериода", Контекст, Форма,,,, Обработчик);
	Иначе
		Диалог			= Новый ДиалогРедактированияСтандартногоПериода;
		Диалог.Период	= Период;
		Диалог.Показать(Обработчик);
	КонецЕсли;
КонецПроцедуры

Процедура О_СдвинутьПериод(Форма, ИмяКоманды) Экспорт
	Если СтрНайти(НРег(ИмяКоманды), "назад") > 0 Тогда
		НаправлениеСдвига	= -1;
		Путь				= СтрЗаменить(ИмяКоманды, "СдвинутьПериодНазад", "Период");
	Иначе
		НаправлениеСдвига	= 1;
		Путь				= СтрЗаменить(ИмяКоманды, "СдвинутьПериодВперед", "Период");
	КонецЕсли;

	Период = Форма[Путь]; // СтандартныйПериод

	Если Не ЗначениеЗаполнено(Период.ДатаНачала) Или Не ЗначениеЗаполнено(Период.ДатаОкончания) Тогда
		Возврат;
	КонецЕсли;

	Если Период.ДатаНачала = НачалоДня(Период.ДатаОкончания) Тогда
		Период.ДатаНачала		= Период.ДатаНачала + 86400 * НаправлениеСдвига;
		Период.ДатаОкончания	= КонецДня(Период.ДатаНачала);
	ИначеЕсли Период.ДатаНачала = НачалоМесяца(Период.ДатаОкончания) Тогда
		Период.ДатаНачала		= ДобавитьМесяц(Период.ДатаНачала, НаправлениеСдвига);
		Период.ДатаОкончания	= КонецМесяца(Период.ДатаНачала);
	ИначеЕсли Период.ДатаНачала = НачалоКвартала(Период.ДатаОкончания) Тогда
		Период.ДатаНачала		= ДобавитьМесяц(Период.ДатаНачала, 3 * НаправлениеСдвига);
		Период.ДатаОкончания	= КонецКвартала(Период.ДатаНачала);
	ИначеЕсли Период.ДатаНачала = НачалоГода(Период.ДатаОкончания) И КонецГода(Период.ДатаНачала) = Период.ДатаОкончания Тогда
		Период.ДатаНачала		= ДобавитьМесяц(Период.ДатаНачала, 12 * НаправлениеСдвига);
		Период.ДатаОкончания	= КонецГода(Период.ДатаНачала);
	ИначеЕсли Период.ДатаНачала = НачалоГода(Период.ДатаОкончания) И (Месяц(Период.ДатаОкончания) = 6 Или Месяц(Период.ДатаОкончания) = 9) Тогда
		Период.ДатаНачала		= ДобавитьМесяц(Период.ДатаНачала, 12 * НаправлениеСдвига);
		Период.ДатаОкончания	= ДобавитьМесяц(Период.ДатаОкончания, 12 * НаправлениеСдвига);
	КонецЕсли;

	Форма[Путь] = Период;
	О_УстановитьПериод(Форма, Путь);

	ВариантыОтчетовКлиентСервер.О_ОповеститьОИзмененииНастроек(Форма);
КонецПроцедуры

Процедура О_ВыбратьПериодЗавершение(РезультатВыбора, Контекст) Экспорт
	Если РезультатВыбора = Неопределено Тогда
		Возврат;
	КонецЕсли;

	Если ТипЗнч(РезультатВыбора) = Тип("Структура") И РезультатВыбора.Свойство("Событие") Тогда
		О_ВыбратьПериод(РезультатВыбора.ВладелецФормы, РезультатВыбора.ИмяКоманды, РезультатВыбора.ВариантПериода);

		Возврат;
	ИначеЕсли ТипЗнч(РезультатВыбора) = Тип("Структура") Тогда
		Форма	= РезультатВыбора.ВладелецФормы;
		Период	= РезультатВыбора.Период;
	Иначе
		Форма	= Контекст.Форма;
		Период	= РезультатВыбора;
	КонецЕсли;

	Форма[Контекст.Путь] = Период;
	О_УстановитьПериод(Форма, Контекст.Путь);

	ВариантыОтчетовКлиентСервер.О_ОповеститьОИзмененииНастроек(Форма);
КонецПроцедуры

Процедура О_УстановитьПериод(Форма, Знач Путь) Экспорт
	КомпоновщикНастроек = Форма.Отчет.КомпоновщикНастроек;

	Свойства = СтрРазделить("ДатаНачала, ДатаОкончания", ", ", Ложь);
	Для Каждого Свойство Из Свойства Цикл
		Путь = СтрЗаменить(Путь, Свойство, "");
	КонецЦикла;

	Индекс = Форма.ПутьКДаннымЭлементов.ПоИмени[Путь];
	Если Индекс = Неопределено Тогда
		Путь	= Путь + "Период";
		Индекс	= Форма.ПутьКДаннымЭлементов.ПоИмени[Путь];
	КонецЕсли;

	Период = Форма[Путь];

	ЭлементПользовательскойНастройки				= КомпоновщикНастроек.ПользовательскиеНастройки.Элементы[Индекс];
	ЭлементПользовательскойНастройки.Использование	= Истина;

	Если ТипЗнч(ЭлементПользовательскойНастройки) = Тип("ЗначениеПараметраНастроекКомпоновкиДанных") Тогда
		ЭлементПользовательскойНастройки.Значение = Период;
	Иначе // Элемент отбора.
		ЭлементПользовательскойНастройки.ПравоеЗначение = Период;
	КонецЕсли;

	ИмяКнопкиВыбораПериода			= СтрЗаменить(Путь, "Период", "ВыбратьПериод");
	КнопкаВыбораПериода				= Форма.Элементы.Найти(ИмяКнопкиВыбораПериода);
	КнопкаВыбораПериода.Заголовок	= БазоваяПодсистемаКлиентСервер.сОН_ПредставлениеПериодаВТексте(Период.ДатаНачала, Период.ДатаОкончания, "", Ложь);

	ВариантыОтчетовКлиентСервер.О_ОповеститьОИзмененииНастроек(Форма);
КонецПроцедуры

Функция О_ТипЗначенияОграниченныйСвязьюПоТипу(Настройки, ПользовательскиеНастройки, ЭлементНастройки, ОписаниеЭлементаНастройки, ТипЗначения = Неопределено) Экспорт
	Если ОписаниеЭлементаНастройки = Неопределено Тогда
		Возврат ?(ТипЗначения = Неопределено, Новый ОписаниеТипов("Неопределено"), ТипЗначения);
	КонецЕсли;

	Если ТипЗначения = Неопределено Тогда
		ТипЗначения = ОписаниеЭлементаНастройки.ТипЗначения;
	КонецЕсли;

	СвязьПоТипу = ОписаниеЭлементаНастройки.СвязьПоТипу;

	СвязанныйЭлементНастройки = О_ЭлементНастройкиПоПолю(Настройки, ПользовательскиеНастройки, СвязьПоТипу.Поле);
	Если СвязанныйЭлементНастройки = Неопределено Тогда
		Возврат ТипЗначения;
	КонецЕсли;

	ДопустимыеВидыСравнения = Новый Массив;
	ДопустимыеВидыСравнения.Добавить(ВидСравненияКомпоновкиДанных.Равно);
	ДопустимыеВидыСравнения.Добавить(ВидСравненияКомпоновкиДанных.ВИерархии);

	Если ТипЗнч(СвязанныйЭлементНастройки) = Тип("ЭлементОтбораКомпоновкиДанных")
		И (Не СвязанныйЭлементНастройки.Использование
		Или ДопустимыеВидыСравнения.Найти(СвязанныйЭлементНастройки.ВидСравнения) = Неопределено) Тогда
		Возврат ТипЗначения;
	КонецЕсли;

	ОписаниеСвязанногоЭлементаНастройки = ВариантыОтчетовКлиентСервер.О_НайтиДоступнуюНастройку(Настройки, СвязанныйЭлементНастройки);
	Если ОписаниеСвязанногоЭлементаНастройки = Неопределено Тогда
		Возврат ТипЗначения;
	КонецЕсли;

	Если ТипЗнч(СвязанныйЭлементНастройки) = Тип("ЗначениеПараметраНастроекКомпоновкиДанных") И (ОписаниеСвязанногоЭлементаНастройки.Использование <> ИспользованиеПараметраКомпоновкиДанных.Всегда Или Не СвязанныйЭлементНастройки.Использование) Тогда
		Возврат ТипЗначения;
	КонецЕсли;

	Если ТипЗнч(СвязанныйЭлементНастройки) = Тип("ЗначениеПараметраНастроекКомпоновкиДанных") Тогда
		ЗначениеСвязанногоЭлементаНастройки = СвязанныйЭлементНастройки.Значение;
	ИначеЕсли ТипЗнч(СвязанныйЭлементНастройки) = Тип("ЭлементОтбораКомпоновкиДанных") Тогда
		ЗначениеСвязанногоЭлементаНастройки = СвязанныйЭлементНастройки.ПравоеЗначение;
	КонецЕсли;

	ТипСубконто = ВариантыОтчетовВызовСервера.ВО_ТипСубконто(ЗначениеСвязанногоЭлементаНастройки, СвязьПоТипу.ЭлементСвязи);
	Если ТипЗнч(ТипСубконто) = Тип("ОписаниеТипов") Тогда
		СвязанныеТипы = ТипСубконто.Типы();
	Иначе
		СвязанныеТипы = ОписаниеСвязанногоЭлементаНастройки.ТипЗначения.Типы();
	КонецЕсли;

	ВычитаемыеТипы	= ТипЗначения.Типы();
	Индекс			= ВычитаемыеТипы.ВГраница();
	Пока Индекс >= 0 Цикл
		Если СвязанныеТипы.Найти(ВычитаемыеТипы[Индекс]) <> Неопределено Тогда
			ВычитаемыеТипы.Удалить(Индекс);
		КонецЕсли;
		Индекс = Индекс - 1;
	КонецЦикла;

	Возврат Новый ОписаниеТипов(ТипЗначения,, ВычитаемыеТипы);
КонецФункции

Функция О_ЭлементНастройкиПоПолю(Настройки, ПользовательскиеНастройки, Поле)
	ЭлементНастройки = О_ЭлементПараметровДанныхПоПолю(Настройки, ПользовательскиеНастройки, Поле);

	Если ЭлементНастройки = Неопределено Тогда
		О_НайтиЭлементОтбораПоПолю(Поле, Настройки.Отбор.Элементы, ПользовательскиеНастройки, ЭлементНастройки);
	КонецЕсли;

	Возврат ЭлементНастройки;
КонецФункции

Функция О_ЭлементПараметровДанныхПоПолю(Настройки, ПользовательскиеНастройки, Поле)
	Если ТипЗнч(Настройки) <> Тип("НастройкиКомпоновкиДанных") Тогда
		Возврат Неопределено;
	КонецЕсли;

	ЭлементыНастроек = Настройки.ПараметрыДанных.Элементы;
	Для Каждого Элемент Из ЭлементыНастроек Цикл
		ЭлементПользовательский	= ПользовательскиеНастройки.Найти(Элемент.ИдентификаторПользовательскойНастройки);
		ЭлементАнализируемый	= ?(ЭлементПользовательский = Неопределено, Элемент, ЭлементПользовательский);

		Поля = Новый Массив;
		Поля.Добавить(Новый ПолеКомпоновкиДанных(Строка(Элемент.Параметр)));
		Поля.Добавить(Новый ПолеКомпоновкиДанных("ПараметрыДанных." + Строка(Элемент.Параметр)));

		Если ЭлементАнализируемый.Использование И (Поля[0] = Поле Или Поля[1] = Поле) Тогда
			Возврат ЭлементАнализируемый;
		КонецЕсли;
	КонецЦикла;

	Возврат Неопределено;
КонецФункции

Процедура О_НайтиЭлементОтбораПоПолю(Поле, ЭлементыОтбора, ПользовательскиеНастройки, ЭлементНастройки)
	Для Каждого Элемент Из ЭлементыОтбора Цикл
		Если ТипЗнч(Элемент) = Тип("ГруппаЭлементовОтбораКомпоновкиДанных") Тогда
			О_НайтиЭлементОтбораПоПолю(Поле, Элемент.Элементы, ПользовательскиеНастройки, ЭлементНастройки)
		Иначе
			ЭлементПользовательский	= ПользовательскиеНастройки.Найти(Элемент.ИдентификаторПользовательскойНастройки);
			ЭлементАнализируемый	= ?(ЭлементПользовательский = Неопределено, Элемент, ЭлементПользовательский);

			Если ЭлементАнализируемый.Использование И Элемент.ЛевоеЗначение = Поле Тогда
				ЭлементНастройки = ЭлементАнализируемый;

				Прервать;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

Функция О_СведенияОЭлементеНастройки(КомпоновщикНастроек, Идентификатор) Экспорт
	Настройки					= КомпоновщикНастроек.Настройки;
	ПользовательскиеНастройки	= КомпоновщикНастроек.ПользовательскиеНастройки;

	Если ТипЗнч(Идентификатор) = Тип("Число") Тогда
		Индекс = Идентификатор;
	Иначе
		Индекс = ВариантыОтчетовКлиентСервер.О_ИндексЭлементаНастройкиПоПути(Идентификатор);
	КонецЕсли;

	ЭлементПользовательскойНастройки = ПользовательскиеНастройки.Элементы[Индекс];

	ИерархияНастроек	= Новый Массив;
	Элемент				= ВариантыОтчетовКлиентСервер.О_ПолучитьОбъектПоПользовательскомуИдентификатору(Настройки, ЭлементПользовательскойНастройки.ИдентификаторПользовательскойНастройки, ИерархияНастроек, ПользовательскиеНастройки);

	Настройки	= ?(ИерархияНастроек.Количество() > 0, ИерархияНастроек[ИерархияНастроек.ВГраница()], Настройки);
	Описание	= ВариантыОтчетовКлиентСервер.О_НайтиДоступнуюНастройку(Настройки, Элемент);

	Сведения = Новый Структура;
	Сведения.Вставить("Настройки",							Настройки);
	Сведения.Вставить("Индекс",								Индекс);
	Сведения.Вставить("ЭлементПользовательскойНастройки",	ЭлементПользовательскойНастройки);
	Сведения.Вставить("Элемент",							Элемент);
	Сведения.Вставить("Описание",							Описание);

	Возврат Сведения;
КонецФункции

Функция О_ЭтоВыборОбъектовМетаданных(ДоступныеТипы, Знач ОтмеченныеЗначения, Обработчик) Экспорт
	СоставТипов = ДоступныеТипы.Типы();

	Индекс = СоставТипов.ВГраница();
	Пока Индекс >= 0 Цикл
		Если СоставТипов.Найти(Тип("СправочникСсылка.ИдентификаторыОбъектовМетаданных")) <> Неопределено Или СоставТипов.Найти(Тип("СправочникСсылка.ИдентификаторыОбъектовРасширений")) <> Неопределено Тогда
			СоставТипов.Удалить(Индекс);
		КонецЕсли;

		Индекс = Индекс - 1;
	КонецЦикла;

	ЭтоВыборОбъектовМетаданных = (СоставТипов.Количество() = 0);

	Если ЭтоВыборОбъектовМетаданных Тогда
		О_ПроверитьОтмеченныеЗначения(ОтмеченныеЗначения);

		ПараметрыПодбора = Новый Структура;
		ПараметрыПодбора.Вставить("ВыбранныеОбъектыМетаданных",	ОтмеченныеЗначения);
		ПараметрыПодбора.Вставить("ВыбиратьСсылки",				Истина);
		ПараметрыПодбора.Вставить("Заголовок",					"Подбор таблиц");

		ОткрытьФорму("ОбщаяФорма.ВыборОбъектовМетаданных", ПараметрыПодбора,,,,, Обработчик);
	КонецЕсли;

	Возврат ЭтоВыборОбъектовМетаданных;
КонецФункции

Функция О_ЭтоВыборПользователей(ДоступныеТипы, Знач ОтмеченныеЗначения, ПараметрыВыбора, Обработчик) Экспорт
	КоличествоТипов = ДоступныеТипы.Типы().Количество();

	ЭтоВыборПользователей = КоличествоТипов = 1 И ДоступныеТипы.СодержитТип(Тип("СправочникСсылка.Пользователи")) Или КоличествоТипов = 2 И ДоступныеТипы.СодержитТип(Тип("СправочникСсылка.Пользователи")) И ДоступныеТипы.СодержитТип(Тип("СправочникСсылка.ГруппыПользователей"));

	Если ЭтоВыборПользователей Тогда
		О_ПроверитьОтмеченныеЗначения(ОтмеченныеЗначения);
		О_ВыбратьПользователей(ДоступныеТипы, ОтмеченныеЗначения, ПараметрыВыбора, Обработчик);
	КонецЕсли;

	Возврат ЭтоВыборПользователей;
КонецФункции

Процедура О_ПроверитьОтмеченныеЗначения(ОтмеченныеЗначения)
	Индекс = ОтмеченныеЗначения.Количество() - 1;

	Пока Индекс >= 0 Цикл
		Элемент	= ОтмеченныеЗначения[Индекс];
		Индекс	= Индекс - 1;

		Если Не ЗначениеЗаполнено(Элемент.Значение) Тогда
			ОтмеченныеЗначения.Удалить(Элемент);
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

Процедура О_ВыбратьПользователей(ДоступныеТипы, ОтмеченныеЗначения, ПараметрыВыбора, Обработчик)
	ВыборГруппПользователей = ДоступныеТипы.СодержитТип(Тип("СправочникСсылка.ГруппыПользователей"));
	ВыбранныеПользователи = ОтмеченныеЗначения.ВыгрузитьЗначения();

	Если ДоступныеТипы.СодержитТип(Тип("СправочникСсылка.ГруппыПользователей")) Тогда
		ЗаголовокФормыПодбора = "Подбор групп и пользователей";
	Иначе
		ЗаголовокФормыПодбора = "Подбор пользователей";
	КонецЕсли;

	ПараметрыПодбора = Новый Структура;
	ПараметрыПодбора.Вставить("РежимВыбора",				Истина);
	ПараметрыПодбора.Вставить("ЗакрыватьПриВыборе",			Ложь);
	ПараметрыПодбора.Вставить("МножественныйВыбор",			Истина);
	ПараметрыПодбора.Вставить("РасширенныйПодбор",			Истина);
	ПараметрыПодбора.Вставить("ВыборГруппПользователей",	ВыборГруппПользователей);
	ПараметрыПодбора.Вставить("ПараметрыВыбора",			ПараметрыВыбора);
	ПараметрыПодбора.Вставить("ЗаголовокФормыПодбора",		ЗаголовокФормыПодбора);
	ПараметрыПодбора.Вставить("ВыбранныеПользователи",		ВыбранныеПользователи);

	ОткрытьФорму("Справочник.Пользователи.ФормаВыбора", ПараметрыПодбора, ЭтотОбъект,,,, Обработчик);
КонецПроцедуры

Функция О_ПриДобавленииВКоллекциюНужноУказыватьТипЭлемента(ТипКоллекции) Экспорт
	Возврат ТипКоллекции <> Тип("КоллекцияЭлементовСтруктурыТаблицыКомпоновкиДанных")
		И ТипКоллекции <> Тип("КоллекцияЭлементовСтруктурыДиаграммыКомпоновкиДанных")
		И ТипКоллекции <> Тип("КоллекцияЭлементовУсловногоОформленияКомпоновкиДанных");
КонецФункции

Функция О_КэшЗначенияОтбора(КомпоновщикНастроек, ЭлементОтбора) Экспорт
	ЗначениеОтбора = Неопределено;

	ДополнительныеСвойства = КомпоновщикНастроек.ПользовательскиеНастройки.ДополнительныеСвойства;

	КэшЗначенийОтборов = БазоваяПодсистемаКлиентСервер.ОН_СвойствоСтруктуры(ДополнительныеСвойства, "КэшЗначенийОтборов", Новый Соответствие);

	Если ЗначениеЗаполнено(ЭлементОтбора.ИдентификаторПользовательскойНастройки) Тогда
		ЗначениеОтбора = КэшЗначенийОтборов[ЭлементОтбора.ИдентификаторПользовательскойНастройки];
	КонецЕсли;

	Если ЗначениеОтбора <> Неопределено Тогда
		Возврат ЗначениеОтбора;
	КонецЕсли;

	ЭлементОтбораОсновныхНастроек = Неопределено;

	Если ЗначениеЗаполнено(ЭлементОтбора.ИдентификаторПользовательскойНастройки) Тогда
		НайденныеЭлементыНастроек = КомпоновщикНастроек.ПользовательскиеНастройки.ПолучитьОсновныеНастройкиПоИдентификаторуПользовательскойНастройки(ЭлементОтбора.ИдентификаторПользовательскойНастройки);

		Если НайденныеЭлементыНастроек.Количество() > 0 Тогда
			ЭлементОтбораОсновныхНастроек = НайденныеЭлементыНастроек[0];
		КонецЕсли;
	Иначе
		ЭлементОтбораОсновныхНастроек = ЭлементОтбора;
	КонецЕсли;

	Если ТипЗнч(ЭлементОтбораОсновныхНастроек) = Тип("ЭлементОтбораКомпоновкиДанных") Тогда
		ЗначениеОтбора = КэшЗначенийОтборов[ЭлементОтбораОсновныхНастроек.ЛевоеЗначение];
	КонецЕсли;

	Возврат ЗначениеОтбора;
КонецФункции

#КонецОбласти

#Область КонструкторФормул

Процедура КФ_НачатьРедактированиеФормулы(Параметры, ОбработчикЗавершения) Экспорт
	ОткрытьФорму("Обработка.КонструкторФормул.Форма.РедактированиеФормулы", Параметры, , , , , ОбработчикЗавершения);
КонецПроцедуры

Процедура КФ_СписокПолейПередРазворачиванием(Форма, Элемент, Строка, Отказ) Экспорт
	Элемент.ТекущаяСтрока	= Строка;
	КоллекцияЭлементов		= Форма[Элемент.Имя].НайтиПоИдентификатору(Строка).ПолучитьЭлементы();
	Если КоллекцияЭлементов.Количество() > 0 И КоллекцияЭлементов[0].Поле = Неопределено Тогда
		Отказ = Истина;
		Форма.ПодключитьОбработчикОжидания("Подключаемый_РазвернутьТекущийЭлементСпискаПолей", 0.1, Истина);
	КонецЕсли;
КонецПроцедуры

Процедура КФ_РазвернутьТекущийЭлементСпискаПолей(Форма) Экспорт
	СписокПолей			= Форма.ТекущийЭлемент;
	ИдентификаторСтроки	= СписокПолей.ТекущаяСтрока;

	ПараметрыЗаполнения = Новый Структура;
	ПараметрыЗаполнения.Вставить("ИдентификаторСтроки",	ИдентификаторСтроки);
	ПараметрыЗаполнения.Вставить("ИмяСписка",			СписокПолей.Имя);

	Форма.Подключаемый_ЗаполнитьСписокДоступныхПолей(ПараметрыЗаполнения);
	СписокПолей.Развернуть(ИдентификаторСтроки);
КонецПроцедуры

Процедура КФ_СписокПолейНачалоПеретаскивания(Форма, Элемент, ПараметрыПеретаскивания, Выполнение) Экспорт
	Реквизит							= Форма[Элемент.Имя].НайтиПоИдентификатору(ПараметрыПеретаскивания.Значение);
	ПараметрыПеретаскивания.Значение	= "[" + Реквизит.ПредставлениеПутиКДанным + "]";
КонецПроцедуры

Функция КФ_ВыбранноеПолеВСпискеПолей(Форма, ИмяСпискаПолей = Неопределено) Экспорт
	Если Форма.ПодключенныеСпискиПолей.Количество() = 0 Тогда
		Возврат Неопределено;
	КонецЕсли;

	Если ИмяСпискаПолей = Неопределено Тогда
		СписокПолей		= Форма.ТекущийЭлемент; // ТаблицаФормы
		ИмяСпискаПолей	= СписокПолей.Имя;
		Если ВариантыОтчетовКлиентСервер.КФ_НастройкиСпискаПолей(Форма, ИмяСпискаПолей)= Неопределено Тогда
			ИмяСпискаПолей = Форма.ПодключенныеСпискиПолей[0].ИмяСпискаПолей;
		КонецЕсли;
	КонецЕсли;

	СписокПолей		= Форма.Элементы[ИмяСпискаПолей];
	ТекущиеДанные	= СписокПолей.ТекущиеДанные;

	Если ТекущиеДанные = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;

	Возврат КФ_ОписаниеВыбранногоПоля(ТекущиеДанные);
КонецФункции

Процедура КФ_СтрокаПоискаИзменениеТекстаРедактирования(Форма, Элемент, Текст, СтандартнаяОбработка) Экспорт
	Форма[Элемент.Имя] = Текст;
	Форма.ОтключитьОбработчикОжидания("Подключаемый_ВыполнитьПоискВСпискеПолей");
	Форма.ПодключитьОбработчикОжидания("Подключаемый_ВыполнитьПоискВСпискеПолей", 0.5, Истина);
КонецПроцедуры

Функция КФ_ОписаниеВыбранногоПоля(Поле)
	Результат = Новый Структура;
	Результат.Вставить("Имя");
	Результат.Вставить("Заголовок");
	Результат.Вставить("ПутьКДанным");
	Результат.Вставить("ПредставлениеПутиКДанным");
	Результат.Вставить("Тип");
	Результат.Вставить("Родитель");

	ЗаполнитьЗначенияСвойств(Результат, Поле);

	Родитель = Поле.ПолучитьРодителя();
	Если Родитель <> Неопределено Тогда
		Результат.Родитель = КФ_ОписаниеВыбранногоПоля(Родитель);
	КонецЕсли;

	Возврат Результат;
КонецФункции

#КонецОбласти
