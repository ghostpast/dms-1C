///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2020, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	Если Не СтрНайти(ПараметрЗапуска, "ОтключитьЛогикуНачалаРаботыСистемы") > 0 Тогда
		Возврат;
	КонецЕсли;

	Элементы.ТестовыйРежим.Видимость = Истина;

	ЗаголовокТестовогоРежима = "{Тестирование} ";
	ТекущийЗаголовок = КлиентскоеПриложение.ПолучитьЗаголовок();

	Если СтрНачинаетсяС(ТекущийЗаголовок, ЗаголовокТестовогоРежима) Тогда
		Возврат;
	КонецЕсли;

	КлиентскоеПриложение.УстановитьЗаголовок(ЗаголовокТестовогоРежима + ТекущийЗаголовок);

	ЗарегистрироватьОтключениеЛогикиНачалаРаботыСистемы();
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ЗарегистрироватьОтключениеЛогикиНачалаРаботыСистемы()
	УстановитьПривилегированныйРежим(Истина);

	ВладелецДанных = Справочники.ИдентификаторыОбъектовМетаданных.ПолучитьСсылку(
		Новый УникальныйИдентификатор("627a6fb8-872a-11e3-bb87-005056c00008")); // Константы.

	ДатыОтключения = БазоваяПодсистемаСервер.ОН_ПрочитатьДанныеИзБезопасногоХранилища(ВладелецДанных);
	Если ТипЗнч(ДатыОтключения) <> Тип("Массив") Тогда
		ДатыОтключения = Новый Массив;
	КонецЕсли;

	ДатыОтключения.Добавить(ТекущаяДатаСеанса());
	БазоваяПодсистемаСервер.ОН_ЗаписатьДанныеВБезопасноеХранилище(ВладелецДанных, ДатыОтключения);
КонецПроцедуры
