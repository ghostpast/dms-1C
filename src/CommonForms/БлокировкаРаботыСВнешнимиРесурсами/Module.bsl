///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2022, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	ИмяСобытияЖурналаРегистрации = "Работа с внешними ресурсами заблокирована";

	ПараметрыБлокировки	= РегламентныеЗаданияСервер.БРСВР_СохраненныеПараметрыБлокировки();
	ПроверятьИмяСервера	= ПараметрыБлокировки.ПроверятьИмяСервера;

	Если Параметры.ПринятиеРешенияОБлокировке Тогда
		ТекстСнятияБлокировки	= РегламентныеЗаданияСервер.сРЗ_ЗначениеНастройки("РасположениеКомандыСнятияБлокировки");

		УточнениеМасштабируемыйКластер = ?(БазоваяПодсистемаСервер.ОН_ИнформационнаяБазаФайловая(), "",
				"• При работе в масштабируемом кластере для предотвращения ложных срабатываний из-за смены компьютеров, выступающих
				           |  в роли рабочих серверов, отключите проверку имени компьютера, нажмите <b>Еще - Проверять имя сервера.</b>");

		НадписьПредупреждение = СтрШаблон("Работа со всеми внешними ресурсами (синхронизация данных, отправка почты и т.п.), выполняемая по расписанию,
				           |заблокирована для предотвращения конфликтов с основой информационной базой.
				           |
				           |%1
				           |
				           |<a href = ""%2"">Техническая информация о причине блокировки</a>
				           |
				           |• Если информационная база будет использоваться для ведения учета, нажмите <b>Информационная база перемещена</b>.
				           |• Если это копия информационной базы, нажмите <b>Это копия информационной базы</b>.
				           |%3
				           |
				           |%4",
			ПараметрыБлокировки.ПричинаБлокировки,
			"ЖурналРегистрации",
			УточнениеМасштабируемыйКластер,
			ТекстСнятияБлокировки);

		Элементы.НадписьПредупреждение.Заголовок = БазоваяПодсистемаСервер.СФ_ФорматированнаяСтрока(НадписьПредупреждение);

		Если БазоваяПодсистемаСервер.ОН_ИнформационнаяБазаФайловая() Тогда
			Элементы.ФормаГруппаЕще.Видимость = Ложь;
		Иначе
			Элементы.ФормаПроверятьИмяСервера.Пометка	= ПроверятьИмяСервера;
			Элементы.ФормаСправка.Видимость				= Ложь;
		КонецЕсли;
	Иначе
		Элементы.ГруппаПараметрыФормы.ТекущаяСтраница	= Элементы.ГруппаПараметрыБлокировки;
		Элементы.НадписьПредупреждение.Видимость		= Ложь;
		Элементы.ЗаписатьИЗакрыть.КнопкаПоУмолчанию		= Истина;
		Заголовок										= "Параметры блокировки работы с внешними ресурсами";
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура НадписьПредупреждениеОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;

	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("СобытиеЖурналаРегистрации", ИмяСобытияЖурналаРегистрации);
	ОткрытьФорму("Обработка.ЖурналРегистрации.Форма.ЖурналРегистрации", ПараметрыФормы);
КонецПроцедуры

&НаКлиенте
Процедура ИнформационнаяБазаПеремещена(Команда)
	РазрешитьРаботуСВнешнимиРесурсами();
	БазоваяПодсистемаКлиент.СП_УстановитьРасширенныйЗаголовокПриложения();
	ОбновитьИнтерфейс();
	Закрыть();
КонецПроцедуры

&НаКлиенте
Процедура ЭтоКопияИнформационнойБазы(Команда)
	ЗапретитьРаботуСВнешнимиРесурсами();
	БазоваяПодсистемаКлиент.СП_УстановитьРасширенныйЗаголовокПриложения();
	ОбновитьИнтерфейс();
	Закрыть();
КонецПроцедуры

&НаКлиенте
Процедура ПроверятьИмяСервера(Команда)
	ПроверятьИмяСервера							= Не ПроверятьИмяСервера;
	Элементы.ФормаПроверятьИмяСервера.Пометка	= ПроверятьИмяСервера;
	УстановитьПроверкуИмениСервераВПараметрыБлокировки(ПроверятьИмяСервера);
КонецПроцедуры

&НаКлиенте
Процедура ЗаписатьИЗакрыть(Команда)
	УстановитьПроверкуИмениСервераВПараметрыБлокировки(ПроверятьИмяСервера);
	Закрыть();
КонецПроцедуры

&НаСервереБезКонтекста
Процедура РазрешитьРаботуСВнешнимиРесурсами()
	РегламентныеЗаданияСервер.БРСВР_РазрешитьРаботуСВнешнимиРесурсами();
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ЗапретитьРаботуСВнешнимиРесурсами()
	РегламентныеЗаданияСервер.БРСВР_ЗапретитьРаботуСВнешнимиРесурсами();
КонецПроцедуры

&НаСервереБезКонтекста
Процедура УстановитьПроверкуИмениСервераВПараметрыБлокировки(ПроверятьИмяСервера)
	РегламентныеЗаданияСервер.БРСВР_УстановитьПроверкуИмениСервераВПараметрыБлокировки(ПроверятьИмяСервера);
КонецПроцедуры
