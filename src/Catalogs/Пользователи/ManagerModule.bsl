///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2021, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

Процедура ОбработкаПолученияДанныхВыбора(ДанныеВыбора, Параметры, СтандартнаяОбработка)
	Если НЕ Параметры.Отбор.Свойство("Недействителен") Тогда
		Параметры.Отбор.Вставить("Недействителен", Ложь);
	КонецЕсли;

	Если НЕ Параметры.Отбор.Свойство("Служебный") Тогда
		Параметры.Отбор.Вставить("Служебный", Ложь);
	КонецЕсли;
КонецПроцедуры

Процедура ЗарегистрироватьДанныеКОбработкеДляПереходаНаНовуюВерсию(Параметры) Экспорт
	СписокПользователей = ПользователиСервер.сП_ПользователиДляВключенияВосстановленияПароля();

	Если СписокПользователей.Количество() > 0 Тогда
		ВключитьСтандартныеНастройкиВосстановленияПароля();
		ОбновлениеВерсииИБСервер.ОИБ_ОтметитьКОбработке(Параметры, СписокПользователей);
	КонецЕсли;
КонецПроцедуры

Процедура ОбработатьДанныеДляПереходаНаНовуюВерсию(Параметры) Экспорт
	ПользовательСсылка = ОбновлениеВерсииИБСервер.ОИБ_ВыбратьСсылкиДляОбработки(Параметры.Очередь, "Справочник.Пользователи");

	ПроблемныхОбъектов	= 0;
	ОбъектовОбработано	= 0;
	СписокОшибок		= Новый Массив;

	Пока ПользовательСсылка.Следующий() Цикл
		Результат = ПользователиСервер.сП_ОбновитьПочтуДляВосстановленияПароля(ПользовательСсылка.Ссылка);

		Если Результат.Статус = "Ошибка" Тогда
			ПроблемныхОбъектов = ПроблемныхОбъектов + 1;
			СписокОшибок.Добавить(Результат.ТекстОшибки);
		Иначе
			ОбъектовОбработано = ОбъектовОбработано + 1;
			ОбновлениеВерсииИБСервер.ОИБ_ОтметитьВыполнениеОбработки(ПользовательСсылка.Ссылка);
		КонецЕсли;
	КонецЦикла;

	Параметры.ОбработкаЗавершена = НЕ ОбновлениеВерсииИБСервер.ОИБ_ЕстьДанныеДляОбработки(Параметры.Очередь, "Справочник.Пользователи", Неопределено);

	Если ОбъектовОбработано = 0 И ПроблемныхОбъектов <> 0 Тогда
		ТекстСообщения = СтрШаблон("Процедуре ОбработатьДанныеДляПереходаНаНовуюВерсию не удалось обработать некоторых пользователей (пропущены): %1
			|%2", ПроблемныхОбъектов, СтрСоединить(СписокОшибок, Символы.ПС));

		ВызватьИсключение ТекстСообщения;
	Иначе
		ЗаписьЖурналаРегистрации("Обновление информационной базы", УровеньЖурналаРегистрации.Информация, Метаданные.Справочники.Пользователи,, СтрШаблон("Процедура ОбработатьДанныеДляПереходаНаНовуюВерсию обработала очередную порцию пользователей: %1", ОбъектовОбработано));
	КонецЕсли;
КонецПроцедуры

Процедура ВключитьСтандартныеНастройкиВосстановленияПароля()
	Настройки = ПользователиСервер.сП_НастройкиВосстановленияПароля();

	Если Настройки = Неопределено Тогда
		Возврат;
	КонецЕсли;

	Настройки.СпособВосстановленияПароля = ПользователиСервер.сП_СпособВосстановленияПароляПользователяИнформационнойБазы("ОтправкаКодаПодтвержденияЧерезСтандартныйСервис");
	ПользователиСервер.сП_УстановитьНастройкиВосстановленияПароля(Настройки);
КонецПроцедуры

#КонецЕсли
