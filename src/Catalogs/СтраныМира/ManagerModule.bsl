///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2021, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

Процедура ПриНастройкеНачальногоЗаполненияЭлементов(Настройки) Экспорт
	Настройки.ПриНачальномЗаполненииЭлемента = Ложь;
КонецПроцедуры

Процедура ПриНачальномЗаполненииЭлементов(КодыЯзыков, Элементы, ТабличныеЧасти) Экспорт
	КонтактнаяИнформацияСервер.РСА_ПриНачальномЗаполненииЭлементов(КодыЯзыков, Элементы, ТабличныеЧасти);
КонецПроцедуры

Процедура ЗарегистрироватьДанныеКОбработкеДляПереходаНаНовуюВерсию(Параметры) Экспорт
	СписокСтран = КонтактнаяИнформацияСервер.УКИ_ПользовательскиеСтраныЕАЭС();

	НоваяСтрока                    = СписокСтран.Добавить();
	НоваяСтрока.Код                = "203";
	НоваяСтрока.Наименование       = "ЧЕШСКАЯ РЕСПУБЛИКА";
	НоваяСтрока.КодАльфа2          = "CZ";
	НоваяСтрока.КодАльфа3          = "CZE";

	НоваяСтрока                    = СписокСтран.Добавить();
	НоваяСтрока.Код                = "270";
	НоваяСтрока.Наименование       = "ГАМБИЯ";
	НоваяСтрока.КодАльфа2          = "GM";
	НоваяСтрока.КодАльфа3          = "GMB";
	НоваяСтрока.НаименованиеПолное = "Республика Гамбия";

	НоваяСтрока                    = СписокСтран.Добавить();
	НоваяСтрока.Код                = "807";
	НоваяСтрока.Наименование       = "РЕСПУБЛИКА МАКЕДОНИЯ";
	НоваяСтрока.КодАльфа2          = "MK";
	НоваяСтрока.КодАльфа3          = "MKD";
	НоваяСтрока.НаименованиеПолное = "Республика Македония";

	Запрос			= Новый Запрос;
	Запрос.Текст	= "ВЫБРАТЬ
		|	СписокСтран.Код КАК Код,
		|	СписокСтран.Наименование КАК Наименование,
		|	СписокСтран.КодАльфа2 КАК КодАльфа2,
		|	СписокСтран.КодАльфа3 КАК КодАльфа3,
		|	СписокСтран.НаименованиеПолное КАК НаименованиеПолное
		|ПОМЕСТИТЬ СписокСтран
		|ИЗ
		|	&СписокСтран КАК СписокСтран
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	СтраныМира.Ссылка КАК Ссылка
		|ИЗ
		|	СписокСтран КАК СписокСтран
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.СтраныМира КАК СтраныМира
		|		ПО (СтраныМира.Код = СписокСтран.Код)
		|			И (СтраныМира.Наименование = СписокСтран.Наименование)
		|			И (СтраныМира.КодАльфа2 = СписокСтран.КодАльфа2)
		|			И (СтраныМира.КодАльфа3 = СписокСтран.КодАльфа3)
		|			И (СтраныМира.НаименованиеПолное = СписокСтран.НаименованиеПолное)";

	Запрос.УстановитьПараметр("СписокСтран", СписокСтран);
	РезультатЗапроса = Запрос.Выполнить().Выгрузить();
	СтраныКОбработке = РезультатЗапроса.ВыгрузитьКолонку("Ссылка");

	ОбновлениеВерсииИБСервер.ОИБ_ОтметитьКОбработке(Параметры, СтраныКОбработке);
КонецПроцедуры

Процедура ОбработатьДанныеДляПереходаНаНовуюВерсию(Параметры) Экспорт
	СтранаМираСсылка	= ОбновлениеВерсииИБСервер.ОИБ_ВыбратьСсылкиДляОбработки(Параметры.Очередь, "Справочник.СтраныМира");

	ПроблемныхОбъектов = 0;
	ОбъектовОбработано = 0;

	Пока СтранаМираСсылка.Следующий() Цикл
		Попытка
			ДанныеКлассификатора = КонтактнаяИнформацияСервер.УКИ_ДанныеКлассификатораСтранМираПоКоду(СтранаМираСсылка.Ссылка.Код);

			Если ДанныеКлассификатора <> Неопределено Тогда
				Блокировка			= Новый БлокировкаДанных;
				ЭлементБлокировки	= Блокировка.Добавить("Справочник.СтраныМира");
				ЭлементБлокировки.УстановитьЗначение("Ссылка", СтранаМираСсылка.Ссылка);

				НачатьТранзакцию();
				Попытка
					Блокировка.Заблокировать();

					СтранаМира = СтранаМираСсылка.Ссылка.ПолучитьОбъект();
					ЗаполнитьЗначенияСвойств(СтранаМира, ДанныеКлассификатора);
					ОбновлениеВерсииИБСервер.ОИБ_ЗаписатьДанные(СтранаМира);

					ЗафиксироватьТранзакцию();
				Исключение
					ОтменитьТранзакцию();

					ВызватьИсключение;
				КонецПопытки;
			Иначе
				ОбновлениеВерсииИБСервер.ОИБ_ОтметитьВыполнениеОбработки(СтранаМираСсылка.Ссылка);
			КонецЕсли;

			ОбъектовОбработано = ОбъектовОбработано + 1;
		Исключение
			// Если не удалось обработать страну мира, повторяем попытку снова.
			ПроблемныхОбъектов = ПроблемныхОбъектов + 1;

			ТекстСообщения = СтрШаблон("Не удалось обработать страну: %1 по причине: %2", СтранаМираСсылка.Ссылка, ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			ЗаписьЖурналаРегистрации("Обновление информационной базы", УровеньЖурналаРегистрации.Предупреждение, Метаданные.Справочники.СтраныМира, СтранаМираСсылка.Ссылка, ТекстСообщения);
		КонецПопытки;
	КонецЦикла;

	Параметры.ОбработкаЗавершена = НЕ ОбновлениеВерсииИБСервер.ОИБ_ЕстьДанныеДляОбработки(Параметры.Очередь, "Справочник.СтраныМира", Неопределено);

	Если ОбъектовОбработано = 0 И ПроблемныхОбъектов <> 0 Тогда
		ТекстСообщения = СтрШаблон("Процедуре ОбработатьДанныеДляПереходаНаНовуюВерсию не удалось обработать некоторые страны мира(пропущены): %1", ПроблемныхОбъектов);

		ВызватьИсключение ТекстСообщения;
	Иначе
		ЗаписьЖурналаРегистрации("Обновление информационной базы", УровеньЖурналаРегистрации.Информация, Метаданные.Справочники.СтраныМира,, СтрШаблон("Процедура обновления обработала очередную порцию стран мира: %1", ОбъектовОбработано));
	КонецЕсли;
КонецПроцедуры

#КонецЕсли
