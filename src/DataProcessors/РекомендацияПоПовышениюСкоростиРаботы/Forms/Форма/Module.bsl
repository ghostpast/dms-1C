///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2022, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	ОбщиеПараметры		= БазоваяПодсистемаСервер.ОН_ОбщиеПараметрыБазовойФункциональности();
	РекомендуемыйОбъем	= ОбщиеПараметры.РекомендуемыйОбъемОперативнойПамяти;
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	Отказ = Истина;

	СистемнаяИнформация	= Новый СистемнаяИнформация;
	ДоступныйОбъем		= Окр(СистемнаяИнформация.ОперативнаяПамять / 1024, 1);

	Если ДоступныйОбъем >= РекомендуемыйОбъем Тогда
		Возврат;
	КонецЕсли;

	ТекстСообщения = "На компьютере установлено %1 Гб оперативной памяти.
		|Для того чтобы программа работала быстрее,
		|рекомендуется увеличить объем памяти до %2 Гб.";

	ТекстСообщения = СтрШаблон(ТекстСообщения, ДоступныйОбъем, РекомендуемыйОбъем);

	ПараметрыВопроса			= БазоваяПодсистемаКлиент.СП_ПараметрыВопросаПользователю();
	ПараметрыВопроса.Заголовок	= "Рекомендация по повышению скорости работы";
	ПараметрыВопроса.Картинка	= БиблиотекаКартинок.Предупреждение32;
	ПараметрыВопроса.Вставить("ТекстФлажка", "Не показывать в течение двух месяцев");

	Кнопки = Новый СписокЗначений;
	Кнопки.Добавить("ПродолжитьРаботу", "Продолжить работу");

	ОписаниеОповещения = Новый ОписаниеОповещения("ПослеПоказаРекомендации", ЭтотОбъект);
	БазоваяПодсистемаКлиент.СП_ПоказатьВопросПользователю(ОписаниеОповещения, ТекстСообщения, Кнопки, ПараметрыВопроса);
КонецПроцедуры


&НаКлиенте
Процедура ПослеПоказаРекомендации(Результат, ДополнительныеПараметры) Экспорт
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;

	РекомендацияПоОбъемуОперативнойПамяти = Новый Структура;
	РекомендацияПоОбъемуОперативнойПамяти.Вставить("Показывать",			Не Результат.БольшеНеЗадаватьЭтотВопрос);
	РекомендацияПоОбъемуОперативнойПамяти.Вставить("ДатаПредыдущегоПоказа",	БазоваяПодсистемаКлиент.ОН_ДатаСеанса());


	БазоваяПодсистемаВызовСервера.ОН_ХранилищеОбщихНастроекСохранить("ОбщиеНастройкиПользователя",
		"РекомендацияПоОбъемуОперативнойПамяти", РекомендацияПоОбъемуОперативнойПамяти);
КонецПроцедуры
